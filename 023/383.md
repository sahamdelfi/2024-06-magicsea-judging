Furry Viridian Copperhead

Medium

# Potential issues with ``MasterChefRewarder.sol`` contract when the reward token is a Weird ERC20 token

## Summary
In the Readme of the contests it is explicitly stated that the protocol is expected to interact with different types of Weird ERC20 tokens. 
There are a couple of problems that will arise in the ``MasterChefRewarder.sol`` contract which inherits from the ``BaseRewarder.sol`` contract, if such a token is utilized as the reward token. 
- Token which [revert on zero value transfers](https://github.com/d-xo/weird-erc20?tab=readme-ov-file#revert-on-zero-value-transfers). The [_modify()](https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/MasterchefV2.sol#L539-L564) function of the ``MasterchefV2.sol`` contract which is called on each deposit, withdraw, or claim calls the [onModify()](https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/rewarders/MasterChefRewarder.sol#L68-L78) function in the ``MasterChefRewarder.sol`` contract which internally calls the [_update()](https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/rewarders/BaseRewarder.sol#L285-L299) and [_claim()](https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/rewarders/BaseRewarder.sol#L306-L312) functions. 
```solidity
    function onModify(address account, uint256 pid, uint256 oldBalance, uint256 newBalance, uint256 oldTotalSupply)
        public
        override(BaseRewarder, IBaseRewarder)
        returns (uint256 reward)
    {
        if (_status != Status.Linked) revert MasterChefRewarder__NotLinked();

        reward = BaseRewarder.onModify(account, pid, oldBalance, newBalance, oldTotalSupply);

        _claim(account, reward);
    }
```
When a user initially deposits some amount of tokens to the ``MasterchefV2.sol`` contract and the [_update()](https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/rewarders/BaseRewarder.sol#L285-L299) function in the ``BaseRewarder.sol`` contract is called it will return **0**:
```solidity
    function _update(address account, uint256 oldBalance, uint256 newBalance, uint256 oldTotalSupply)
        internal
        virtual
        returns (uint256 rewards)
    {
        uint256 totalUnclaimedRewards = _totalUnclaimedRewards;
        uint256 reserve = _reserve;

        uint256 totalRewards =
            oldTotalSupply == 0 ? 0 : _rewarder.getTotalRewards(_rewardsPerSecond, _endTimestamp, oldTotalSupply);
        rewards = _rewarder.update(account, oldBalance, newBalance, oldTotalSupply, totalRewards);

        _totalUnclaimedRewards = totalUnclaimedRewards + totalRewards - rewards;
        _reserve = reserve - rewards;
    }
```
Thus if the token reverts on zero value transfers users won't be able to deposit in the ``MasterchefV2.sol`` contract at all, and will lose not only the additional rewards offered by the ``MasterChefRewarder.sol`` contract instance attacked to a **pid** but also the **LUM** token from the normal rewards distributed from the  ``MasterchefV2.sol`` contract.
- Tokens with [low decimals](https://github.com/d-xo/weird-erc20?tab=readme-ov-file#low-decimals), for example if there are tokens with less than 6 decimals, a lot of precision will be lost when [_setRewardParameters()](https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/rewarders/BaseRewarder.sol#L340-L376) function is called and **maxExpectedReward > remainingReward**: 
```solidity
    function _setRewardParameters(uint256 maxRewardPerSecond, uint256 startTimestamp, uint256 expectedDuration)
        internal
        virtual
        returns (uint256 rewardPerSecond)
    {
        if (startTimestamp < block.timestamp) revert BaseRewarder__InvalidStartTimestamp(startTimestamp);
        if (_isStopped) revert BaseRewarder__Stopped();
        if (expectedDuration == 0 && maxRewardPerSecond != 0) revert BaseRewarder__InvalidDuration();

        uint256 totalUnclaimedRewards = _totalUnclaimedRewards;
        uint256 totalSupply = _getTotalSupply();

        uint256 totalRewards = _rewarder.getTotalRewards(_rewardsPerSecond, _endTimestamp, totalSupply);

        totalUnclaimedRewards += totalRewards;

        uint256 remainingReward = _balanceOfThis(_token()) - totalUnclaimedRewards;
        uint256 maxExpectedReward = maxRewardPerSecond * expectedDuration;

        rewardPerSecond = maxExpectedReward > remainingReward ? remainingReward / expectedDuration : maxRewardPerSecond;
        uint256 expectedReward = rewardPerSecond * expectedDuration;

        if (expectedDuration != 0 && expectedReward == 0) revert BaseRewarder__ZeroReward();

        uint256 endTimestamp = startTimestamp + expectedDuration;

        _rewardsPerSecond = rewardPerSecond;
        _reserve = totalUnclaimedRewards + expectedReward;

        _endTimestamp = endTimestamp;
        _totalUnclaimedRewards = totalUnclaimedRewards;

        _rewarder.updateAccDebtPerShare(totalSupply, totalRewards);
        _rewarder.lastUpdateTimestamp = startTimestamp;

        emit RewardParameterUpdated(rewardPerSecond, startTimestamp, endTimestamp);
    }
```
Let's say the **expectedDuration** is **2 weeks = 1209600 seconds**, and the reward token is a token with 2 decimals as the example previously linked and lets say the **remainingReward** is **100_000e2** when **rewardPerSecond** is calculated the result will be **8**.  If we multiply **8 by 1209600** we get **9676800** which means **3232** tokens won't be distributed. 

## Vulnerability Detail

## Impact
If the reward tokens is a Weird ERC20 token, there will be different problems with allocating the rewards as explained above. 

## Code Snippet
https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/rewarders/BaseRewarder.sol#L285-L299
https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/rewarders/BaseRewarder.sol#L340-L376

## Tool used
Manual Review

## Recommendation
Consider whitelisting certain tokens in the ``RewarderFactory.sol`` contract, and allow only this tokens to be used as a reward tokens for each ``MasterChefRewarder.sol`` contract instance created.