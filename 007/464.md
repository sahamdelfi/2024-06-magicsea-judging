Decent Brown Wolf

Medium

# Renewing position harvests rewards to the operator

## Summary

An operator can execute certain actions on behalf of the token's owner. Renewing a locked position is harvesting the rewards to the caller meaning the operator can expropriate pending rewards from the entitled owner. 

## Vulnerability Detail

Functions `renewLockPosition` and `extendLockPosition` can be executed by an operator:
```solidity
/**
     * @dev Renew lock from a staking position
     *
     * Can only be called by lsNFT's owner or approved address
     */
    function renewLockPosition(uint256 tokenId) external nonReentrant {
        _requireOnlyApprovedOrOwnerOf(tokenId);

        _updatePool();
        _lockPosition(tokenId, _stakingPositions[tokenId].lockDuration, false);
    }

    /**
     * @dev Extends a lock position, lockDuration is the new lock duration
     * Lock duration must be greater than existing lock duration
     * Can only be called by lsNFT's owner or approved address
     *
     * @param tokenId The id of the lsNFT
     * @param lockDuration The new lock duration
     */
    function extendLockPosition(uint256 tokenId, uint256 lockDuration) external nonReentrant {
        _requireOnlyApprovedOrOwnerOf(tokenId);

        _updatePool();
        _lockPosition(tokenId, lockDuration, true);
    }
```

They call `_lockPosition` which does the harvesting this way:
```solidity
_harvestPosition(tokenId, msg.sender);
```

Harvest function is sending pending rewards to the specified address:
```solidity
    /**
     * @dev Harvest rewards from a position
     * Will also update the position's totalMultiplier
     */
    function _harvestPosition(uint256 tokenId, address to) internal {
        StakingPosition storage position = _stakingPositions[tokenId];

        // compute position's pending rewards
        uint256 pending = position.amountWithMultiplier * _accRewardsPerShare / PRECISION_FACTOR - position.rewardDebt;

        // transfer rewards
        if (pending > 0) {
            // send rewards
            _safeRewardTransfer(to, pending);
        }
        emit HarvestPosition(tokenId, to, pending);
    }
```

If an operator is executing this action, `msg.sender` will be their address, meaning all the accrued rewards from this position will be sent not to the owner, but to the operator.

## Impact

Sending rewards directly to the operator is not an appropriate default behavior. All the other invocations of harvesting send rewards to the owner or allow specifying the recipient. Rewards might be lost if the operator cannot handle them. Also, an owner should be able to choose an operator for convenience to manage their positions, but not to drain their rewards.

## Code Snippet

https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/MlumStaking.sol#L510-L513

https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/MlumStaking.sol#L525-L528

https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/MlumStaking.sol#L710

https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/MlumStaking.sol#L683

## Tool used

Manual Review

## Recommendation

Should use `_harvestPosition(tokenId, _ownerOf(tokenId));`.
