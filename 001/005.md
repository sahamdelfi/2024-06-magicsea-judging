Lone Opaque Mustang

High

# `addToPosition()` accounts for fee incorrectly

## Summary

The staking functionality of MagicSea allows the use of Fee-On-Transfer (FOT) tokens. The createPosition function correctly accounts for the FOT reduction when calculating staking amounts and lock durations. However, the addToPosition function does not adjust for FOT tokens before calculating the new lock duration, resulting in an incorrect extension of the lockup period. This issue causes users to experience a denial-of-service (DoS) by having their stakes locked for longer than intended.

## Vulnerability Detail

The staking functionality of MagicSea allows for the usage of FOT [(Fee-On-Transfer)](https://help.1inch.io/en/articles/5651059-what-is-a-fee-on-transfer-token) tokens. To handle the behavior of these tokens, the [`_transferSupportingFeeOnTransfer()`](https://github.com/sherlock-audit/2024-06-magicsea/blob/7fd1a65b76d50f1bf2555c699ef06cde2b646674/magicsea-staking/src/MlumStaking.sol#L724-L731) function was implemented. 

```solidity
function _transferSupportingFeeOnTransfer(IERC20 token, address user, uint256 amount)
    internal
    returns (uint256 receivedAmount)
{
    uint256 previousBalance = token.balanceOf(address(this));
    token.safeTransferFrom(user, address(this), amount);
    return token.balanceOf(address(this)) - previousBalance;
}
```

This function accounts for the reduction on transfer and returns the actual received amount.

When we look at the deposit functionality, we can see that the `amount` parameter is updated to the actual transferred amount before the multipliers are calculated.

```solidity
function createPosition(uint256 amount, uint256 lockDuration) external override nonReentrant {
    // other checks

    // handle tokens with transfer tax
    amount = _transferSupportingFeeOnTransfer(stakedToken, msg.sender, amount);
    require(amount != 0, "zero amount"); // createPosition: amount cannot be null

    // mint NFT position token
    uint256 currentTokenId = _mintNextTokenId(msg.sender);

    // calculate bonuses
    uint256 lockMultiplier = getMultiplierByLockDuration(lockDuration);
    uint256 amountWithMultiplier = amount * (lockMultiplier + 1e4) / 1e4;

    // create position
    _stakingPositions[currentTokenId] = StakingPosition({
        initialLockDuration: lockDuration,
        amount: amount,
        rewardDebt: amountWithMultiplier * (_accRewardsPerShare) / (PRECISION_FACTOR),
        lockDuration: lockDuration,
        startLockTime: _currentBlockTimestamp(),
        lockMultiplier: lockMultiplier,
        amountWithMultiplier: amountWithMultiplier,
        totalMultiplier: lockMultiplier
    });

    // update total lp supply
    _stakedSupply = _stakedSupply + amount;
    _stakedSupplyWithMultiplier = _stakedSupplyWithMultiplier + amountWithMultiplier;

    emit CreatePosition(currentTokenId, amount, lockDuration);
}
```

The problem is that there is a second function that can be used to change one's staking. This is the `addToPosition()` function, which allows users to add additional funds to an existing staking.

```solidity
function addToPosition(uint256 tokenId, uint256 amountToAdd) external override nonReentrant {
    _requireOnlyOperatorOrOwnerOf(tokenId);
    require(amountToAdd > 0, "0 amount"); // addToPosition: amount cannot be null

    _updatePool();
    address nftOwner = ERC721Upgradeable.ownerOf(tokenId);
    _harvestPosition(tokenId, nftOwner);

    StakingPosition storage position = _stakingPositions[tokenId];

    // we calculate the avg lock time:
    // lock_duration = (remainin_lock_time * staked_amount + amount_to_add * inital_lock_duration) / (staked_amount + amount_to_add)
    uint256 remainingLockTime = _remainingLockTime(position);
    uint256 avgDuration = (remainingLockTime * position.amount + amountToAdd * position.initialLockDuration)
        / (position.amount + amountToAdd);

    position.startLockTime = _currentBlockTimestamp();
    position.lockDuration = avgDuration;

    // lock multiplier stays the same
    position.lockMultiplier = getMultiplierByLockDuration(position.initialLockDuration);

    // handle tokens with transfer tax
    amountToAdd = _transferSupportingFeeOnTransfer(stakedToken, msg.sender, amountToAdd);

    // update position
    position.amount = position.amount + amountToAdd;
    _stakedSupply = _stakedSupply + amountToAdd;
    _updateBoostMultiplierInfoAndRewardDebt(position);

    emit AddToPosition(tokenId, msg.sender, amountToAdd);
}
```

The `lock_duration` must be shifted on addition to the staked position. This shifting is based on the number of new tokens added and their relation to the already added tokens. Unfortunately, the function uses the full amount without accounting for fees in this calculation, resulting in the lockup duration being extended by more than it should and locking the user's stake for longer than intended.

## Impact
This issue results in a DOS of the user's lockup. The duration of this depends on the amount of tokens added and the lockup periods defined for the pool. Considering that the maxLockupDuration is 365 days, a DOS of more than seven days will occur most of the time.

## Code Snippet

## Tool used

Manual Review

## Recommendation

We recommend updating the `amountToAdd` before calculating the new lockup period.

```solidity
function addToPosition(uint256 tokenId, uint256 amountToAdd) external override nonReentrant {
    _requireOnlyOperatorOrOwnerOf(tokenId);
    require(amountToAdd > 0, "0 amount"); // addToPosition: amount cannot be null

    _updatePool();
    address nftOwner = ERC721Upgradeable.ownerOf(tokenId);
    _harvestPosition(tokenId, nftOwner);

    StakingPosition storage position = _stakingPositions[tokenId];

    // handle tokens with transfer tax
    amountToAdd = _transferSupportingFeeOnTransfer(stakedToken, msg.sender, amountToAdd);

    // we calculate the avg lock time:
    // lock_duration = (remainin_lock_time * staked_amount + amount_to_add * inital_lock_duration) / (staked_amount + amount_to_add)
    uint256 remainingLockTime = _remainingLockTime(position);
    uint256 avgDuration = (remainingLockTime * position.amount + amountToAdd * position.initialLockDuration)
        / (position.amount + amountToAdd);

    position.startLockTime = _currentBlockTimestamp();
    position.lockDuration = avgDuration;

    // lock multiplier stays the same
    position.lockMultiplier = getMultiplierByLockDuration(position.initialLockDuration);


    // update position
    position.amount = position.amount + amountToAdd;
    _stakedSupply = _stakedSupply + amountToAdd;
    _updateBoostMultiplierInfoAndRewardDebt(position);

    emit AddToPosition(tokenId, msg.sender, amountToAdd);
}
```