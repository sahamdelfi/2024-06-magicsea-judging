Decent Brown Wolf

High

# Notify bribes about deposit will revert breaking the voting

## Summary

The bribe rewarder incorrectly acknowledges the owner of the token when the caller is the `Voter` contract itself. The deposit function will always revert in runtime making it practically unusable.

## Vulnerability Detail

When voting for the current period, it verifies that the user is a legitimate owner of the token, and then calls `_notifyBribes`:

```solidity
   /**
     * Cast votes for current voting period
     *
     * @param tokenId - token id of mlum staking position
     * @param pools - array of pool addresses
     * @param deltaAmounts - array of amounts must not exceed the total voting power
     */
    function vote(uint256 tokenId, address[] calldata pools, uint256[] calldata deltaAmounts) external {
        ...

        // check ownership of tokenId
        if (_mlumStaking.ownerOf(tokenId) != msg.sender) {
            revert IVoter__NotOwner();
        }

        ...

        for (uint256 i = 0; i < pools.length; ++i) {
            ...

            _notifyBribes(_currentVotingPeriodId, pool, tokenId, deltaAmount); // msg.sender, deltaAmount);
        }

        ...
    }
```

`_notifyBribes` invokes rewarders `deposit` function:
```solidity
    function _notifyBribes(uint256 periodId, address pool, uint256 tokenId, uint256 deltaAmount) private {
        IBribeRewarder[] storage rewarders = _bribesPerPriod[periodId][pool];
        for (uint256 i = 0; i < rewarders.length; ++i) {
            if (address(rewarders[i]) != address(0)) {
                rewarders[i].deposit(periodId, tokenId, deltaAmount);
                _userBribesPerPeriod[periodId][tokenId].push(rewarders[i]);
            }
        }
    }
```

Down the road `deposit` invokes `_modify`:
```solidity
    function deposit(uint256 periodId, uint256 tokenId, uint256 deltaAmount) public onlyVoter {
        _modify(periodId, tokenId, deltaAmount.toInt256(), false);

        emit Deposited(periodId, tokenId, _pool(), deltaAmount);
    }
```

And modify checks the owner again:
```solidity
    function _modify(uint256 periodId, uint256 tokenId, int256 deltaAmount, bool isPayOutReward)
        private
        returns (uint256 rewardAmount)
    {
        if (!IVoter(_caller).ownerOf(tokenId, msg.sender)) {
            revert BribeRewarder__NotOwner();
        }
```
It passes `msg.sender` but in this case, it will be the address of the `Voter` (`_caller`), so it will always revert making the `vote` function unusable.

## Impact

Users can't vote unless they do not specify any pools. The bribe rewarder deposit function is not working as intended and reverts runtime.

## Code Snippet

https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/Voter.sol#L161-L163

https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/Voter.sol#L211

https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/Voter.sol#L225

https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/rewarders/BribeRewarder.sol#L144

https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/rewarders/BribeRewarder.sol#L264-L266

This issue is not caught in your tests, because you use a mock which always returns true:
```solidity
    function ownerOf(uint256 tokenId, address account) external pure override returns (bool) {
        {
            tokenId;
            account;
        }
        return true;
    }
```

## Tool used

Manual Review

## Recommendation

Remove the auth check from `_modify` (trust the `Voter`), refactor this check to a modifier, and add it to the `claim` function.
