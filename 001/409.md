Glorious Bronze Ladybug

Medium

# User can not claim rewards after last period

## Summary

In the `BribeRewarder::claim` function, the code iterates through all periods to claim the reward from every period. The problem arises when the `latestFinishedPeriod` is larger than the end period of the current `BribeRewarder`. This causes the function to iterate over periods that do not exist, resulting in `array out-of-bounds`.

## Vulnerability Detail

Consider a scenario where a briber has a start period of 2 and an end period of 4, and the `latestFinishedPeriod` is 5. The current logic of `BribeRewarder::claim` will iterate from period 2 to period 5 and will eventually revert when trying to access a non-existent period. This issue prevents users from claiming their rewards if the briber's `lastVotingPeriod` does not match the `latestFinishedPeriod`.

https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/rewarders/BribeRewarder.sol#L153-L164

### Proof of Concept

Some changes are needed for this test before running it,  add the following changes to `VoterMock.sol`:

```diff
mapping(uint256 => VotingPeriod) _startTimes;

...

function setStartAndEndTime(
        uint256 periodId,
        uint256 startTime,
        uint256 endTime
    ) external {
    VotingPeriod storage period = _startTimes[periodId];
    period.startTime = startTime;
    period.endTime = endTime;
}

+ function getPeriodStartEndtime(
+        uint256 periodId
+    ) external view override returns (uint256, uint256) {
+    return (_startTimes[periodId].startTime, _startTimes[periodId].endTime);
+ }


- function getPeriodStartEndtime(
-        uint256 periodId
-    ) external view override returns (uint256, uint256) {
-    return (_startTime, _endTime);
- }

```

Add this test to `BribeRewarder.sol`

```solidity

function testDepositAndClaimPendingRewardWhenVoterPeriodEnded() public {
        ERC20Mock(address(rewardToken)).mint(address(this), 20e18);
        ERC20Mock(address(rewardToken)).approve(address(rewarder), 20e18);

        rewarder.fundAndBribe(1, 1, 10e18);

        _voterMock.setCurrentPeriod(1);
        _voterMock.setStartAndEndTime(1, 0, 20);

        vm.prank(address(_voterMock));
        vm.warp(0);
        rewarder.deposit(1, 1, 0.2e18);

        vm.warp(21);
        _voterMock.setCurrentPeriod(2);
        _voterMock.setLatestFinishedPeriod(3);
        vm.prank(alice);
        rewarder.claim(1);

        assertEq(10000000000000000000, rewardToken.balanceOf(alice));
    }
```

## Impact

Users will be unable to withdraw their rewards when the `_lastVotingPeriod` of the briber is smaller than the `latestFinishedPeriod`. This can lead to significant inconvenience and potential financial losses for users who are unable to access their rewards due to this logic error.

## Code Snippet

```solidity
function claim(uint256 tokenId) external override {
        uint256 endPeriod = IVoter(_caller).getLatestFinishedPeriod();

        uint256 totalAmount;

        // calc emission per period cause every period can every other durations
        for (uint256 i = _startVotingPeriod; i <= endPeriod; ++i) {
            totalAmount += _modify(i, tokenId, 0, true);
        }

        emit Claimed(tokenId, _pool(), totalAmount);
    }
```

## Tool used

Manual Review

## Recommendation 

First in the `_bribe` fix the array creation, because if we have `_startVotingPeriod` equals to 1 and `_lastVotingPeriod` equals to 2, this array will be created with 3 elements which will be not correct.

```diff
- for (uint256 i = 0; i <= bribeEpochs; ++i) {
+ for (uint256 i = 0; i < bribeEpochs; ++i) {
    _rewards.push();
}
```

And then fix the real problem with this in the `claim`:

```diff
function claim(uint256 tokenId) external override {
    uint256 endPeriod = IVoter(_caller).getLatestFinishedPeriod();
+   if(endPeriod > _lastVotingPeriod) endPeriod = _lastVotingPeriod;
    uint256 totalAmount;

    // calc emission per period cause every period can every other durations
    for (uint256 i = _startVotingPeriod; i <= endPeriod; ++i) {
        totalAmount += _modify(i, tokenId, 0, true);
    }

    emit Claimed(tokenId, _pool(), totalAmount);
}
```