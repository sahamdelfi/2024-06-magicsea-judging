Salty Sky Caribou

High

# Wrong check in _requireOnlyOperatorOrOwnerOf in MlumStaking.sol leading to anyone being able to add to someone else's position

## Summary
The function `_requireOnlyOperatorOrOwnerOf` is intended to check if a userAddress has privileged rights on a spNFT and revert if not. 
However, it serves no purpose in the way it's currently implemented. 
## Vulnerability Detail
Let's see the implementation of `_requireOnlyOperatorOrOwnerOf`:
```solidity
    /**
     * @dev Check if a userAddress has privileged rights on a spNFT
     */
    function _requireOnlyOperatorOrOwnerOf(uint256 tokenId) internal view {
        // isApprovedOrOwner: caller has no rights on token
        require(ERC721Upgradeable._isAuthorized(msg.sender, msg.sender, tokenId), "FORBIDDEN"); 
    }
```
The function require that ERC721Upgradeable._isAuthorized(msg.sender, msg.sender, tokenId) returns true:
```solidity
    function _isAuthorized(address owner, address spender, uint256 tokenId) internal view virtual returns (bool) {
        return
            spender != address(0) &&
            (owner == spender || isApprovedForAll(owner, spender) || _getApproved(tokenId) == spender);
    }
```
However, since we're passing `msg.sender` to both the `owner` and `spender` parameters, the condition `owner == spender` will always return true, making the whole function always return true.

The `_requireOnlyOperatorOrOwnerOf` function is used in ` addToPosition()` with the intent that it should prevent anyone else but the owner of the staking position or operators adding to the specific position. However, this invariant currently doesn't hold true.
```solidity
    /**
     * @dev Add to an existing staking position
     *
     * Can only be called by lsNFT's owner or operators
     */
    function addToPosition(uint256 tokenId, uint256 amountToAdd) external override nonReentrant {
        
        _requireOnlyOperatorOrOwnerOf(tokenId); <--

       //unrelated

        // we calculate the avg lock time:
        // lock_duration = (remainin_lock_time * staked_amount + amount_to_add * inital_lock_duration) / (staked_amount + amount_to_add)
        uint256 remainingLockTime = _remainingLockTime(position);
        uint256 avgDuration = (remainingLockTime * position.amount + amountToAdd * position.initialLockDuration)
            / (position.amount + amountToAdd);

        position.startLockTime = _currentBlockTimestamp();
        position.lockDuration = avgDuration;

        //unrelated
    }
```
As demonstrated, anyone is able to add to anyone else's staking position. And since the added amount impacts the duration of the lock time, it enables an attacker to perpetually extend someone else's position leaving him in a locked state for as long as he wants.
## Impact
Anyone can add to someone else's position, forcefully extending their locked period.
## Code Snippet
https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/MlumStaking.sol#L140-L143
## Tool used

Manual Review

## Recommendation
Pass the owner in the `_requireOnlyOperatorOrOwnerOf`, like that
```solidity
    function _requireOnlyOperatorOrOwnerOf(uint256 tokenId) internal view {
        address owner = _ownerOf(tokenId);
        // isApprovedOrOwner: caller has no rights on token
        require(ERC721Upgradeable._isAuthorized(owner, msg.sender, tokenId), "FORBIDDEN"); 
    }
```