Decent Brown Wolf

High

# Incorrect operator authorization

## Summary

function `_requireOnlyOperatorOrOwnerOf` incorrectly authorizes `msg.sender` passing successfully every time.

## Vulnerability Detail

This function is meant to allow only an owner or approved operator of the token to execute certain actions:
```solidity
    /**
     * @dev Check if a userAddress has privileged rights on a spNFT
     */
    function _requireOnlyOperatorOrOwnerOf(uint256 tokenId) internal view {
        // isApprovedOrOwner: caller has no rights on token
        require(ERC721Upgradeable._isAuthorized(msg.sender, msg.sender, tokenId), "FORBIDDEN");
    }
```

However, it passes `msg.sender` twice, as an owner and an operator, thus `_isAuthorized` will always return true:
```solidity
    /**
     * @dev Returns whether `spender` is allowed to manage `owner`'s tokens, or `tokenId` in
     * particular (ignoring whether it is owned by `owner`).
     *
     * WARNING: This function assumes that `owner` is the actual owner of `tokenId` and does not verify this
     * assumption.
     */
    function _isAuthorized(address owner, address spender, uint256 tokenId) internal view virtual returns (bool) {
        return
            spender != address(0) &&
            (owner == spender || isApprovedForAll(owner, spender) || _getApproved(tokenId) == spender);
    }
```
Even the WARNING above the function says: _This function assumes that `owner` is the actual owner of `tokenId` and does not verify this assumption_.
Because `owner == spender` it will always return true making this check useless.

## Impact

`_requireOnlyOperatorOrOwnerOf` is only used in function `addToPosition`:
```solidity
    /**
     * @dev Add to an existing staking position
     *
     * Can only be called by lsNFT's owner or operators
     */
    function addToPosition(uint256 tokenId, uint256 amountToAdd) external override nonReentrant {
        _requireOnlyOperatorOrOwnerOf(tokenId);
```
this makes everyone eligible to modify an existing position. What is more, even not yet existing token id can be used (not minted yet) to execute this function.

Basically, unauthorized addresses can perform actions on tokens they don't own or aren't approved for.

## Code Snippet

https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/MlumStaking.sol#L142

A simplified version of the contract to test this assumption:

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.20;

import "https://github.com/OpenZeppelin/openzeppelin-contracts-upgradeable/blob/v5.0.0/contracts/token/ERC721/extensions/ERC721EnumerableUpgradeable.sol";

contract MlumStaking is ERC721EnumerableUpgradeable {

    constructor() {
        uint256 tokenId = 1;
        
        _mint(address(this), tokenId);
    }

    function test(uint256 tokenId) public view returns(bool) {
        return ERC721Upgradeable._isAuthorized(msg.sender, msg.sender, tokenId);
    }
}
```

## Tool used

Manual Review

## Recommendation

Should use `_isAuthorized(_ownerOf(tokenId), msg.sender, tokenId)`.
