Sleepy Navy Ant

Medium

# Users unable to claim all of their bribe rewards after the last bribe period passes

## Summary

When a user creates a BribeRewarder, they specify the periods for which they want to bribe. However, when claiming rewards, there is no check to ensure that the latest finished period does not exceed the last period ID of the bribe. This can cause the function to revert due to an array out-of-bounds error.

## Vulnerability Detail


When a bribe wants to bribe for certain periods, they can call the [BribeRewarder::bribe](https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/rewarders/BribeRewarder.sol#L132-L133) function, which accepts 3 parameters:

1. `startId` - This is the start period from where the BribeRewarder should start receiving deposits.
2. `endId` - This is the end period from where the BribeRewarder should stop receiving deposits.
3. `amountPerPeriod` - How much rewards should be distributed per period.

The function then calls the internal `_bribe` function:

```solidity
function _bribe(uint256 startId, uint256 lastId, uint256 amountPerPeriod) internal {
    _checkAlreadyInitialized();
    if (lastId < startId) revert BribeRewarder__WrongEndId();
    if (amountPerPeriod == 0) revert BribeRewarder__ZeroReward();

    IVoter voter = IVoter(_caller);

    if (startId <= voter.getCurrentVotingPeriod()) {
        revert BribeRewarder__WrongStartId();
    }

    uint256 totalAmount = _calcTotalAmount(startId, lastId, amountPerPeriod);

    uint256 balance = _balanceOfThis(_token());

    if (balance < totalAmount) revert BribeRewarder__InsufficientFunds();

    _startVotingPeriod = startId;
    _lastVotingPeriod = lastId;
    _amountPerPeriod = amountPerPeriod;

    // create rewards per period
    uint256 bribeEpochs = _calcPeriods(startId, lastId);
    for (uint256 i = 0; i <= bribeEpochs; ++i) {
        _rewards.push();
    }

    _lastUpdateTimestamp = block.timestamp;

    IVoter(_caller).onRegister();

    emit BribeInit(startId, lastId, amountPerPeriod);
}
```

As shown in the snippet, the function performs some checks and then calls _calcPeriods, which returns lastId - startId. For example, calling bribe with startId=1 and endId=3 results in bribeEpochs being 2, so rewards will be filled from 0 to 2, making its length 3.

However, when claiming, there is no check to ensure that latestPeriodId is less than or equal to lastPeriod. This can cause a revert due to an array out-of-bounds error within _modify:

```solidity
function claim(uint256 tokenId) external override {
    uint256 endPeriod = IVoter(_caller).getLatestFinishedPeriod();

    uint256 totalAmount;

    for (uint256 i = _startVotingPeriod; i <= endPeriod; ++i) {
        totalAmount += _modify(i, tokenId, 0, true);
    }

    emit Claimed(tokenId, _pool(), totalAmount);
}
```

```solidity

function _modify(uint256 periodId, uint256 tokenId, int256 deltaAmount, bool isPayOutReward)
    private
    returns (uint256 rewardAmount)
{
    RewardPerPeriod storage reward = _rewards[_indexByPeriodId(periodId)];
    ...
}
```

## Proof of Concept
1. A bribe registers for Pool A for periods 1 to 3.
2. `_calcPeriods` returns 2. We add the periods to the rewards, making `rewards.length=3`.
3. When the first period passes and users call claim, it will loop only once because endPeriod=1.
4. When the second period passes and users call claim, it will loop twice because endPeriod=2.
5. When the third period passes and users call claim, it will loop three times because endPeriod=3.
6. However, after the fourth period passes, any user that wants to claim their rewards prior to that period will cause _modify to be called with periodId=4. _rewards[_indexByPeriodId(4)] will revert since _indexByPeriodId(4) returns 4-1=3, but the last index of the rewards array is 2.

## Impact

Any user that  claims after the period after the last one finishes, will not be able to due array-out-of-bounds error

## Code Snippet

https://github.com/sherlock-audit/2024-06-magicsea/tree/main/magicsea-staking/src/rewarders/BribeRewarder.sol#L315-L317

https://github.com/sherlock-audit/2024-06-magicsea/tree/main/magicsea-staking/src/rewarders/BribeRewarder.sol#L333-L335

https://github.com/sherlock-audit/2024-06-magicsea/tree/main/magicsea-staking/src/rewarders/BribeRewarder.sol#L153-L164

https://github.com/sherlock-audit/2024-06-magicsea/tree/main/magicsea-staking/src/rewarders/BribeRewarder.sol#L226-L258

## Tool used

Manual Review

## Recommendation

Consider only allowing claims up until the last period the bribe registered for:

```diff
function claim(uint256 tokenId) external override {
    uint256 endPeriod = IVoter(_caller).getLatestFinishedPeriod();

    uint256 totalAmount;

+   uint256 _endPeriod = endPeriod > _lastVotingPeriod ? _lastVotingPeriod : endPeriod;

-   for (uint256 i = _startVotingPeriod; i <= endPeriod; ++i) {
+   for (uint256 i = _startVotingPeriod; i <= _endPeriod; ++i) {
        totalAmount += _modify(i, tokenId, 0, true);
    }

    emit Claimed(tokenId, _pool(), totalAmount);
}
```

This ensures that claims are only processed for valid periods within the range the bribe registered for.
