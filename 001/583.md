Smooth Taffy Moth

High

# The `MlumStaking._requireOnlyOperatorOrOwnerOf()` function always returns `true`

## Summary

The `_requireOnlyApprovedOrOwnerOf()` function invokes the `ERC721Upgradeable._isAuthorized(msg.sender, msg.sender, tokenId)` function. However, it consistently returns `true` because the first parameter is `msg.sender` and not the owner of `tokenId`.

## Vulnerability Detail

The `_requireOnlyApprovedOrOwnerOf()` function invokes the `ERC721Upgradeable._isAuthorized(msg.sender, msg.sender, tokenId)` function. When considering the first parameter, it is currently set as `msg.sender` and not the owner of `tokenId`. Consequently, the `ERC721Upgradeable._isAuthorized()` function returns `true`, leading to the `_requireOnlyApprovedOrOwnerOf()` function always returning `true` as well.

```solidity
MlumStaking.sol

    function _requireOnlyOperatorOrOwnerOf(uint256 tokenId) internal view {
        // isApprovedOrOwner: caller has no rights on token
        require(ERC721Upgradeable._isAuthorized(msg.sender, msg.sender, tokenId), "FORBIDDEN");
    }

--------------------------

ERC721Upgradeable.sol

    function _isAuthorized(address owner, address spender, uint256 tokenId) internal view virtual returns (bool) {
        return
            spender != address(0) &&
            (owner == spender || isApprovedForAll(owner, spender) || _getApproved(tokenId) == spender);
    }
```

This issue can be exploited by attackers.

### Scenario 1:

1. Alice has a position with no remaining lock time and is preparing to withdraw and close the position. 
2. Bob invokes the `addToPosition()` function to add a small amount to Alice's position. The `addToPosition()` function only includes the `_requireOnlyOperatorOrOwnerOf()` check as the caller validation. Consequently, Bob's action succeeds as the `_requireOnlyOperatorOrOwnerOf()` always evaluates to `true`. 

```solidity
    function addToPosition(uint256 tokenId, uint256 amountToAdd) external override nonReentrant {
        _requireOnlyOperatorOrOwnerOf(tokenId);
        ...
    }
```

3. Subsequently, Alice's position is modified, causing the remaining lock time of her position to become non-zero.

```solidity
        uint256 remainingLockTime = _remainingLockTime(position);
        uint256 avgDuration = (remainingLockTime * position.amount + amountToAdd * position.initialLockDuration)
            / (position.amount + amountToAdd);

        position.startLockTime = _currentBlockTimestamp();
        position.lockDuration = avgDuration;
```

This scenario could potentially result in a `DoS` situation for Alice during the withdrawal process, as withdrawing requires that there is no remaining lock time for the position.

### Scenario 2:

1. Alice currently holds a position with a short remaining lock time and possesses voting power. 
2. Bob executes the `addToPosition()` function to include a dust amount to Alice's position. Consequently, the `lockDuration` of Alice's position approaches the remaining lock time of the position, causing the `lockDuration` to become less than `2 weeks` (which is the `_periodDuration` of the `Voter` contract) based on the provided code snippet in Scenario 1. As a result, Alice will forfeit her voting power since the `Voter.vote()` function mandates that the `lockDuration` of the position must not be less than `_periodDuration`.

```solidity
    function vote(uint256 tokenId, address[] calldata pools, uint256[] calldata deltaAmounts) external {
        ...

        if (_mlumStaking.getStakingPosition(tokenId).lockDuration < _periodDuration) {
            revert IVoter__InsufficientLockTime();
        }

        ...
    }
```

## Impact

1. Attackers can execute a `DoS` attack on any withdrawal process within the `MlumStaking` contract. 
2. Attackers can cause others to lose their voting power.

## Code Snippet

https://github.com/OpenZeppelin/openzeppelin-contracts-upgradeable/blob/master/contracts/token/ERC721/ERC721Upgradeable.sol#L215-L219

https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/MlumStaking.sol#L140-L143

https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/MlumStaking.sol#L397-L428

https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/Voter.sol#L175-L177

## Tool used

Manual Review

## Recommendation

The `_requireOnlyOperatorOrOwnerOf()` function should be fixed as follows.

```diff
    function _requireOnlyOperatorOrOwnerOf(uint256 tokenId) internal view {
        // isApprovedOrOwner: caller has no rights on token
-       require(ERC721Upgradeable._isAuthorized(msg.sender, msg.sender, tokenId), "FORBIDDEN");
+       require(ERC721Upgradeable._isAuthorized(_ownerOf(tokenId), msg.sender, tokenId), "FORBIDDEN");
    }
```