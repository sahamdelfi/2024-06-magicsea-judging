Furry Viridian Copperhead

Medium

# Staking positions don't expire

## Summary
In the ``MlumStaking.sol`` contract when a user wants to stake tokens, and create a position he has to call the [createPosition()](https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/MlumStaking.sol#L354-L390) function:
```solidity
    function createPosition(uint256 amount, uint256 lockDuration) external override nonReentrant {
        // no new lock can be set if the pool has been unlocked
        if (isUnlocked()) {
            require(lockDuration == 0, "locks disabled");
        }
        ...
        // calculate bonuses
        uint256 lockMultiplier = getMultiplierByLockDuration(lockDuration);
        uint256 amountWithMultiplier = amount * (lockMultiplier + 1e4) / 1e4;

        // create position
        _stakingPositions[currentTokenId] = StakingPosition({
            initialLockDuration: lockDuration,
            amount: amount,
            rewardDebt: amountWithMultiplier * (_accRewardsPerShare) / (PRECISION_FACTOR),
            lockDuration: lockDuration,
            startLockTime: _currentBlockTimestamp(),
            lockMultiplier: lockMultiplier,
            amountWithMultiplier: amountWithMultiplier,
            totalMultiplier: lockMultiplier
        });

        // update total lp supply
        _stakedSupply = _stakedSupply + amount;
        _stakedSupplyWithMultiplier = _stakedSupplyWithMultiplier + amountWithMultiplier;

        emit CreatePosition(currentTokenId, amount, lockDuration);
    }
```
**amountWithMultiplier**  is later used in the calculation of the rewards each user should receive. As can be seen in the above code snippet in order to calculate the **amountWithMultiplier** the [getMultiplierByLockDuration()](https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/MlumStaking.sol#L217-L227) function is called which calculates the multiplier in the following way:

```solidity
    function getMultiplierByLockDuration(uint256 lockDuration) public view returns (uint256) {
        // in case of emergency unlock
        if (isUnlocked()) return 0;

        if (_maxLockDuration == 0 || lockDuration == 0) return 0;

        // capped to maxLockDuration
        if (lockDuration >= _maxLockDuration) return _maxLockMultiplier;

        return (_maxLockMultiplier * lockDuration) / (_maxLockDuration);
    }
```
[_maxLockMultiplier](https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/MlumStaking.sol#L109) is set when the contract is initialized and its value is set to **20_000**. Later on it can be updated by the admin by calling the [setLockMultiplierSettings()](https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/MlumStaking.sol#L269-L278) function, but it can only be decreased from its initial value due to [MAX_LOCK_MULTIPLIER_LIMIT](https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/MlumStaking.sol#L69) being a constant set to **15_000**:
```solidity
    function setLockMultiplierSettings(uint256 maxLockDuration, uint256 maxLockMultiplier) external {
        require(msg.sender == owner() || msg.sender == _operator, "FORBIDDEN");
        // onlyOperatorOrOwner: caller has no operator rights
        require(maxLockMultiplier <= MAX_LOCK_MULTIPLIER_LIMIT, "too high");
        // setLockSettings: maxGlobalMultiplier is too high
        _maxLockDuration = maxLockDuration;
        _maxLockMultiplier = maxLockMultiplier;

        emit SetLockMultiplierSettings(maxLockDuration, maxLockMultiplier);
    }
```

The [_updateBoostMultiplierInfoAndRewardDebt()](https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/MlumStaking.sol#L656-L668) function:
```solidity
    function _updateBoostMultiplierInfoAndRewardDebt(StakingPosition storage position) internal {
        // keep the original lock multiplier and recompute current boostPoints multiplier
        uint256 newTotalMultiplier = position.lockMultiplier;
        if (newTotalMultiplier > _maxGlobalMultiplier) newTotalMultiplier = _maxGlobalMultiplier;

        position.totalMultiplier = newTotalMultiplier;
        uint256 amountWithMultiplier = position.amount * (newTotalMultiplier + 1e4) / 1e4;
        // update global supply
        _stakedSupplyWithMultiplier = _stakedSupplyWithMultiplier - position.amountWithMultiplier + amountWithMultiplier;
        position.amountWithMultiplier = amountWithMultiplier;

        position.rewardDebt = amountWithMultiplier * _accRewardsPerShare / PRECISION_FACTOR;
    }
```
A user may call the [harvestPosition()](https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/MlumStaking.sol#L442-L448) function as many times as he wants in order to harvest the rewards he has accrued up to this moment. The [harvestPosition()](https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/MlumStaking.sol#L442-L448) function internally calls the  [_updateBoostMultiplierInfoAndRewardDebt()](https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/MlumStaking.sol#L656-L668) function however the **amountWithMultiplier** will never be decreased even if the **_maxLockMultiplier** global parameter has been decreased. Because the **_maxGlobalMultiplier** can't be updated once the ``MlumStaking.sol`` contract is initialized. It's value is [set](https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/MlumStaking.sol#L107) to **20_000**. The problem is that once a position is created it doesn't expire. If the **_maxLockMultiplier** has been decreased users that create new positions, add to their positions by calling the [addToPosition()](https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/MlumStaking.sol#L397-L428), increase their lock duration by calling the [renewLockPosition()](https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/MlumStaking.sol#L509-L514) or [extendLockPosition()](https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/MlumStaking.sol#L524-L529) functions will get a lower multiplier, and thus receive less rewards. However a user who created a stake position with a higher **_maxLockMultiplier**, can keep his position until the contract is live, and rewards are deposited to it, there is nothing that the protocol can do, he will still have a higher multiplier and thus receive more rewards, even after his position is expired. Note that the above described vulnerability is about positions that when created have a **lockMultiplier**  at least bigger than **15_000**, meaning that initially positions have to be open for a longer period. There are some edge cases where admins may decided to set the **_maxLockMultiplier** parameter to a much smaller value than the contract have been initialized with, such as **1_000**. In that scenario the impact of the above described vulnerability only amplifies, as the malicious user will acquire much bigger rewards than he is supposed to. 

## Vulnerability Detail

## Impact
Once a stake position is created it doesn't expire, a user with expired position can continue harvesting rewards as long as rewards are being deposited to the contract. This creates unfavorable conditions to other users as they have to lock their tokens for a certain period of time, and then lock again if they don't know about this vulnerability (most crypto users are not proficient developers). Once the initial lock of a malicious user has expired, he is no longer risking any sharp price declines in the staking token as he can withdraw immediately. However a bigger problem is the calculation of **amountWithMultiplier** for each position. As explained **_maxLockMultiplier** can only decrease from its initial value if updated by an admin, users that modify their position in any way after **_maxLockMultiplier** has been decreased will start receiving less rewards. This way a malicious user with an expired position is effectively stealing rewards from other users, because a malicious user receiving more rewards means that other users are receiving less rewards, as the rewards distributed are a predetermined amount. 

## Code Snippet
https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/MlumStaking.sol#L217-L227

## Tool used
Manual Review

## Recommendation
Consider adding a functionality to update **_maxGlobalMultiplier** as well, and not allow users with expired positions to harvest rewards, once their positions are expired if they don't increase their lock.