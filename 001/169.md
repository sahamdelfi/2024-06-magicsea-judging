Slow Sapphire Barracuda

High

# Incorrect access control in `BribeRewarder::_modify` blocks users deposits

## Summary
Access control for calls to the `BribeRewarder::deposit` function is enforced by the modifier `onlyVoter` which will check if the address calling  `deposit`  is `Voter.sol`:

```javascript
    function deposit(
        uint256 periodId,
        uint256 tokenId,
        uint256 deltaAmount
@>  ) public onlyVoter {
        _modify(periodId, tokenId, deltaAmount.toInt256(), false);

        emit Deposited(periodId, tokenId, _pool(), deltaAmount);
    }
```

```javascript
    modifier onlyVoter() {
        _checkVoter();
        _;
    }
...
    function _checkVoter() internal view virtual {
        if (msg.sender != address(_caller)) {
            revert BribeRewarder__OnlyVoter();
        }
    }
```

To be eligible for a deposit in the bribe rewarder from a corresponding token ID, a user must be registered as the owner of the NFT. However, since only the voter contract can call this function and the token ownership check uses `msg.sender`, users will be ineligible for deposits:

```javascript
    function _modify(
        uint256 periodId,
        uint256 tokenId,
        int256 deltaAmount,
        bool isPayOutReward
    ) private returns (uint256 rewardAmount) {
        if (!IVoter(_caller).ownerOf(tokenId, msg.sender)) {
            revert BribeRewarder__NotOwner();
        }
```
## Vulnerability Detail
A call to `BribeRewarder::_modify` will revert and prevent users from depositing in the rewarder. This means users will not be able to vote pools in periods where they are assigned a bribe rewarder.
## Impact
For a better understanding refer to the following PoC:

<details>

<summary> See PoC </summary>

Place in your test suite.

```javascript
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.20;

import "forge-std/Test.sol";

import "../src/rewarders/BribeRewarder.sol";
import "../src/rewarders/RewarderFactory.sol";
import "../src/transparent/TransparentUpgradeableProxy2Step.sol";
import "./mocks/ERC20.sol";
import "../src/Voter.sol";
import "../src/MlumStaking.sol";
import "src/interfaces/IBribeRewarder.sol";
import "test/mocks/MasterChefMock.sol";

contract BribeRewarderTest is Test {
    Voter _voter;

    BribeRewarder rewarder;

    MlumStaking _pool;

    RewarderFactory factory;

    MasterChefMock mock;

    IERC20 tokenA;
    IERC20 rewardToken;

    function setUp() public {
        tokenA = IERC20(new ERC20Mock("Token A", "TA", 18));
        rewardToken = IERC20(new ERC20Mock("Reward Token", "RT", 6));

        address factoryImpl = address(new RewarderFactory());
        factory = RewarderFactory(
            address(
                new TransparentUpgradeableProxy2Step(
                    factoryImpl,
                    ProxyAdmin2Step(address(1)),
                    abi.encodeWithSelector(
                        RewarderFactory.initialize.selector,
                        address(this),
                        new uint8[](0),
                        new address[](0)
                    )
                )
            )
        );

        address poolImpl = address(new MlumStaking(tokenA, rewardToken));

        _pool = MlumStaking(
            address(
                new TransparentUpgradeableProxy2Step(
                    poolImpl,
                    ProxyAdmin2Step(address(1)),
                    abi.encodeWithSelector(
                        MlumStaking.initialize.selector,
                        address(this)
                    )
                )
            )
        );

        mock = new MasterChefMock();

        address voterImpl = address(
            new Voter(mock, _pool, IRewarderFactory(address(factory)))
        );

        _voter = Voter(
            address(
                new TransparentUpgradeableProxy2Step(
                    voterImpl,
                    ProxyAdmin2Step(address(1)),
                    abi.encodeWithSelector(
                        Voter.initialize.selector,
                        address(this)
                    )
                )
            )
        );

        factory.setRewarderImplementation(
            IRewarderFactory.RewarderType.BribeRewarder,
            IRewarder(address(new BribeRewarder(address(_voter))))
        );

        rewarder = BribeRewarder(
            payable(
                address(
                    factory.createBribeRewarder(rewardToken, address(_pool))
                )
            )
        );
    }

    function onERC721Received(
        address from,
        address to,
        uint256 tokenId,
        bytes memory data
    ) public returns (bytes4 retval) {
        return IERC721Receiver.onERC721Received.selector;
    }

    function testDepositFails() public {
        ERC20Mock(address(rewardToken)).mint(address(this), 20e18);
        ERC20Mock(address(rewardToken)).approve(address(rewarder), 20e18);

        ERC20Mock(address(tokenA)).mint(address(this), 1e18);
        ERC20Mock(address(tokenA)).approve(address(_pool), 1e18);

        rewarder.fundAndBribe(1, 2, 10e18);

        _pool.createPosition(1e18, 1 days);

        vm.startPrank(address(_voter));
        vm.expectRevert(IBribeRewarder.BribeRewarder__NotOwner.selector);
        rewarder.deposit(1, 1, 0.2e18);
    }
}
```
</details>

## Code Snippet
https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/rewarders/BribeRewarder.sol#L264-L266
## Tool used

Manual Review

## Recommendation
I would recommend to remove the check since it is already included in `Voter::vote` and add another in `BribeRewarder::claim`. Or to pass the address of the `msg.sender`  as a parameter to the deposit call.