Smooth Taffy Moth

High

# `BribeRewarder._calculateRewards()` can return incorrect value when `_lastUpdateTimestamp < startTime`

## Summary

The `BribeRewarder._calculateRewards()` function returns the total pending rewards accrued during the given epoch. However, it returns incorrect value when `_lastUpdateTimestamp < startTime`.

## Vulnerability Detail

When `_lastUpdateTimestamp < startTime`, [L312](https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/rewarders/BribeRewarder.sol#L312) returns larger value than expacted.

```solidity
https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/rewarders/BribeRewarder.sol#L300-L313
300:     function _calculateRewards(uint256 periodId) internal view returns (uint256) {
301:         (uint256 startTime, uint256 endTime) = IVoter(_caller).getPeriodStartEndtime(periodId);
302: 
303:         if (endTime == 0 || startTime > block.timestamp) {
304:             return 0;
305:         }
306: 
307:         uint256 duration = endTime - startTime;
308:         uint256 emissionsPerSecond = _amountPerPeriod / duration;
309: 
310:         uint256 lastUpdateTimestamp = _lastUpdateTimestamp;
311:         uint256 timestamp = block.timestamp > endTime ? endTime : block.timestamp;
312:         return timestamp > lastUpdateTimestamp ? (timestamp - lastUpdateTimestamp) * emissionsPerSecond : 0;
313:     }
```

As a result, at [L280](https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/rewarders/BribeRewarder.sol#L280), `totalRewards` is calculated larger, which means that users who claims earlier may receive more rewards than expected, while other users who claimed later may not receive any rewards, due to the limited pool of available bribe rewards.

https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/rewarders/BribeRewarder.sol#L260-L298
```solidity
260:     function _modify(uint256 periodId, uint256 tokenId, int256 deltaAmount, bool isPayOutReward)
261:         private
262:         returns (uint256 rewardAmount)
263:     {
264:         if (!IVoter(_caller).ownerOf(tokenId, msg.sender)) {
265:             revert BribeRewarder__NotOwner();
266:         }
267: 
268:         // extra check so we dont calc rewards before starttime
269:         (uint256 startTime,) = IVoter(_caller).getPeriodStartEndtime(periodId);
270:         if (block.timestamp <= startTime) {
271:             _lastUpdateTimestamp = startTime;
272:         }
273: 
274:         RewardPerPeriod storage reward = _rewards[_indexByPeriodId(periodId)];
275:         Amounts.Parameter storage amounts = reward.userVotes;
276:         Rewarder2.Parameter storage rewarder = reward.rewarder;
277: 
278:         (uint256 oldBalance, uint256 newBalance, uint256 oldTotalSupply,) = amounts.update(tokenId, deltaAmount);
279: 
280:         uint256 totalRewards = _calculateRewards(periodId);
281: 
282:         rewardAmount = rewarder.update(bytes32(tokenId), oldBalance, newBalance, oldTotalSupply, totalRewards);
283: 
284:         if (block.timestamp > _lastUpdateTimestamp) {
285:             _lastUpdateTimestamp = block.timestamp;
286:         }
287: 
288:         if (isPayOutReward) {
289:             rewardAmount = rewardAmount + unclaimedRewards[periodId][tokenId];
290:             unclaimedRewards[periodId][tokenId] = 0;
291:             if (rewardAmount > 0) {
292:                 IERC20 token = _token();
293:                 _safeTransferTo(token, msg.sender, rewardAmount);
294:             }
295:         } else {
296:             unclaimedRewards[periodId][tokenId] += rewardAmount;
297:         }
298:     }
```

## Impact

Bribe reward can be distributed incorrectly. Users who claims earlier may receive more rewards than expected, while other users who claimed later may not receive any rewards, due to the limited pool of available bribe rewards.

## Code Snippet

https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/rewarders/BribeRewarder.sol#L300-L313

## Tool used

Manual Review

## Recommendation

```diff
    function _calculateRewards(uint256 periodId) internal view returns (uint256) {
        (uint256 startTime, uint256 endTime) = IVoter(_caller).getPeriodStartEndtime(periodId);

        if (endTime == 0 || startTime > block.timestamp) {
            return 0;
        }

        uint256 duration = endTime - startTime;
        uint256 emissionsPerSecond = _amountPerPeriod / duration;
-       uint256 lastUpdateTimestamp = _lastUpdateTimestamp;
+       uint256 lastUpdateTimestamp = _lastUpdateTimestamp < startTime ? startTime : _lastUpdateTimestamp;
        uint256 timestamp = block.timestamp > endTime ? endTime : block.timestamp;
        return timestamp > lastUpdateTimestamp ? (timestamp - lastUpdateTimestamp) * emissionsPerSecond : 0;
    }
```