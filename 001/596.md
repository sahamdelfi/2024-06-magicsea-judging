Tall Porcelain Puppy

Medium

# Incorrect Check in `_requireOnlyOperatorOrOwnerOf` Allows Unauthorized Access

## Summary
The `_requireOnlyOperatorOrOwnerOf` function assigns `msg.sender` address  to both the owner and spender to when invoking `ERC721Upgradeable._isAuthorized`. This results in unauthorized access being granted to any address instead of restricting it to the token owner and approved operators.

## Vulnerability Detail

The `_requireOnlyOperatorOrOwnerOf` function, shown below, invokes the `ERC721Upgradeable._isAuthorized` function to handle token access control:

```solidity
function _requireOnlyOperatorOrOwnerOf(uint256 tokenId) internal view {
    // isApprovedOrOwner: caller has no rights on token
    require(
        ERC721Upgradeable._isAuthorized(msg.sender, msg.sender, tokenId),
        "FORBIDDEN"
    );
}
```

The OpenZeppelin `_isAuthorized` function (can be found [here](https://github.com/OpenZeppelin/openzeppelin-contracts-upgradeable/blob/master/contracts/token/ERC721/ERC721Upgradeable.sol#L215C5-L219C6)) is shown below:

```solidity
function _isAuthorized(address owner, address spender, uint256 tokenId) internal view virtual returns (bool) {
    return
        spender != address(0) &&
        (owner == spender || isApprovedForAll(owner, spender) || _getApproved(tokenId) == spender);
}
```

The issue is that both the `owner` and `spender` addresses are assigned the `msg.sender` address when `_isAuthorized` is called in `_requireOnlyOperatorOrOwnerOf`. According to the `_isAuthorized` logic, it will always return true in this case since `owner == spender`. Consequently, the `_requireOnlyOperatorOrOwnerOf` function will never revert and will allow any address to access the function in which the method is used.

In the `MlumStaking` contract, only the `addToPosition` function implements `_requireOnlyOperatorOrOwnerOf`:

```solidity
function addToPosition(
    uint256 tokenId,
    uint256 amountToAdd
) external override nonReentrant {
    _requireOnlyOperatorOrOwnerOf(tokenId);
    require(amountToAdd > 0, "0 amount"); // addToPosition: amount cannot be null

    _updatePool();
    address nftOwner = ERC721Upgradeable.ownerOf(tokenId);
    _harvestPosition(tokenId, nftOwner);

    ...
}
```

As a result, anyone will be able to call the `addToPosition` function for any tokenId. Although this might seem harmless since any user calling it would essentially be donating funds to the token owner, the function also harvests rewards and attempts to send them to the position owner (`nftOwner`). This is problematic because, in all other parts of the contract, the right to harvest rewards for a position is strictly controlled, with only the owner or approved addresses being allowed to call the harvest method. A review of all the functions responsible for the harvest shows that they all implement `_requireOnlyApprovedOrOwnerOf`.

Therefore, the protocol's intent to allow only the token owner or approved addresses to harvest rewards can be bypassed by any user who calls `addToPosition` and provides a dust amount.

## Impact

Anyone can harvest rewards for other positions' tokens by calling `addToPosition` and providing a dust amount. This is caused by the incorrect check in `_requireOnlyOperatorOrOwnerOf`.

## Code Snippet

https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/MlumStaking.sol#L140-L143

https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/MlumStaking.sol#L397-L403

## Tool used

Manual Review

## Recommendation
To address this issue, `_requireOnlyOperatorOrOwnerOf` should be updated as follows:

```solidity
function _requireOnlyOperatorOrOwnerOf(uint256 tokenId) internal view {
    // isApprovedOrOwner: caller has no rights on token
    require(
        ERC721Upgradeable._isAuthorized(_ownerOf(tokenId), msg.sender, tokenId),
        "FORBIDDEN"
    );
}
```