Young Iron Beaver

Medium

# Incorrect Lock Duration Calculation for Fee-on-Transfer Tokens in `addToPosition` Function

## Summary
The protocol and contract both account for fee-on-transfer tokens. However, in handling such tokens, the `addToPosition` function contains a logical error. It incorrectly uses the parameter value of the token amount rather than the actual transferred amount when calculating the average lock duration. This can result in an unintended extension of the user's lock duration, potentially causing financial losses. Users may misjudge the withdrawal time of their staked tokens, leading to unexpected liquidation or reduced token value due to market fluctuations.

## Vulnerability Detail

**According to the README, any type of ERC20 token is considered.**

```md
Any type of ERC20 token. Pools are permissionless. So users can open pools even with weird tokens. Issues regarding any weird token will be valid if they have Med/High impact.
```

Additionally, the implementation of the MlumStaking contract considers fee-on-transfer tokens. The following `_transferSupportingFeeOnTransfer` function is implemented to handle such tokens.

```solidity
    /**
     * @dev Handle deposits of tokens with transfer tax
     */
    function _transferSupportingFeeOnTransfer(IERC20 token, address user, uint256 amount)
        internal
        returns (uint256 receivedAmount)
    {
        uint256 previousBalance = token.balanceOf(address(this));
        token.safeTransferFrom(user, address(this), amount);
        return token.balanceOf(address(this)) - previousBalance;
    }
```

In both the `createPosition` and `addToPosition` functions, the above `_transferSupportingFeeOnTransfer` function is used to accurately determine the number of tokens transferred.

However, in the `addToPosition` function, there is a logical error that may unintentionally extend the user's position's lockDuration, potentially causing financial losses.

```solidity
function addToPosition(uint256 tokenId, uint256 amountToAdd) external override nonReentrant {
        _requireOnlyOperatorOrOwnerOf(tokenId);
        require(amountToAdd > 0, "0 amount"); // addToPosition: amount cannot be null

        _updatePool();
        address nftOwner = ERC721Upgradeable.ownerOf(tokenId);
        _harvestPosition(tokenId, nftOwner);

        StakingPosition storage position = _stakingPositions[tokenId];

        // we calculate the avg lock time:
        // lock_duration = (remainin_lock_time * staked_amount + amount_to_add * inital_lock_duration) / (staked_amount + amount_to_add)
        uint256 remainingLockTime = _remainingLockTime(position);
        uint256 avgDuration = (remainingLockTime * position.amount + amountToAdd * position.initialLockDuration)
            / (position.amount + amountToAdd);

        position.startLockTime = _currentBlockTimestamp();
        position.lockDuration = avgDuration;

        // lock multiplier stays the same
        position.lockMultiplier = getMultiplierByLockDuration(position.initialLockDuration);

        // handle tokens with transfer tax
        amountToAdd = _transferSupportingFeeOnTransfer(stakedToken, msg.sender, amountToAdd);

        // update position
        position.amount = position.amount + amountToAdd;
        _stakedSupply = _stakedSupply + amountToAdd;
        _updateBoostMultiplierInfoAndRewardDebt(position);

        emit AddToPosition(tokenId, msg.sender, amountToAdd);
    }
```

The `addToPosition` function is used to add more stakedTokens to an existing position and update relevant data such as `position.amount`, `position.startLockTime`, and `position.lockDuration`.

```solidity
// we calculate the avg lock time:
// lock_duration = (remainin_lock_time * staked_amount + amount_to_add * inital_lock_duration) / (staked_amount + amount_to_add)
```

According to the code logic and CODE COMMENTS, after adding stakedTokens, the position's lock duration data should be updated, i.e., setting `startLockTime` to the current time and calculating the `avgDuration` based on `amountToAdd`, then setting it as the `lockDuration`. This means extending the `lockDuration` based on `amountToAdd`.

However, when calculating `avgDuration`, the parameter `amountToAdd` is used directly. For fee-on-transfer tokens, the actual number of tokens added will be smaller than the parameter value due to the transfer fee. This logical error may unintentionally extend the user's lockDuration, causing potential financial losses.

Let's illustrate this with the following scenario. Assume the stakedToken is fee-on-transfer, and a 50% fee is charged during transfer.
1. At time T, Alice has 2e stakedTokens and calls `createPosition` with 2e amount and a 365-day duration. Due to the 50% transfer fee, only 1e is actually transferred to the contract, so `position.amount` will be 1e.
2. After 364 days (T+364), Alice acquires another 2e stakedTokens and plans to stake them. She chooses to call `addToPosition` to add them to her existing position.
3. Based on the documentation or code comments, Alice can calculate when she can withdraw the stakedTokens after adding them.
```md
New Lock Time = (Remaining Lock Time * Staked Amount + Amount to add * Initial Lock Duration) / (Staked Amount + Amount to add)
```
If Alice does not know the stakedToken is fee-on-transfer, she would think:
Remaining Lock Time is 1 day, Staked Amount is 2e, Amount to add is 2e, so (1 * 2 + 2 * 365)/(2+2) = 183 days, i.e., she needs to stake for another 183 days.
If Alice knows the stakedToken is fee-on-transfer, she would think:
Remaining Lock Time is 1 day, Staked Amount is 1e, Amount to add is 1e, so (1 * 1 + 1 * 365)/(1+1) = 183 days, i.e., she needs to stake for another 183 days.
In either case, Alice would expect to withdraw her stakedTokens in about six months after adding 2e stakedTokens.

However, due to the logical error in `addToPosition`, the actual calculation would be:
Remaining Lock Time is 1 day, Staked Amount is 1e, and without first calling `_transferSupportingFeeOnTransfer`, Amount to add remains the parameter value of 2e. This results in (1 * 1 + 2 * 365)/(1+2) â‰ˆ 243.67 days. Thus, the actual staking time will far exceed Alice's expectations.

## Impact
This unexpected extension of lockDuration can cause potential financial losses for Alice. For instance, Alice may expect to withdraw and sell her stakedTokens in six months to repay a loan, but the unexpected extra two months may lead to liquidation, resulting in financial loss. Alternatively, due to token price fluctuations, Alice may plan to sell after six months but is forced to wait an additional two months, possibly resulting in a lower token value and financial loss.

## Code Snippet
https://github.com/sherlock-audit/2024-06-magicsea/blob/42e799446595c542eff9519353d3becc50cdba63/magicsea-staking/src/MlumStaking.sol#L397-L428

## Tool used

Manual Review

## Recommendation

The `addToPosition` function logic should be modified as follows:

```diff
function addToPosition(uint256 tokenId, uint256 amountToAdd) external override nonReentrant {
        _requireOnlyOperatorOrOwnerOf(tokenId);
        require(amountToAdd > 0, "0 amount"); // addToPosition: amount cannot be null

        _updatePool();
        address nftOwner = ERC721Upgradeable.ownerOf(tokenId);
        _harvestPosition(tokenId, nftOwner);

        StakingPosition storage position = _stakingPositions[tokenId];

+        amountToAdd = _transferSupportingFeeOnTransfer(stakedToken, msg.sender, amountToAdd);
        // we calculate the avg lock time:
        // lock_duration = (remainin_lock_time * staked_amount + amount_to_add * inital_lock_duration) / (staked_amount + amount_to_add)
        uint256 remainingLockTime = _remainingLockTime(position);
        uint256 avgDuration = (remainingLockTime * position.amount + amountToAdd * position.initialLockDuration)
            / (position.amount + amountToAdd);

        position.startLockTime = _currentBlockTimestamp();
        position.lockDuration = avgDuration;

        // lock multiplier stays the same
        position.lockMultiplier = getMultiplierByLockDuration(position.initialLockDuration);

        // handle tokens with transfer tax
-        amountToAdd = _transferSupportingFeeOnTransfer(stakedToken, msg.sender, amountToAdd);

        // update position
        position.amount = position.amount + amountToAdd;
        _stakedSupply = _stakedSupply + amountToAdd;
        _updateBoostMultiplierInfoAndRewardDebt(position);

        emit AddToPosition(tokenId, msg.sender, amountToAdd);
}
```

Use the actual transferred token amount to calculate `avgDuration`.
