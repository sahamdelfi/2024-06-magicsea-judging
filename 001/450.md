Elegant Vanilla Crane

High

# First user to open position can fully drain `MlumStaking` rewards

## Summary

The first user to open a position in `MlumStaking.sol` gets assigned all of the contract's reward tokens when using `harvestPosition()`.

## Vulnerability Detail

The `MlumStaking` contract allows user to stake their tokens and earn reward tokens proportional to the amount of time they have staked. Users can stake any amount and set any lock duration for their stakes, they are also allowed to harvest their rewards at any time. The issue arises from the way `_accRewardsPerShare` is initially set. For the first user that opens a position using `createPosition()`, `_accRewardsPerShare` will be `0`, because `_updatePools()`, will return early, due to `_stakedSupply == 0`.

```solidity
    function _updatePool() internal {
        uint256 accRewardsPerShare = _accRewardsPerShare;
        uint256 rewardBalance = rewardToken.balanceOf(address(this));
        uint256 lastRewardBalance = _lastRewardBalance;

        // recompute accRewardsPerShare if not up to date
 @>     if (lastRewardBalance == rewardBalance || _stakedSupply == 0) { // returns early for first user
            return;
        }

        uint256 accruedReward = rewardBalance - lastRewardBalance;
@>      _accRewardsPerShare = // no update happens for first user
            accRewardsPerShare + ((accruedReward * (PRECISION_FACTOR)) / (_stakedSupplyWithMultiplier));

        _lastRewardBalance = rewardBalance;

        emit PoolUpdated(_currentBlockTimestamp(), accRewardsPerShare);
    }
```

This will lead to opening a position where the `rewardDebt` is `0`:

```solidity
function createPosition(uint256 amount, uint256 lockDuration) external override nonReentrant {
        // no new lock can be set if the pool has been unlocked
        if (isUnlocked()) {
            require(lockDuration == 0, "locks disabled");
        }

        _updatePool();

        __SNIP__

        // create position
        _stakingPositions[currentTokenId] = StakingPosition({
            initialLockDuration: lockDuration,
            amount: amount,
@>          rewardDebt: amountWithMultiplier * (_accRewardsPerShare) / (PRECISION_FACTOR), // 0 rewardDebt for first user
            lockDuration: lockDuration,
            startLockTime: _currentBlockTimestamp(),
            lockMultiplier: lockMultiplier,
            amountWithMultiplier: amountWithMultiplier,
            totalMultiplier: lockMultiplier
        });

        // update total lp supply
 @>     _stakedSupply = _stakedSupply + amount; // global staked supply is increased, meaning that on the next update pool _accRewardsPerShare will be increased
        _stakedSupplyWithMultiplier = _stakedSupplyWithMultiplier + amountWithMultiplier;

        emit CreatePosition(currentTokenId, amount, lockDuration);
    }
```

Now, if the user uses `harvestPosition()` to claim the rewards, they will receive all the rewards accumulated in the contract. This is because `harvestPosition()` calls `_updatePools()`, which now will update `_accRewardsPerShare`, because `_stakedSupply` is no longer `0`. `_accRewardsPerShare` will be:

```solidity
_accRewardsPerShare = accRewardsPerShare + ((accruedReward * (PRECISION_FACTOR)) / (_stakedSupplyWithMultiplier));
```

Where `accruedReward` will be the total reward token in the contract as it is calculated as `uint256 rewardBalance = rewardToken.balanceOf(address(this));`. `PRECISION_FACTOR` can be quite large reaching values around `1e24` and `_stakedSupplyWithMultiplier` will be something in the range of `~e18`. If the reward token is for example `USDC (Xe6)` and the staking token is `MLUM (Ye18)`, we will have the following:

```solidity
_accRewardsPerShare = 0 (initially set as 0) + ((Xe6 * (1e24)) / (Ye18)); = (X/Y)e12;
```

Now, in the `harvestPosition()` function, the `pending` amount will be calculated as:

```solidity
function _harvestPosition(uint256 tokenId, address to) internal {
        StakingPosition storage position = _stakingPositions[tokenId];

        // compute position's pending rewards
 @>     uint256 pending = position.amountWithMultiplier * _accRewardsPerShare / PRECISION_FACTOR - position.rewardDebt;

        // transfer rewards
        if (pending > 0) {
            // send rewards
            _safeRewardTransfer(to, pending);
        }
        emit HarvestPosition(tokenId, to, pending);
    }
```

`pending` will be:

```solidity
pending = Ye18 * (X/Y)e12 / 1e24 - 0 = Ye18 * (X/Y) / 1e12 = (XY/Y) * 1e6 = Xe6; // pending rewards equal total amount of reward tokens of the contract
```

Even if we have another user who joins the staking contract, the first user will always get the full reward. When a second user joins, `_accRewardsPerShare` will be calculated as non-zero, at the very beginning, meaning that the `rewardDebt` will be non-zero, more specifically being:

```solidity
rewardDebt: amountWithMultiplier * (_accRewardsPerShare) / (PRECISION_FACTOR)
```

If we look at the pending amount calculated in the `harvestPosition()` function, we can see that the `rewardDebt` is subtracted from the pending amount:

```solidity
uint256 pending = position.amountWithMultiplier * _accRewardsPerShare / PRECISION_FACTOR - position.rewardDebt;
->
pending = position.amountWithMultiplier * _accRewardsPerShare / PRECISION_FACTOR - amountWithMultiplier * (_accRewardsPerShare) / (PRECISION_FACTOR);
->
pending = X - X = 0; // always zero for the second user, and pretty much every user after that
```

Even worse, the first user can open positions with amounts as low as `1 wei` and lock for a duration as low as `1 second`.

## Impact

First user to stake will receive all of the rewards, leaving all other users with 0 rewards.

## Code Snippet

https://github.com/sherlock-audit/2024-06-magicsea/blob/42e799446595c542eff9519353d3becc50cdba63/magicsea-staking/src/MlumStaking.sol#L377
https://github.com/sherlock-audit/2024-06-magicsea/blob/42e799446595c542eff9519353d3becc50cdba63/magicsea-staking/src/MlumStaking.sol#L387
https://github.com/sherlock-audit/2024-06-magicsea/blob/42e799446595c542eff9519353d3becc50cdba63/magicsea-staking/src/MlumStaking.sol#L585
https://github.com/sherlock-audit/2024-06-magicsea/blob/42e799446595c542eff9519353d3becc50cdba63/magicsea-staking/src/MlumStaking.sol#L678

## Tool used

Manual Review

## Recommendation

Mitigating this issue will require complex re-working of the overall logic regarding opening new positions, with focus on how the first user opens a position. The first thing we suggest is that `_accRewardsPerShare` is properly calculated for the first user before creating his position NFT, so that the `rewardDebt` is properly taken into account. Another thing that needs to be added is a check for a minium `lockDuration` when using `createPosition()` otherwise the lock multiplier will alayws be 0 for specific values:

```solidity
function getMultiplierByLockDuration(uint256 lockDuration) public view returns (uint256) {
    // in case of emergency unlock
    if (isUnlocked()) return 0;

    if (_maxLockDuration == 0 || lockDuration == 0) return 0;

    // capped to maxLockDuration
    if (lockDuration >= _maxLockDuration) return _maxLockMultiplier;

@>  return (_maxLockMultiplier * lockDuration) / (_maxLockDuration); // will be 0 for lockDuration < _maxLockDuration /_maxLockMultiplier
}
```

There is also another discrepency which when combined with the above leads to inacurate calculations. Basically, because of the calculation of `amountWithMultiplier` in [here](https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/MlumStaking.sol#L371) if `lockMultiplier` is 0, then `amountWithMultiplier` won't be `0` but will be `1`, which will cause issues further as `_stakedSupplyWithMultiplier` will be set to `1`, instread of being 0. Because of this `_accRewardsPerShare` will be inflated in `updatePools()`, leading to inacurate reward calculation in the `harvestPosition()` pending amount. If this is mitigated in the future an additional check should be added in [`updatePools()`](https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/MlumStaking.sol#L580) to check if `_stakedSupplyWithMultiplier == 0`, so that there is no zero-division in `updatePools()`.
