Sticky Hickory Hare

Medium

# Malicious `BribeRewarder`s with useless reward tokens can be created to fill rewarders of all voting epochs

## Summary
anyone is able to create a `BribeRewarder` with an arbitrary token and register it in `Voter` contract. Since there are no checks to ensure that a `BribeRewarder` is using a valid ERC20 reward token a malicious actor might exploit this by creating multiple **dummy** bribe rewarders with a useless ERC20 token and then register them in voter contract for all of the pools and all of upcoming epochs.

## Vulnerability Detail
`RewarderFactory::createBribeRewarder` allows any address to create a `BribeRewarder` with an arbitrary `token` as the reward token:
https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/rewarders/RewarderFactory.sol#L109-L113
```solidity
    function createBribeRewarder(
        IERC20 token, //@audit no checks for this, can be anything
        address pool
    ) external returns (IBribeRewarder rewarder) {
        rewarder = IBribeRewarder(
            _cloneBribe(RewarderType.BribeRewarder, token, pool)
        );
        emit BribeRewarderCreated(
            RewarderType.BribeRewarder,
            token,
            pool,
            rewarder
        );
    }
```
As we can see, there are no validations for `token` parameter even inside `_cloneBribe` (it can be any address):
https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/rewarders/RewarderFactory.sol#L152-L173
```solidity
    function _cloneBribe(RewarderType rewarderType, IERC20 token, address pool)
        private
        returns (IBribeRewarder rewarder)
    {
        //...
        //@audit token is used here to form the initialization calldata, other than that, no additional checks for this token
        bytes memory immutableData = abi.encodePacked(token, pool);
       //...
    }
```
To register this rewarder in `voter` contract, owner only needs to call `fundAndBribe(startId, lastId, amountPerPeriod)`:
https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/rewarders/BribeRewarder.sol#L123
```solidity
    function fundAndBribe(
        uint256 startId,
        uint256 lastId,
        uint256 amountPerPeriod
    ) external payable onlyOwner {
        //...
        _bribe(startId, lastId, amountPerPeriod);
    }
```
which in result calls `voter::onRegister`:
https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/rewarders/BribeRewarder.sol#L255
```solidity
    function _bribe(
        uint256 startId,
        uint256 lastId,
        uint256 amountPerPeriod
    ) internal {
        //...
        //@audit register the contract on voter
        IVoter(_caller).onRegister();
       //...
    }
```
if we look at `Voter::onRegister` function, we see that its only checking whether `msg.sender` is registered within `RewarderFactory` with a type of `BribeRewarder`:
https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/Voter.sol#L133
```solidity
    function onRegister() external override {
        IBribeRewarder rewarder = IBribeRewarder(msg.sender);
         //...
         //@audit checking rewarder
        _checkRegisterCaller(rewarder);
       //...
    }
```
`_checkRegisterCaller:`
https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/Voter.sol#L427-L431
```solidity
    //@audit get reward type from factory and check whether it is BribeRewarder
    function _checkRegisterCaller(IBribeRewarder rewarder) internal view {
        if (_rewarderFactory.getRewarderType(rewarder) != IRewarderFactory.RewarderType.BribeRewarder) {
            revert Voter__InvalidRegisterCaller();
        }
    }
```
`RewarderFactory::getRewarderType` returns type of rewarder:
https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/rewarders/RewarderFactory.sol#L84-L86
```solidity
    function getRewarderType(IRewarder rewarder) external view returns (RewarderType) {
        return _rewarderTypes[rewarder];
    }
```
if we look at `_cloneBribe`, we see that `getRewarderType` returns `BribeRewarder` type for our newly created rewarder, which means that there are no limitations for registering this rewarder with the arbitrary `token` into `Voter`:
```solidity
    function _cloneBribe(RewarderType rewarderType, IERC20 token, address pool)
        private
        returns (IBribeRewarder rewarder)
    {
         //...
        //@audit rewarder is created here
        //@audit BribeRewarder type is assigned to it, which makes it eligible to be added into Voter contract
        _rewarderTypes[rewarder] = rewarderType;
       //...
    }
```
## Impact
Lets assume that there are `n` pools registered in `Voter` and users are going to vote for this pools in upcoming epochs.
A malicious actor deploys a trash ERC20 token with an unlimited supply:
```solidity
contract TrashERC20 is ERC20 {
  constructor() ERC20("Dummy", "D") {
          _mint(msg.sender, ~uint256(0));
   }
}
```
and then creates `n * 5` (since there could be a maximum of 5 rewarders for each pool in each epoch) different `BribeRewarder`s using this trash token (we can ignore the gas spent to deploy all of this, since we are using IOTA chain):
```js
   let pools = [pool_1, pool_2, ..., pool_n]
   pools.forEach(p => {
        for(let i; i < 5; i++) {
             const tx = await rewarderFactory.createBribeRewarder(trash_token_address, p);
             await tx.wait(1)
        }
   });
```
then registers all of dummy rewarders in `Voter` for all pools and for all upcoming epochs:
```js
   let dummy_rewarders = [rewarder_1, rewarder_2, ..., rewarderl_n*5]
   dummy_rewarders.forEach(r => {
        const tx = await r.fundAndBribe(next_epoch_id, next_epoch_id + n, 100_000_00 ethers);
        await tx.wait(1)
   });
```
finally, `Voter` is in a state where voters don't receive any valuable rewards. Team needs to redeploy `Voter` contract in order to fix this issue or move the current epoch id high enough so that there are no more rewarders (because there is no way to remove rewarders)

## Code Snippet

## Tool used

Manual Review

## Recommendation
when creating a `BribeRewarder`, the `token` should be chosen from a set of tokens validated by developers:
```solidity
    function _cloneBribe(RewarderType rewarderType, IERC20 token, address pool)
        private
        returns (IBribeRewarder rewarder)
    {
        //...
        //@audit check whether token is validated by team
        require(isValidToken[token], "This token is not allowed");
       //...
    }
```
also add this function to `RewarderFactory` to enable/disable tokens:
```solidity
    function setTokenIsValid(IERC20 token, bool _status)
        external
        onlyOwner
    {
        isValidToken[token] = _status;
    }
```
