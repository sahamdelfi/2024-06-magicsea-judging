Sneaky Neon Mole

High

# Improper Time Accounting Can Set All Rewards To Zero

## Summary

The `_calculateRewards` function fails to return the correct rewards under several scenarios. If the last update is performed after previous deposits all those deposit can be accounted as zero rewards.

## Vulnerability Detail

If the global state variable `lastUpdateTimestamp` is bigger tor equal than the local variable `timestamp` on [BribeRewarder.sol#L312](https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/rewarders/BribeRewarder.sol#L312) the `_calculateRewards` function will return zero rewards. *The local variable timestamp is not necessarily the current timestamp*. This scenario does happen whenever a user perform a voting in a more advanced period than the previous voters. Setting the previous voter rewards to zero.

### Secenario 1: Users are able to vote and get rewards

1. Create valid positions for both users
2. Advance to period 1
3. Create bribe rewarder for periods 2 to 4 and register it
4. Advance to period 2
5. Vote with user0. Observe deposit event.
6. Vote with user1. Observe deposit event.
7. Advance to period 5
8. Claim rewards with user0. Get rewards
9. Claim rewards with user1. Get rewards
```solidity
    function test_zeroRewardBribeDistribution() public {
        address user0 = makeAddr('user0');
        address user1 = makeAddr('user1');
        _createPosition(user0, 100 ether, 90 days);        // create position for user0
        _createPosition(user1, 100 ether, 90 days);        // create position for user1
        vm.warp(1 days);
        voter.startNewVotingPeriod();               // PERIOD 1
        IBribeRewarder rewarder = rewarderFactory.createBribeRewarder(dai, address(daiPool));
        dai.mint(address(rewarder), 1000 ether);
        rewarder.bribe(2, 4, 200 ether);
        address[] memory pools = new address[](1);
        uint256[] memory amounts = new uint256[](1);
        pools[0] = address(daiPool);
        amounts[0] = 100 ether;
        vm.warp(block.timestamp + 13 days);
        voter.startNewVotingPeriod();               // PERIOD 2
        //User 1 votes and gets rewards
        vm.prank(user0);
        voter.vote(1, pools, amounts);
        //User 2 votes and gets rewards
        vm.prank(user1);
        voter.vote(2, pools, amounts);
        vm.warp(block.timestamp + period);
        voter.startNewVotingPeriod();               // PERIOD 3
        vm.warp(block.timestamp + period);
        voter.startNewVotingPeriod();
        vm.warp(block.timestamp + period);
        voter.startNewVotingPeriod();               // PERIOD 4
        vm.warp(block.timestamp + period);
        voter.startNewVotingPeriod();               // PERIOD 5 No more rewards to distribute
        vm.prank(user0);
        rewarder.claim(1);                                   // User0 claim rewards and gets 99999999999999791995
        vm.prank(user1);
        rewarder.claim(2);                                  // User1 claim rewards and gets 99999999999999791995
    }
```

### Scenario 2: Users vote but get zero rewards

1. Create valid positions for both users
2. Advance to period 1
3. Create bribe rewarder for periods 2 to 4 and register it
4. Advance to period 2
5. Vote with user0. Observe deposit event.
6. Advance to period 3
7. Vote with user1. Observe deposit event.
8. Advance to period 5.
9. Claim rewards with user0. Get 0 rewards
10. Claim rewards with user1. Get 0 rewards
```solidity
    function test_zeroRewardBribeDistribution() public {
        address user0 = makeAddr('user0');
        address user1 = makeAddr('user1');
        _createPosition(user0, 100 ether, 90 days);        // create position for user0
        _createPosition(user1, 100 ether, 90 days);        // create position for user1
        vm.warp(1 days);
        voter.startNewVotingPeriod();               // PERIOD 1
        IBribeRewarder rewarder = rewarderFactory.createBribeRewarder(dai, address(daiPool));
        dai.mint(address(rewarder), 1000 ether);
        rewarder.bribe(2, 4, 200 ether);
        address[] memory pools = new address[](1);
        uint256[] memory amounts = new uint256[](1);
        pools[0] = address(daiPool);
        amounts[0] = 100 ether;
        vm.warp(block.timestamp + 13 days);
        voter.startNewVotingPeriod();               // PERIOD 2
        //User0 votes and gets rewards
        vm.prank(user0);
        voter.vote(1, pools, amounts);
        vm.warp(block.timestamp + period);
        voter.startNewVotingPeriod();               // PERIOD 3
        vm.warp(block.timestamp + period);
        voter.startNewVotingPeriod();
        //User1 votes and gets rewards
        vm.prank(user1);                                     // Now User1 votes on 3 period
        voter.vote(2, pools, amounts);
        vm.warp(block.timestamp + period);
        voter.startNewVotingPeriod();               // PERIOD 4
        vm.warp(block.timestamp + period);
        voter.startNewVotingPeriod();               // PERIOD 5 No more rewards to distribute
        vm.prank(user0);
        rewarder.claim(1);                                   // User0 claim rewards and gets 0
        vm.prank(user1);
        rewarder.claim(2);                                  // User1 claim rewards and gets 0
    }
```

## Impact

This vulnerability heavily impacts on the rewards distribution to the users.

## Code Snippet

[BribeRewarder.sol#L300-L313](https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/rewarders/BribeRewarder.sol#L300-L313)
```solidity
  function _calculateRewards(uint256 periodId) internal view returns (uint256) {
      (uint256 startTime, uint256 endTime) = IVoter(_caller).getPeriodStartEndtime(periodId);

      if (endTime == 0 || startTime > block.timestamp) {
          return 0;
      }

      uint256 duration = endTime - startTime;
      uint256 emissionsPerSecond = _amountPerPeriod / duration;

      uint256 lastUpdateTimestamp = _lastUpdateTimestamp;
      uint256 timestamp = block.timestamp > endTime ? endTime : block.timestamp;
      return timestamp > lastUpdateTimestamp ? (timestamp - lastUpdateTimestamp) * emissionsPerSecond : 0;
  }
```

[](https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/rewarders/BribeRewarder.sol#L248-L251)
```solidity
  uint256 bribeEpochs = _calcPeriods(startId, lastId);
  for (uint256 i = 0; i <= bribeEpochs; ++i) {
      _rewards.push();
  }
```

## Tool used

Manual Review

## Recommendation

There are several valid fixes, however it depends on how the protocol wants to proceed. In any case reconsider the `_calculateRewards` function and the reward distribution time updates. Also do not allow the `_rewards` array to be bigger than it should.
