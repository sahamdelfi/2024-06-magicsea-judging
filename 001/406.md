Glorious Bronze Ladybug

Medium

# `addToPosition` could be call by anyone

## Summary

The function `MlumStaking::addToPosition` is intended to be callable only by the owner or an authorized operator of a position. However, the check implemented in `MlumStaking::_requireOnlyOperatorOrOwnerOf` use `ERC721Upgradeable::_isAuthorized` incorrectly. As a result, anyone can add to a position.

## Vulnerability Detail

In `MlumStaking::_requireOnlyOperatorOrOwnerOf`, the parameters passed to `ERC721Upgradeable::_isAuthorized` are `msg.sender` as the owner, `msg.sender` as the spender, and `tokenId`. Due to the internal logic of `_isAuthorized`, this will always return true because it checks if the spender is the owner, which is always true in this case.

https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/MlumStaking.sol#L397-L405

https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/MlumStaking.sol#L140C5-L143C6

### POC
Add this test to `MlumStaking.t.sol`

```solidity
function testCreatePositionAndThenAddToPositionWithUserWithoutPermissions()
        public
    {
        _rewardToken.mint(address(_pool), 100_000_000);
        _stakingToken.mint(ALICE, 2 ether);
        _stakingToken.mint(MARK, 2 ether);

        vm.startPrank(ALICE);
        _stakingToken.approve(address(_pool), 1 ether);
        _pool.createPosition(1 ether, 1 days);
        vm.stopPrank();

        assertEq(ERC721(address(_pool)).ownerOf(1), ALICE);

        skip(3600);

        vm.prank(ALICE);
        _pool.harvestPosition(1);

        assertGt(_rewardToken.balanceOf(ALICE), 0);

        vm.startPrank(MARK);

        _stakingToken.approve(address(_pool), 1 ether);

        vm.expectRevert(bytes("FORBIDDEN"));
        _pool.addToPosition(1, 0.5 ether);

        vm.stopPrank();
    }
```
## Impact

Malicious users could exploit this vulnerability to interfere with other users' positions. For example, a position that is locked for a long period with a small amount could be targeted. A malicious user could easily stake the same amount, thereby increasing the position's lock duration without the owner's permission. This could cause significant inconvenience and potential losses to legitimate users.

## Code Snippet

```solidity
function _requireOnlyOperatorOrOwnerOf(uint256 tokenId) internal view {
        // isApprovedOrOwner: caller has no rights on token
        require(ERC721Upgradeable._isAuthorized(msg.sender, msg.sender, tokenId), "FORBIDDEN");
    }
```

## Tool used

Manual Review

## Recommendation

Here is a potential fix:

```diff
function _requireOnlyOperatorOrOwnerOf(uint256 tokenId) internal view {
+   address nftOwner = ERC721Upgradeable.ownerOf(tokenId);

    // isApprovedOrOwner: caller has no rights on token
    require(
+        ERC721Upgradeable._isAuthorized(nftOwner, msg.sender, tokenId),
-        ERC721Upgradeable._isAuthorized(msg.sender, msg.sender, tokenId),
         "FORBIDDEN"
    );
}
```

