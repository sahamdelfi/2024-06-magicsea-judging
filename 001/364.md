Salty Sky Caribou

High

# Users can claim rewards for voting periods that they didn't participated in

## Summary
In the BribeRewarder contract, the current design allows a user to claim rewards for periods they did not vote in, as long as they had votes recorded from previous periods. This happens because the reward calculation in _modify and the use of Rewarder2.update consider a user's balance and accumulated rewards, even if no new votes were cast in the current period.
## Vulnerability Detail
- When a user votes in the Voter contract, the deposit() function is called in the BribeRewarder, recording their votes for the active period.
- The user can claim rewards for the active period by calling claim(), which calculates and transfers the accumulated rewards.

However, If a user does not vote in a subsequent period, their balance from previous periods is still considered in the _modify() function.
This happens because the Rewarder2.update() function calculates rewards based on the old and new balance, total supply, and total rewards, without checking if the user voted in the current period.

Let's consider the following scenario and we'll go over the code:
- A user votes with 100 tokens in period1
- Period1 ends, user claims his bribe rewards, period2 begins
- Period2 ends, user claims rewards again and receives rewards for period2 even though he didn't vote in it

When a user votes for a period, the deposit() function is called:
```solidity
    function deposit(uint256 periodId, uint256 tokenId, uint256 deltaAmount) public onlyVoter {
        _modify(periodId, tokenId, deltaAmount.toInt256(), false);

        emit Deposited(periodId, tokenId, _pool(), deltaAmount);
    }
```
which calls modify:
```solidity
function _modify(uint256 periodId, uint256 tokenId, int256 deltaAmount, bool isPayOutReward)
        private
        returns (uint256 rewardAmount)
    {
        if (!IVoter(_caller).ownerOf(tokenId, msg.sender)) {
            revert BribeRewarder__NotOwner();
        }

        // extra check so we dont calc rewards before starttime
        (uint256 startTime,) = IVoter(_caller).getPeriodStartEndtime(periodId);
        if (block.timestamp <= startTime) {
            _lastUpdateTimestamp = startTime;
        }

        RewardPerPeriod storage reward = _rewards[_indexByPeriodId(periodId)];
        Amounts.Parameter storage amounts = reward.userVotes;
        Rewarder2.Parameter storage rewarder = reward.rewarder;

        (uint256 oldBalance, uint256 newBalance, uint256 oldTotalSupply,) = amounts.update(tokenId, deltaAmount);

        uint256 totalRewards = _calculateRewards(periodId);

        rewardAmount = rewarder.update(bytes32(tokenId), oldBalance, newBalance, oldTotalSupply, totalRewards);

        if (block.timestamp > _lastUpdateTimestamp) {
            _lastUpdateTimestamp = block.timestamp;
        }

        if (isPayOutReward) {
            rewardAmount = rewardAmount + unclaimedRewards[periodId][tokenId];
            unclaimedRewards[periodId][tokenId] = 0;
            if (rewardAmount > 0) {
                IERC20 token = _token();
                _safeTransferTo(token, msg.sender, rewardAmount);
            }
        } else {
            unclaimedRewards[periodId][tokenId] += rewardAmount;
        }
    }
```
and ` unclaimedRewards[periodId][tokenId]` gets increased. The problem comes from the fact that even if ` unclaimedRewards[periodId][tokenId]` is 0 in subsequent periods, the `rewardAmount` is still calculated with the same balance we used to have:
```solidity
rewardAmount = rewarder.update(bytes32(tokenId), oldBalance, newBalance, oldTotalSupply, totalRewards);
```
And if the user is claiming, he'll enter the if:
```solidity
        if (isPayOutReward) {
            rewardAmount = rewardAmount + unclaimedRewards[periodId][tokenId];
            unclaimedRewards[periodId][tokenId] = 0;
            if (rewardAmount > 0) {
                IERC20 token = _token();
                _safeTransferTo(token, msg.sender, rewardAmount);
            }
```
and the `rewardAmount` will be as calculated here:
```solidity
rewardAmount = rewarder.update(bytes32(tokenId), oldBalance, newBalance, oldTotalSupply, totalRewards);
```
As you can see, claiming rewards for previous periods doesn't impact the balance of tokens a user has, therefore including votes(tokens) that were casted in previous periods in the calculations for rewards of the current period.

You can see the calculations for the `rewardAmount` and the calculation is always the same as long as he casted some votes previously, doesn't matter if a user has voted in the current period or not:
```solidity
    function update(
        Parameter storage rewarder,
        bytes32 id,
        uint256 oldBalance,
        uint256 newBalance,
        uint256 totalSupply,
        uint256 totalRewards
    ) internal returns (uint256 rewards) {
        uint256 accDebtPerShare = updateAccDebtPerShare(rewarder, totalSupply, totalRewards);

        rewards = oldBalance == 0 ? 0 : getDebt(accDebtPerShare, oldBalance) - rewarder.debt[id];

        rewarder.debt[id] = getDebt(accDebtPerShare, newBalance);
    }
```
## Impact
Users are able to claim bribe rewards for periods they haven't participated in, as long as they have participated in at least 1 previous period.
## Code Snippet
https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/rewarders/BribeRewarder.sol#L282
## Tool used

Manual Review

## Recommendation
Check in the claim() function if a user has participated in the current periodId. You already have a `hasVoted` function in Voter.sol. So you can modify the claim function like this:
```solidity
    function claim(uint256 tokenId) external override {
        uint256 endPeriod = IVoter(_caller).getLatestFinishedPeriod();

        uint256 totalAmount;

        // calc emission per period cause every period can every other durations
        for (uint256 i = _startVotingPeriod; i <= endPeriod; ++i) {

            if(IVoter(_caller).hasVoted) {
            totalAmount += _modify(i, tokenId, 0, true);
            }

        }

        emit Claimed(tokenId, _pool(), totalAmount);
    }
```