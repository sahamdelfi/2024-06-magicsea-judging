Tall Porcelain Puppy

Medium

# Unclaimed Bribe Rewards Remain Stuck in `BribeRewarder`

## Summary
Unclaimed bribe rewards should be returned to the briber after the next voting period ends. However, there is no way for the briber to retrieve those funds from the `BribeRewarder` contract, causing the funds to remain stuck inside the contract.

## Vulnerability Detail
When a new Bribe contract is registered to the Voter contract, it is configured for a range of voting periods as specified by `getBribePeriods()`. After that range, bribe rewards will no longer be distributed upon voting.

According to the [protocol documentation](https://docs.magicsea.finance/protocol/magic/magic-lum-voting#bribes):

> Bribes as an additional incentive to vote can be claimed 24-48 hours after an epoch has ended. Voters can claim the rewards until the next epoch ends. Unclaimed rewards will be sent back to the briber.

Based on this, voters can call the `BribeRewarder.claim` function to claim their rewards until the next voting period ends. After that, rewards can no longer be claimed because any call to the function will revert due to the bribe rewards not being set up for that next epoch when the bribe was created (the function will revert with an `_rewards` array index out of range error).

```solidity
function claim(uint256 tokenId) external override {
    uint256 endPeriod = IVoter(_caller).getLatestFinishedPeriod();

    uint256 totalAmount;

    // calculate emission per period as every period can have different durations
    for (uint256 i = _startVotingPeriod; i <= endPeriod; ++i) {
        totalAmount += _modify(i, tokenId, 0, true);
    }

    emit Claimed(tokenId, _pool(), totalAmount);
}
```

```solidity
function _modify(uint256 periodId, uint256 tokenId, int256 deltaAmount, bool isPayOutReward)
    private
    returns (uint256 rewardAmount)
{   
    if (!IVoter(_caller).ownerOf(tokenId, msg.sender)) {
        revert BribeRewarder__NotOwner();
    }

    // extra check so we don't calculate rewards before start time
    (uint256 startTime,) = IVoter(_caller).getPeriodStartEndtime(periodId);
    if (block.timestamp <= startTime) {
        _lastUpdateTimestamp = startTime;
    }

    //@audit will revert with index out of range for periodId after the last epoch
    RewardPerPeriod storage reward = _rewards[_indexByPeriodId(periodId)];

    ...
}
```

After the next voting period ends, unclaimed bribe rewards will remain in the `BribeRewarder` contract. These should be returned to the briber (the account that deployed/owns the Bribe contract), as mentioned in the documentation. However, there is no function that allows the briber to withdraw those funds, leaving them stuck inside the Bribe contract.

## Impact
Unclaimed bribe rewards will remain stuck inside the `BribeRewarder` contract and cannot be sent back to the briber.

## Code Snippet

https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/rewarders/BribeRewarder.sol#L153-L164

https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/rewarders/BribeRewarder.sol#L189-L199

https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/rewarders/BribeRewarder.sol#L260-L274

## Tool used

Manual Review

## Recommendation
To address this issue, a withdraw function should be added to the `BribeRewarder` contract to allow the briber to retrieve the unclaimed reward funds after the next epoch ends (when all bribe reward epochs have ended). The following function should be added:

```solidity
function withdraw() external onlyOwner {
    uint256 endPeriod = IVoter(_caller).getLatestFinishedPeriod();
    if (endPeriod < _lastVotingPeriod + 1) revert BribeRewarder__EpochsNotEnded();

    IERC20 token = _token();
    uint256 balance = _balanceOfThis(token);
    if (balance == 0) revert BribeRewarder__InsufficientFunds();
    _safeTransferTo(token, msg.sender, balance);
}
```