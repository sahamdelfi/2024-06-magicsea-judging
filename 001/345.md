Fluffy Arctic Hippo

Medium

# Miscalculation of `avgDuration` in `MlumStaking::addToPosition` Causes Extended Lock Periods

## Summary
Even though `MlumStaking` supports fee-transfer-token it does not correctly calculate `lockDuration` when it is recalculated by adding more to the position. This recalculated duration is higher than expected. 

## Vulnerability Detail
When adding more tokens to an existing position using `MlumStaking::addToPosition`, the `avgDuration` is recalculated based on the amount being added. The calculation does not account for the transfer fee, leading to an incorrect `avgDuration`:

```solidity
function addToPosition(uint256 tokenId, uint256 amountToAdd) external override nonReentrant {
        _requireOnlyOperatorOrOwnerOf(tokenId);
        require(amountToAdd > 0, "0 amount"); // addToPosition: amount cannot be null

        _updatePool();
        address nftOwner = ERC721Upgradeable.ownerOf(tokenId);
        _harvestPosition(tokenId, nftOwner);

        StakingPosition storage position = _stakingPositions[tokenId];

        // we calculate the avg lock time:
        // lock_duration = (remainin_lock_time * staked_amount + amount_to_add * inital_lock_duration) / (staked_amount + amount_to_add)
        uint256 remainingLockTime = _remainingLockTime(position);
@>      uint256 avgDuration = (remainingLockTime * position.amount + amountToAdd * position.initialLockDuration)
            / (position.amount + amountToAdd);

        position.startLockTime = _currentBlockTimestamp();
        position.lockDuration = avgDuration;

        // lock multiplier stays the same
        position.lockMultiplier = getMultiplierByLockDuration(position.initialLockDuration);

        // handle tokens with transfer tax
        amountToAdd = _transferSupportingFeeOnTransfer(stakedToken, msg.sender, amountToAdd);

        // update position
        position.amount = position.amount + amountToAdd;
        _stakedSupply = _stakedSupply + amountToAdd;
        _updateBoostMultiplierInfoAndRewardDebt(position);

        emit AddToPosition(tokenId, msg.sender, amountToAdd);
    }
```
The `avgDuration` is recalculated based on the `amountToAdd` before accounting for the transfer fee. If the token has a fee-on-transfer, the actual amount added is less, leading to a miscalculated `avgDuration`.

**Scenario 1: Larger amountToAdd (no fee)**
Let's assume:
* remainingLockTime = 1000
* position.amount = 500
* amountToAdd_no_fee = 300
* position.initialLockDuration = 2000

avgDuration = (1000 * 500 + 300 * 2000) /  (500 +300)
avgDuration =  (500000 + 600000 ) / 800 = 1375

**Scenario 2: amountToAdd after transfer fee**
Assuming a fee of 10%:
* amountToAdd_with_fee = amountToAdd_no_fee * (1 - 0.10) = 300 * 0.90 = 270

avgDuration = (1000 * 500 + 270 * 2000) /  (500 + 270)
avgDuration = ( 500000 + 540000) / 770 = 1350

This discrepancy demonstrates that the calculation is misleading when fee-on-transfer tokens are used.

## Impact
The lock position lasts longer than expected when using fee-on-transfer tokens, not precise functionality

## Code Snippet
https://github.com/sherlock-audit/2024-06-magicsea/blob/7fd1a65b76d50f1bf2555c699ef06cde2b646674/magicsea-staking/src/MlumStaking.sol#L410

## Tool used

Manual Review

## Recommendation
Use the `amountToAdd` after the transfer has been processed to ensure accurate calculation of `avgDuration`:

```diff
    function addToPosition(uint256 tokenId, uint256 amountToAdd) external override nonReentrant {
        _requireOnlyOperatorOrOwnerOf(tokenId);
        require(amountToAdd > 0, "0 amount"); // addToPosition: amount cannot be null

        _updatePool();
        address nftOwner = ERC721Upgradeable.ownerOf(tokenId);
        _harvestPosition(tokenId, nftOwner);

        StakingPosition storage position = _stakingPositions[tokenId];
+       // handle tokens with transfer tax
+        amountToAdd = _transferSupportingFeeOnTransfer(stakedToken, msg.sender, amountToAdd);
        // we calculate the avg lock time:
        // lock_duration = (remainin_lock_time * staked_amount + amount_to_add * inital_lock_duration) / (staked_amount + amount_to_add)
        uint256 remainingLockTime = _remainingLockTime(position);
        uint256 avgDuration = (remainingLockTime * position.amount + amountToAdd * position.initialLockDuration)
            / (position.amount + amountToAdd);

        position.startLockTime = _currentBlockTimestamp();
        position.lockDuration = avgDuration;

        // lock multiplier stays the same
        position.lockMultiplier = getMultiplierByLockDuration(position.initialLockDuration);

-       // handle tokens with transfer tax
-       amountToAdd = _transferSupportingFeeOnTransfer(stakedToken, msg.sender, amountToAdd);

        // update position
        position.amount = position.amount + amountToAdd;
        _stakedSupply = _stakedSupply + amountToAdd;
        _updateBoostMultiplierInfoAndRewardDebt(position);

        emit AddToPosition(tokenId, msg.sender, amountToAdd);
    }
```
