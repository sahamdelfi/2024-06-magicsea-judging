Tall Porcelain Puppy

High

# `Voter.vote` Allows Users to Vote with Expired Locks, Introducing Voting Manipulation Risks

## Summary
The `Voter.vote` function allows users to vote with an expired lock, which introduces risks of voting manipulation where user can vote twice with same MLUM token amount in a voting period.

## Vulnerability Detail
When a user calls `Voter.vote` to vote, the function performs checks to ensure that the user has sufficient lock time:

```solidity
function vote(uint256 tokenId, address[] calldata pools, uint256[] calldata deltaAmounts) external {
    ...

    // check if _minimumLockTime >= initialLockDuration and it is locked
    if (_mlumStaking.getStakingPosition(tokenId).initialLockDuration < _minimumLockTime) {
        revert IVoter__InsufficientLockTime();
    }
    if (_mlumStaking.getStakingPosition(tokenId).lockDuration < _periodDuration) {
        revert IVoter__InsufficientLockTime();
    }

    ...
}
```

However, these checks only verify the initial lock duration and the lock duration (which also equals the initial lock duration unless the user has added a new amount to their staking position). The function does not check the actual remaining lock duration, which allows the user to vote with a lock that has already expired.

This issue goes against the protocol's intent of allowing only locks with a 90-day lock duration to be eligible for votes. More severely, it can be used to manipulate the voting process.

The following voting manipulation scenario could easily occur:

1. A user named Bob creates a lock for a duration of 90 days.
2. Bob waits until his lock has expired to call `Voter.vote` to vote on specific pools, he is able to do so as the function only checks `initialLockDuration` and `lockDuration` which are both equals to 90 days (but actual lock time remaining is 0).
3. After voting, Bob calls `withdrawFromPosition` (possible because his lock has already expired) to destroy his lock and withdraw all his staked amount.
4. Bob creates a new lock position for a 90-day duration by staking his withdrawn MLUM amount from the previously expired lock.
5. Bob can then vote again using his new lock position on the same pools he has voted on the first time (this is possible because votes are recorded for each tokenId in the `_hasVotedInPeriod` mapping).

In this scenario, Bob was able to vote twice in the same period using the same MLUM tokens for two staking positions. Thus, Bob was able to inflate the total votes for the pools he voted on twice, resulting in those pools having more rewards distributed upon vote closing.

Bob has successfully manipulated the voting during that specific epoch, and he can perform similar manipulation many times with different lock positions.

To summarize, the fact that `Voter.vote` doesn't check for the remaining lock duration allows malicious users to vote with an expired lock and then use the same staked amount to create another lock (after destroying the first one) and vote again, thus bypassing the `_hasVotedInPeriod` check and manipulating the voting process.

## Impact
Users could manipulate the voting mechanism and vote twice using the same staked MLUM tokens.

## Code Snippet

[https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/rewarders/BribeRewarder.sol#L143-L147

https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/rewarders/BribeRewarder.sol#L260-L267

https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/Voter.sol#L153-L219

## Tool used

Manual Review

## Recommendation
To avoid this kind of manipulation, the `Voter.vote` function must check that the remaining lock duration is always greater than the current voting period:

```diff
function vote(uint256 tokenId, address[] calldata pools, uint256[] calldata deltaAmounts) external {
    if (pools.length != deltaAmounts.length) revert IVoter__InvalidLength();

    // check voting started
    if (!_votingStarted()) revert IVoter_VotingPeriodNotStarted();
    if (_votingEnded()) revert IVoter_VotingPeriodEnded();

    // check ownership of tokenId
    if (_mlumStaking.ownerOf(tokenId) != msg.sender) {
        revert IVoter__NotOwner();
    }

    uint256 currentPeriodId = _currentVotingPeriodId;
    // check if already voted
    if (_hasVotedInPeriod[currentPeriodId][tokenId]) {
        revert IVoter__AlreadyVoted();
    }

    // check if _minimumLockTime >= initialLockDuration and it is locked
    if (_mlumStaking.getStakingPosition(tokenId).initialLockDuration < _minimumLockTime) {
        revert IVoter__InsufficientLockTime();
    }
--  if (_mlumStaking.getStakingPosition(tokenId).lockDuration < _periodDuration) {
--      revert IVoter__InsufficientLockTime();
--  }
++  if (_mlumStaking.remainingLockTime(_mlumStaking.getStakingPosition(tokenId)) < _periodDuration) {
++      revert IVoter__InsufficientLockTime();
++  }

    ...
}
```

This will also require adding an external view function `remainingLockTime` in the `MlumStaking` contract to get the position's remaining lock duration:

```solidity
function remainingLockTime(StakingPosition memory position) external view returns (uint256) {
    return _remainingLockTime(position);
}
```