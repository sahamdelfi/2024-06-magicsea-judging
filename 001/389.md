Stable Grape Panda

High

# Users with `StakingPosition::initialLockDuration` >= 3 months get permanent voting permission

## Summary
Users could get permanent voting permission, due to the only restriction for StakingPosition to be valid(time based) for `Voter::vote` is `initialLockDuration >= 3 months`

## Vulnerability Detail
1. `Voter::vote` considers tokenId as valid(time based) for voting if `initialLockDuration >= 3 months` && `lockDuration >= 2 weeks`. 
```solidity
    function vote(uint256 tokenId, address[] calldata pools, uint256[] calldata deltaAmounts) external {

        .....

        // check if _minimumLockTime >= initialLockDuration and it is locked
        if (_mlumStaking.getStakingPosition(tokenId).initialLockDuration < _minimumLockTime) {
            revert IVoter__InsufficientLockTime();
        }
        if (_mlumStaking.getStakingPosition(tokenId).lockDuration < _periodDuration) {

        .....

    }
```
2. From the code in `MlumStaking` we can assume that always `initialLockDuration <= lockDuration`, so now the only restriction is that `initialLockDuration >= 3 months`
```solidity
    function createPosition(uint256 amount, uint256 lockDuration) external override nonReentrant {
        .....

        // create position
        _stakingPositions[currentTokenId] = StakingPosition({
            initialLockDuration: lockDuration,
            amount: amount,
            rewardDebt: amountWithMultiplier * (_accRewardsPerShare) / (PRECISION_FACTOR),
            lockDuration: lockDuration,
            startLockTime: _currentBlockTimestamp(),
            lockMultiplier: lockMultiplier,
            amountWithMultiplier: amountWithMultiplier,
            totalMultiplier: lockMultiplier
        });

        .....

    }

    function extendLockPosition(uint256 tokenId, uint256 lockDuration) external nonReentrant {
        _requireOnlyApprovedOrOwnerOf(tokenId);

        _updatePool();
        _lockPosition(tokenId, lockDuration, true);
    }

    function _lockPosition(uint256 tokenId, uint256 lockDuration, bool resetInitial) internal {

        .....

        // for extend lock postion we reset the initial lock duration
        // we have to check that the lock duration is greater then the current
        if (resetInitial) {
            require(lockDuration > position.initialLockDuration, "invalid");
            position.initialLockDuration = lockDuration;
        }

        .....

    }

```
3. However, so far we can assume that there is no checks if the tokenIds's StakingPosition is still locked. So in scenario where Bob stakes tokens trough `MulmStaking::createPosition` for at least 3 months, now he will be able to vote whenever he wants, until deciding to withdraw all staked tokens from the position. 

## Impact
Core invariant broke, due to voting allowance does not depend on StakingPosition endLockTime is reached  

## Code Snippet
https://github.com/sherlock-audit/2024-06-magicsea/blob/42e799446595c542eff9519353d3becc50cdba63/magicsea-staking/src/Voter.sol#L153-L219

## Tool used

Manual Review

## Recommendation
Append new external view function in `MlumStaking` called `getRemainingLockTime` that returns `MlumStaking::_remainingLockTime` based on tokenId's StakingPosition, and rewrite `Voter::vote` as shown below:
```diff
    function vote(uint256 tokenId, address[] calldata pools, uint256[] calldata deltaAmounts) external {
        if (pools.length != deltaAmounts.length) revert IVoter__InvalidLength();

        // check voting started
        if (!_votingStarted()) revert IVoter_VotingPeriodNotStarted();
        if (_votingEnded()) revert IVoter_VotingPeriodEnded();

        // check ownership of tokenId
        if (_mlumStaking.ownerOf(tokenId) != msg.sender) {
            revert IVoter__NotOwner();
        }

        uint256 currentPeriodId = _currentVotingPeriodId;
        // check if alreay voted
        if (_hasVotedInPeriod[currentPeriodId][tokenId]) {
            revert IVoter__AlreadyVoted();
        }

        // check if _minimumLockTime >= initialLockDuration and it is locked
        if (_mlumStaking.getStakingPosition(tokenId).initialLockDuration < _minimumLockTime) {
            revert IVoter__InsufficientLockTime();
        }
---     if (_mlumStaking.getStakingPosition(tokenId).lockDuration < _periodDuration) {
+++     if (_mlumStaking.getRemainingLockTime(tokenId) < _periodDuration) {
            revert IVoter__InsufficientLockTime();
        }

        uint256 votingPower = _mlumStaking.getStakingPosition(tokenId).amountWithMultiplier;

        // check if deltaAmounts > votingPower
        uint256 totalUserVotes;
        for (uint256 i = 0; i < pools.length; ++i) {
            totalUserVotes += deltaAmounts[i];
        }

        if (totalUserVotes > votingPower) {
            revert IVoter__InsufficientVotingPower();
        }

        IVoterPoolValidator validator = _poolValidator;

        for (uint256 i = 0; i < pools.length; ++i) {
            address pool = pools[i];

            if (address(validator) != address(0) && !validator.isValid(pool)) {
                revert Voter__PoolNotVotable();
            }

            uint256 deltaAmount = deltaAmounts[i];

            _userVotes[tokenId][pool] += deltaAmount;
            _poolVotesPerPeriod[currentPeriodId][pool] += deltaAmount;

            if (_votes.contains(pool)) {
                _votes.set(pool, _votes.get(pool) + deltaAmount);
            } else {
                _votes.set(pool, deltaAmount);
            }

            _notifyBribes(_currentVotingPeriodId, pool, tokenId, deltaAmount); // msg.sender, deltaAmount);
        }

        _totalVotes += totalUserVotes;

        _hasVotedInPeriod[currentPeriodId][tokenId] = true;

        emit Voted(tokenId, currentPeriodId, pools, deltaAmounts);
    }
```
