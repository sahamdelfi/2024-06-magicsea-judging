Best Khaki Koala

High

# BribeRewarder::fundAndBribe will always revert if fee on transfer tokens are used as rewarder main token

# `BribeRewarder::fundAndBribe` will always revert if fee on transfer tokens are used as rewarder main token
## Summary
The `fundAndBribe` function within the `BribeRewarder` contract is designed to facilitate the funding and bribing process. However, when utilizing fee-on-transfer (FOT) tokens as the main reward token, the function encounters an issue due to the inherent fee deduction mechanism of these tokens. This leads to a discrepancy between the expected and actual token amounts received by the contract, causing the transaction to revert consistently.

## Vulnerability Detail
Fee-on-transfer tokens deduct a fee from every transfer made, reducing the amount received by the destination address. Consequently, when transferring tokens to the rewarder, the actual value received (`totalAmount - transferFee`) is less than the expected total amount. This discrepancy triggers a revert in the `_bribe` function due to a balance check failure, rendering the protocol incompatible with FOT tokens.

```javascript
    function fundAndBribe(
        uint256 startId,
        uint256 lastId,
        uint256 amountPerPeriod
    ) external payable onlyOwner {
        IERC20 token = _token();
        uint256 totalAmount = _calcTotalAmount(
            startId,
            lastId,
            amountPerPeriod
        );

        if (address(token) == address(0)) {
            if (msg.value < totalAmount) {
                revert BribeRewarder__InsufficientFunds();
            }
        } else {
@>          token.safeTransferFrom(msg.sender, address(this), totalAmount);
        }
        _bribe(startId, lastId, amountPerPeriod);
    }
```
After the transfer happens, the `_bribe` function is called. This function includes a check to ensure the current balance is not less than `totalAmount`, which it is due to fees. As a result, this will always revert, and the protocol won't work with FOT tokens.

```javascript
    function _bribe(
        uint256 startId,
        uint256 lastId,
        uint256 amountPerPeriod
    ) internal {
        _checkAlreadyInitialized();
        if (lastId < startId) revert BribeRewarder__WrongEndId();
        if (amountPerPeriod == 0) revert BribeRewarder__ZeroReward();

        IVoter voter = IVoter(_caller);

        if (startId <= voter.getCurrentVotingPeriod()) {
            revert BribeRewarder__WrongStartId();
        }

        uint256 totalAmount = _calcTotalAmount(
            startId,
            lastId,
            amountPerPeriod
        );

        // audit cont==> totalAmount - fee recieved.
        // balance is less than < totalAmount so it will always revert
        uint256 balance = _balanceOfThis(_token());

    @>  if (balance < totalAmount) revert BribeRewarder__InsufficientFunds();

        _startVotingPeriod = startId;
        _lastVotingPeriod = lastId;
        _amountPerPeriod = amountPerPeriod;

        // create rewads per period
        uint256 bribeEpochs = _calcPeriods(startId, lastId);
        for (uint256 i = 0; i <= bribeEpochs; ++i) {
            _rewards.push();
        }
        // struct RewardPerPeriod {
        //     Amounts.Parameter userVotes;
        //     Rewarder2.Parameter rewarder;
        // }
        _lastUpdateTimestamp = block.timestamp;

        IVoter(_caller).onRegister();

        emit BribeInit(startId, lastId, amountPerPeriod);
    }
```
## Impact
The bribeRewarders won't work with FOT tokens because the `fundAndBribe` will always revert.

## Code Snippet
Links:
[fundAndBribe](https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/rewarders/BribeRewarder.sol#L111)

[_bribe](https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/rewarders/BribeRewarder.sol#L226)

A simple test using a FOT token Mock:
<details>

The test:
```javascript

    function setUp() public {
            tokenA = IERC20(new ERC20Mock("Token A", "TA", 18));
            tokenB = IERC20(new ERC20Mock("Token B", "TB", 18));
            missret = IERC20(0xdAC17F958D2ee523a2206206994597C13D831ec7);
@>          rewardToken = IERC20(new ERC20FOT("Reward Token", "RT", 6));
        .
        .
        .
    }
    .
    .
    .
    function testFOT() public {
        ERC20Mock(address(rewardToken)).mint(address(this), 20e18);
        ERC20Mock(address(rewardToken)).approve(address(rewarder), 20e18);
        vm.expectRevert(
            IBribeRewarder.BribeRewarder__InsufficientFunds.selector
        );
        rewarder.fundAndBribe(1, 2, 1e18);
    }
```
FOT Mock:
```javascript
contract ERC20FOT is ERC20 {
    mapping(address account => uint256) private _balances;

    mapping(address account => mapping(address spender => uint256))
        private _allowances;

    uint256 private _totalSupply;

    uint8 private decimalsOverride;
    uint256 public _fee = 1e16;
    /// @dev Constructor
    /// @param _decimals The number of decimals for this token
    constructor(
        string memory _name,
        string memory _symbol,
        uint8 _decimals
    ) ERC20(_name, _symbol) {
        decimalsOverride = _decimals;
    }

    /// @dev Define the number of decimals
    /// @return The number of decimals
    function decimals() public view override returns (uint8) {
        return decimalsOverride;
    }

    /// @dev Mint _amount to _to.
    /// @param _to The address that will receive the mint
    /// @param _amount The amount to be minted
    function mint(address _to, uint256 _amount) external {
        _mint(_to, _amount);
    }

    function _update(
        address from,
        address to,
        uint256 value
    ) internal virtual override {
        if (from == address(0)) {
            // Overflow check required: The rest of the code assumes that totalSupply never overflows
            _totalSupply += value;
        } else {
            uint256 fromBalance = _balances[from];
            if (fromBalance < value) {
                revert ERC20InsufficientBalance(from, fromBalance, value);
            }
            unchecked {
                // Overflow not possible: value <= fromBalance <= totalSupply.
                _balances[from] = fromBalance - value;
            }
        }

        if (to == address(0)) {
            unchecked {
                // Overflow not possible: value <= totalSupply or value <= fromBalance <= totalSupply.
                _totalSupply -= value;
            }
        } else {
            unchecked {
                // Overflow not possible: balance + value is at most totalSupply, which we know fits into a uint256.
                _balances[to] += value - _fee;
            }
        }

        emit Transfer(from, to, value);
    }
}
```
</details>

## Tool used
Manual Review

## Recommendation
The quick fix is to send the fee amount directly to the contract address. Alternatively, you can add a `_transferFee` as input to `fundAndBribe`:
```diff
function fundAndBribe(
    uint256 startId,
    uint256 lastId,
    uint256 amountPerPeriod,
+   uint256 _transferFee // Added parameter for transfer fee
) external payable onlyOwner {
    IERC20 token = _token();
    uint256 totalAmount = _calcTotalAmount(
        startId,
        lastId,
        amountPerPeriod
    ) ;
+   totalAmount += _transferFee; // Adjust totalAmount to include the transfer fee    

    if (address(token) == address(0)) {
        if (msg.value < totalAmount) {
            revert BribeRewarder__InsufficientFunds();
        }
    } else {
        token.safeTransferFrom(msg.sender, address(this), totalAmount);
    }
    _bribe(startId, lastId, amountPerPeriod);
}

```