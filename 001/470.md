Young Iron Beaver

Medium

# BribeRewarder Contract Lacks Withdrawal Mechanism Leading to Potential Permanent Loss of Funds

## Summary

The BribeRewarder contract in the protocol lacks a withdrawal mechanism, potentially leading to permanent loss of funds. If no users vote for the pool specified by the BribeRewarder, the bribeTokens intended for bribery remain locked in the contract indefinitely, as they can only be withdrawn through the `claim` function. This vulnerability can result in a significant financial loss.

## Vulnerability Detail

The protocol involves a voting and bribery mechanism where stakeholders can create their own BribeRewarder contract to bribe users into voting for their designated pool. However, the BribeRewarder contract lacks a withdraw mechanism, potentially leading to permanent loss of funds.

Specifically, the BribeRewarder contract has the `fundAndBribe` function, which transfers the bribeToken intended for bribery to the created BribeRewarder contract and specifies the bribery period and amount.

```solidity
function fundAndBribe(uint256 startId, uint256 lastId, uint256 amountPerPeriod) external payable onlyOwner {
        IERC20 token = _token();
        uint256 totalAmount = _calcTotalAmount(startId, lastId, amountPerPeriod);

        if (address(token) == address(0)) {
            if (msg.value < totalAmount) {
                revert BribeRewarder__InsufficientFunds();
            }
        } else {
            token.safeTransferFrom(msg.sender, address(this), totalAmount);
        }

        _bribe(startId, lastId, amountPerPeriod);
    }
```

Under normal circumstances, when a user calls the `vote` function of the Voter contract to vote for the pool specified by BribeRewarder, the `deposit` function of BribeRewarder will be called.

```solidity
/**
     * Deposits votes for the given period and token id, only callable by the voter contract
     *
     * @param periodId period id of the voting period
     * @param tokenId owners token id
     * @param deltaAmount amount of votes
     */
    function deposit(uint256 periodId, uint256 tokenId, uint256 deltaAmount) public onlyVoter {
        _modify(periodId, tokenId, deltaAmount.toInt256(), false);

        emit Deposited(periodId, tokenId, _pool(), deltaAmount);
    }
```

This will record the user's voting status, and the user can eventually call the `claim` function of BribeRewarder to receive the bribery tokens.

```solidity
/**
     * Claim the reward for the given period and token id
     * @param tokenId token id of the owner
     */
    function claim(uint256 tokenId) external override {
        uint256 endPeriod = IVoter(_caller).getLatestFinishedPeriod();

        uint256 totalAmount;

        // calc emission per period cause every period can every other durations
        for (uint256 i = _startVotingPeriod; i <= endPeriod; ++i) {
            totalAmount += _modify(i, tokenId, 0, true);
        }

        emit Claimed(tokenId, _pool(), totalAmount);
    }

function _modify(uint256 periodId, uint256 tokenId, int256 deltaAmount, bool isPayOutReward)
        private
        returns (uint256 rewardAmount)
    {
        if (!IVoter(_caller).ownerOf(tokenId, msg.sender)) {
            revert BribeRewarder__NotOwner();
        }

        // extra check so we dont calc rewards before starttime
        (uint256 startTime,) = IVoter(_caller).getPeriodStartEndtime(periodId);
        if (block.timestamp <= startTime) {
            _lastUpdateTimestamp = startTime;
        }

        RewardPerPeriod storage reward = _rewards[_indexByPeriodId(periodId)];
        Amounts.Parameter storage amounts = reward.userVotes;
        Rewarder2.Parameter storage rewarder = reward.rewarder;

        (uint256 oldBalance, uint256 newBalance, uint256 oldTotalSupply,) = amounts.update(tokenId, deltaAmount);

        uint256 totalRewards = _calculateRewards(periodId);

        rewardAmount = rewarder.update(bytes32(tokenId), oldBalance, newBalance, oldTotalSupply, totalRewards);

        if (block.timestamp > _lastUpdateTimestamp) {
            _lastUpdateTimestamp = block.timestamp;
        }

        if (isPayOutReward) {
            rewardAmount = rewardAmount + unclaimedRewards[periodId][tokenId];
            unclaimedRewards[periodId][tokenId] = 0;
            if (rewardAmount > 0) {
                IERC20 token = _token();
                _safeTransferTo(token, msg.sender, rewardAmount);
            }
        } else {
            unclaimedRewards[periodId][tokenId] += rewardAmount;
        }
    }
```

However, it is possible that even if bribes exist, other users may still not vote for the pool expected by the BribeRewarder. If no user votes for that pool, no user will be able to withdraw the bribeTokens through the `claim` function.

Since the contract does not have any other way to withdraw the previously transferred bribeTokens except through the `claim` function, if no one votes, these bribeTokens will be permanently locked in the BribeRewarder contract, leading to a loss of funds.

## Impact

The lack of a withdraw mechanism in the BribeRewarder contract can result in permanent loss of funds if no users vote for the specified pool. This vulnerability leaves the transferred bribeTokens irretrievable, causing them to be permanently locked within the contract.

## Code Snippet
https://github.com/sherlock-audit/2024-06-magicsea/blob/42e799446595c542eff9519353d3becc50cdba63/magicsea-staking/src/rewarders/BribeRewarder.sol#L76-L410

## Tool used

Manual Review

## Recommendation

It is recommended to add the following `withdraw` function to allow the contract owner to withdraw all remaining bribeTokens after the bribery period.

```solidity
function withdraw() external onlyOwner {
    uint256 endPeriod = IVoter(_caller).getLatestFinishedPeriod();
    require(endPeriod > _lastVotingPeriod);
    IERC20 token = _token();
    _safeTransferTo(token, msg.sender, token.balanceOf(address(this)));
}
```