Slow Indigo Butterfly

High

# Voters can loose their rewards due to wrong reward calculation in `_calculateRewards()`

## Summary

Error in reward calculation by using `_lastUpdateTimestamp` can cause voters to loose their rewards.

## Vulnerability Detail

In `BribeRewarder::_calculateRewards` we can see that the `_lastUpdateTimestamp` is used to calculate the amount of reward tokens accrued since the last update. The issue there is that because all periods share the same timestamp(`_lastUpdateTimestamp`) reward calculation after one period ends and voting starts on the next period will always be wrong as the `_lastUpdateTimestamp` would have been updated to the next period (i.e. _lastUpdateTimestamp > endTime of previous period) meaning all rewards after the last vote will be lost as the reward calculation will always return zero.

consider the following test case, add to tests/BribeRewarder.t.sol

```solidity

 function test_rewardLoss() public {
        ERC20Mock(address(rewardToken)).mint(address(this), 20e18);
        ERC20Mock(address(rewardToken)).approve(address(rewarder), 20e18);

        rewarder.fundAndBribe(1, 2, 10e18);

        _voterMock.setCurrentPeriod(1);
        _voterMock.setStartAndEndTime(0, 100);

        // time: 0
        vm.warp(0);
        vm.prank(address(_voterMock));
        rewarder.deposit(1, 1, 0.2e18);

        // time: 100 ,start period 2
        vm.warp(100);
        _voterMock.setCurrentPeriod(2);
        _voterMock.setLatestFinishedPeriod(1);

        //single voter gets all the rewards
        assertEq(10000000000000000000, rewarder.getPendingReward(1));

        //someone votes in period 2 before alice claims
        vm.warp(101);
        vm.prank(address(_voterMock));
        rewarder.deposit(2, 2, 0.2e18);

        //alice lost all rewards from period 1
        assertEq(0, rewarder.getPendingReward(1));

        vm.prank(alice);
        rewarder.claim(1);

        assertEq(0, rewardToken.balanceOf(alice));
    }
```

## Impact

Voters can loose Bribe rewards

## Code Snippet

https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/rewarders/BribeRewarder.sol#L300-L313

## Tool used

Manual Review

## Recommendation

```diff

function _calculateRewards(uint256 periodId) internal view returns (uint256) {
        (uint256 startTime, uint256 endTime) = IVoter(_caller).getPeriodStartEndtime(periodId);

        if (endTime == 0 || startTime > block.timestamp) {
            return 0;
        }

        uint256 duration = endTime - startTime;
        uint256 emissionsPerSecond = _amountPerPeriod / duration;
        
-       uint256 lastUpdateTimestamp = _lastUpdateTimestamp;
+       uint256 lastUpdateTimestamp = _rewards[_indexByPeriod(periodId)].rewarder.lastUpdateTimestamp;
        uint256 timestamp = block.timestamp > endTime ? endTime : block.timestamp;
        return timestamp > lastUpdateTimestamp ? (timestamp - lastUpdateTimestamp) * emissionsPerSecond : 0; 
    }
```

