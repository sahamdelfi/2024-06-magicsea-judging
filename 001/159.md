Interesting Chili Albatross

High

# Voting is not possible when any Bribe rewarder is set for a period

## Summary

When a user votes on the Voter.contract, the flow is `vote => _notifyBribes`, where if a bribe rewarder had been registered for the current period and given pool, then the `Briberewarder.deposit()` is called to notify the rewarder of the user's votes.

The problem is that this call will always revert in `rewarder.deposit => _modify` because it requires the msg.sender to be the owner of the concerned NFT tokenID based on which the voting is happening, while the msg.sender is the Voter contract itself from the rewarder's perspective.

## Vulnerability Detail

The vote function calls `_notifyBribes` [here](https://github.com/sherlock-audit/2024-06-magicsea/blob/42e799446595c542eff9519353d3becc50cdba63/magicsea-staking/src/Voter.sol#L211)

This is the `_notifyBribes` function :

```solidity
    function _notifyBribes(uint256 periodId, address pool, uint256 tokenId, uint256 deltaAmount) private {
        IBribeRewarder[] storage rewarders = _bribesPerPriod[periodId][pool];
        for (uint256 i = 0; i < rewarders.length; ++i) {
            if (address(rewarders[i]) != address(0)) {
                rewarders[i].deposit(periodId, tokenId, deltaAmount);
                _userBribesPerPeriod[periodId][tokenId].push(rewarders[i]);
            }
        }
    }
```

It is clear that rewarder.deposit is an external call on a registered bribe rewarder, and voter.sol will be the msg.sender for this call.

Also, we can see below that the deposit function in BribeRewarder.sol is only accessible by the Voter contract, which means the msg.sender will always be the voter contract.

```solidity

    function deposit(uint256 periodId, uint256 tokenId, uint256 deltaAmount) public onlyVoter {
        _modify(periodId, tokenId, deltaAmount.toInt256(), false);

        emit Deposited(periodId, tokenId, _pool(), deltaAmount);
    }

```

The `_modify` function [here](https://github.com/sherlock-audit/2024-06-magicsea/blob/42e799446595c542eff9519353d3becc50cdba63/magicsea-staking/src/rewarders/BribeRewarder.sol#L264) requires that the caller be the owner of the NFT.

```solidity
function _modify(uint256 periodId, uint256 tokenId, int256 deltaAmount, bool isPayOutReward)
        private
        returns (uint256 rewardAmount)
    {
        if (!IVoter(_caller).ownerOf(tokenId, msg.sender)) {
            revert BribeRewarder__NotOwner();
        }
...snip
```

The `Voter.ownerOf()` function simply [checks that the address passed](https://github.com/sherlock-audit/2024-06-magicsea/blob/42e799446595c542eff9519353d3becc50cdba63/magicsea-staking/src/Voter.sol#L402) is the owner of the NFT in MLUMStaking.sol and returns true if he is. Now here it is clear that the address passed is msg.sender which is the voter contract and that address is not the owner of any NFT used by any user for the voting process.

In fact the original Voter.vote() function already [checks that the caller there is the owner of tokenID](https://github.com/sherlock-audit/2024-06-magicsea/blob/42e799446595c542eff9519353d3becc50cdba63/magicsea-staking/src/Voter.sol#L161).

## Impact

Voting will always revert for all pools and periods wherever a bribe rewarder is registered. None of the users will be able to vote.

High severity because the complete voting and bribing functionality is broken.
This issue is not caught in tests because voterMock is being used, which modifies the `voter.ownerOf()` function to always return true. See [here](https://github.com/sherlock-audit/2024-06-magicsea/blob/42e799446595c542eff9519353d3becc50cdba63/magicsea-staking/test/mocks/VoterMock.sol#L100)

## Code Snippet

https://github.com/sherlock-audit/2024-06-magicsea/blob/42e799446595c542eff9519353d3becc50cdba63/magicsea-staking/src/rewarders/BribeRewarder.sol#L264

## Tool used

Manual Review

## Recommendation

This ownerOf check is not required in the deposit flow. This `deposit => _modify` is only callable by the Voter contract and only once a period, and the voter contract already checks that the voter is owner of the tokenID he is using the voting power of ([as seen here](https://github.com/sherlock-audit/2024-06-magicsea/blob/42e799446595c542eff9519353d3becc50cdba63/magicsea-staking/src/Voter.sol#L161)).

Move the check to the claim function because it is only required in the `claim => _modify` flow.
