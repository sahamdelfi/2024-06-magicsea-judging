Sticky Carmine Dragon

High

# Incorrect validation in `MlumStaking::_requireOnlyOperatorOrOwnerOf`

## Summary

The check in the `MlumStaking::_requireOnlyOperatorOrOwnerOf` function always passes for any `msg.sender` and any `tokenId`.

## Vulnerability Detail

The `MlumStaking::_requireOnlyOperatorOrOwnerOf` function utilizes the `ERC721Upgradeable._isAuthorized` function behind the scenes, which takes `owner` and `sender` as parameters. However, `msg.sender` is sent in both instances, meaning that `msg.sender` is treated as the owner for any `tokenId`. Within the `ERC721Upgradeable._isAuthorized` implementation, there is a check to see if `owner == spender`. If this condition is met, the sender is considered authorized. Consequently, any `msg.sender` for any `tokenId` is deemed authorized.

```solidity
function _requireOnlyOperatorOrOwnerOf(uint256 tokenId) internal view {
    // isApprovedOrOwner: caller has no rights on token
    require(ERC721Upgradeable._isAuthorized(msg.sender, msg.sender, tokenId), "FORBIDDEN");
}
```

```solidity
function _isAuthorized(address owner, address spender, uint256 tokenId) internal view virtual returns (bool) {
    return
        spender != address(0) &&
        (owner == spender || isApprovedForAll(owner, spender) || _getApproved(tokenId) == spender);
}
```

## Impact

Any function that employs the `_requireOnlyOperatorOrOwnerOf` check can be bypassed. Currently, in the contract's implementation, only the `addToPosition` function utilizes this check. This allows malicious users to call it on behalf of another user, thereby increasing their staking duration.

## POC

Modify the `MlumStakingTest::testAddToPosition` test.

```solidity
function testAddToPosition() public {
    _stakingToken.mint(ALICE, 2 ether);

    vm.startPrank(ALICE);
    _stakingToken.approve(address(_pool), 1 ether);
    _pool.createPosition(1 ether, 1 days);
    vm.stopPrank();

    // check lockduration
    MlumStaking.StakingPosition memory position = _pool.getStakingPosition(1);
    assertEq(position.lockDuration, 1 days);

    skip(43200);

    // add to position should take calc. avg. lock duration
    address anyone = makeAddr("anyone");
    _stakingToken.mint(anyone, 1 ether);

    vm.startPrank(anyone);
    _stakingToken.approve(address(_pool), 1 ether);
    _pool.addToPosition(1, 1 ether);
    vm.stopPrank();

    position = _pool.getStakingPosition(1);

    assertEq(position.lockDuration, 64800);
}
```

## Code Snippet

https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/MlumStaking.sol#L140-L143

## Tool used

Manual Review

## Recommendation
```diff
     function _requireOnlyOperatorOrOwnerOf(uint256 tokenId) internal view {
         // isApprovedOrOwner: caller has no rights on token
-        require(ERC721Upgradeable._isAuthorized(msg.sender, msg.sender, tokenId), "FORBIDDEN");
+        address owner = _ownerOf(tokenId);
+        require(ERC721Upgradeable._isAuthorized(owner, msg.sender, tokenId), "FORBIDDEN");
     }
```
