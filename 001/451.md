Salty Sky Caribou

High

# Attacker can drain the rewards via reentrancy if the reward token is native

## Summary
An attacker can drain the rewards via reentrancy if the reward token is native. 
## Vulnerability Detail
Let's imagine the following scenario:
- There's a bribe rewarder with a native reward token, a user has voted in period1, and now he can claim some rewards. 

Now let's take a look at the flow when a user claims rewards. He calls BribeRewarder::claim():
```solidity
    function claim(uint256 tokenId) external override {
        uint256 endPeriod = IVoter(_caller).getLatestFinishedPeriod();

        uint256 totalAmount;

        for (uint256 i = _startVotingPeriod; i <= endPeriod; ++i) {
            totalAmount += _modify(i, tokenId, 0, true);
        }

        emit Claimed(tokenId, _pool(), totalAmount);
    }
```
then `_modify` gets called:
```solidity
    function _modify(uint256 periodId, uint256 tokenId, int256 deltaAmount, bool isPayOutReward)
        private
        returns (uint256 rewardAmount)
    {
        if (!IVoter(_caller).ownerOf(tokenId, msg.sender)) {
            revert BribeRewarder__NotOwner();
        }

        // extra check so we dont calc rewards before starttime
        (uint256 startTime,) = IVoter(_caller).getPeriodStartEndtime(periodId);

        if (block.timestamp <= startTime) {
            _lastUpdateTimestamp = startTime;
        }

        RewardPerPeriod storage reward = _rewards[_indexByPeriodId(periodId)];
        Amounts.Parameter storage amounts = reward.userVotes;
        Rewarder2.Parameter storage rewarder = reward.rewarder;
        (uint256 oldBalance, uint256 newBalance, uint256 oldTotalSupply,) = amounts.update(tokenId, deltaAmount);
        uint256 totalRewards = _calculateRewards(periodId);

        rewardAmount = rewarder.update(bytes32(tokenId), oldBalance, newBalance, oldTotalSupply, totalRewards);

        if (block.timestamp > _lastUpdateTimestamp) {
            _lastUpdateTimestamp = block.timestamp;
        }

        if (isPayOutReward) {
            rewardAmount = rewardAmount + unclaimedRewards[periodId][tokenId];
            unclaimedRewards[periodId][tokenId] = 0;
            if (rewardAmount > 0) {
                IERC20 token = _token();
                _safeTransferTo(token, msg.sender, rewardAmount); 
            }
        } else {
            unclaimedRewards[periodId][tokenId] += rewardAmount;
        }
    }
```
and let's focus on the following parts of the function:
```solidity
      rewardAmount = rewarder.update(bytes32(tokenId), oldBalance, newBalance, oldTotalSupply, totalRewards);

      if (isPayOutReward) {
            rewardAmount = rewardAmount + unclaimedRewards[periodId][tokenId];
            unclaimedRewards[periodId][tokenId] = 0;
            if (rewardAmount > 0) {
                IERC20 token = _token();
                _safeTransferTo(token, msg.sender, rewardAmount); 
            }
```

Now, what can happen is that the user can continuously re-enter the claim function and he'll continue to get rewards. `unclaimedRewards[periodId][tokenId] = 0` will be reset to 0, but this calculation will continue to return a number > 0 since there's nothing changed about it:
```solidity
rewardAmount = rewarder.update(bytes32(tokenId), oldBalance, newBalance, oldTotalSupply, totalRewards);
```
Now let's also take a look at the `_safeTransferTo` function to actually see there's reentrancy possible:
```solidity
    function _safeTransferTo(IERC20 token, address account, uint256 amount) internal virtual {
        if (amount == 0) return;

        if (address(token) == address(0)) {
            
            (bool s,) = account.call{value: amount}("");

            if (!s) revert BribeRewarder__NativeTransferFailed();
        } else {
            token.safeTransfer(account, amount);
        }
    }
```
The function sends the native token to the account. If the account is a malicious smart contract that re-enters the claim function when it receives native, a reentrancy is possible.
## Impact
Attacker can drain all of the rewards.
## Code Snippet
https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/rewarders/BribeRewarder.sol#L293
## Tool used

Manual Review

## Recommendation
Add a nonReentrant modifier or make sure that the `rewardAmount` is calculated as 0 after a user already claimed his rewards for this period.