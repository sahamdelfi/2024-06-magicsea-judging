Abundant Pickle Rattlesnake

High

# Users cannot receive reawrds accurately, and the remaining rewards are permanently locked in the contract.

## Summary
Due to incorrect handling of `_lastUpdateTimestamp` in the `BribeRewarder.sol#_calculateRewards()` function, the bribe reward distribution does not proceed accurately and the remaining rewards are permanently locked in the contract.
## Vulnerability Detail
The bribe funds (`totalAmount`) for the given start and end periods are transferred to the rewarder using the [`fundAndBribe()`](https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/rewarders/BribeRewarder.sol#L111-L124) function. The transferred funds are then distributed to the owners of the tokenId (MlumStaking position) through the `claim()` function.
```solidity
    function claim(uint256 tokenId) external override {
        uint256 endPeriod = IVoter(_caller).getLatestFinishedPeriod();

        uint256 totalAmount;

        // calc emission per period cause every period can every other durations
159:    for (uint256 i = _startVotingPeriod; i <= endPeriod; ++i) {
160:        totalAmount += _modify(i, tokenId, 0, true);
161:    }

        emit Claimed(tokenId, _pool(), totalAmount);
    }
```
As you can see in the code snippet above, the `claim()` function calls the `_modify()` function for all periods to emit rewards.
```solidity
    function _modify(uint256 periodId, uint256 tokenId, int256 deltaAmount, bool isPayOutReward)
        private
        returns (uint256 rewardAmount)
    {
        ...SNIP...

        (uint256 oldBalance, uint256 newBalance, uint256 oldTotalSupply,) = amounts.update(tokenId, deltaAmount);

280:    uint256 totalRewards = _calculateRewards(periodId);

        rewardAmount = rewarder.update(bytes32(tokenId), oldBalance, newBalance, oldTotalSupply, totalRewards);

284:    if (block.timestamp > _lastUpdateTimestamp) {
285:        _lastUpdateTimestamp = block.timestamp;
286:    }

        ...SNIP...
    }
```
As you can see in #L280, the `_modify()` function calls the `_calculateRewards()` function to calculate the total reward for every period.
```solidity
    function _calculateRewards(uint256 periodId) internal view returns (uint256) {
        (uint256 startTime, uint256 endTime) = IVoter(_caller).getPeriodStartEndtime(periodId);

        if (endTime == 0 || startTime > block.timestamp) {
            return 0;
        }

        uint256 duration = endTime - startTime;
        uint256 emissionsPerSecond = _amountPerPeriod / duration;

310:    uint256 lastUpdateTimestamp = _lastUpdateTimestamp;
311:    uint256 timestamp = block.timestamp > endTime ? endTime : block.timestamp;
312:    return timestamp > lastUpdateTimestamp ? (timestamp - lastUpdateTimestamp) * emissionsPerSecond : 0;
    }
```
The basic cause of the issue is that the `_calculateRewards()` function calculates rewards using `_lastUpdateTimestamp`, the state variable of the contract.
As can be seen in #L284~#L286, in the first call of the `_modify()` function(i.e. `i = _startVotingPeriod`), the `_lastUpdateTimestamp` is updated with the current timestamp (`block.timestamp`).
Therefore, from the second call (i.e. `i = _startVotingPeriod + 1`) in #L159 to #L161 of the `claim()` function, 0 is returned from the `_calculateRewards()` function because it is a single transaction.

    > lastUpdateTimestamp = block.timestamp
    > block.timestamp > endTime     =>   timestamp = endTime
    > timestamp < lastUpdateTimestamp   =>     return 0


To illustrate the problem:
 -Assume the current block.timestamp is at the end of 'endPeriod'.
 -Alice calls the `claim()` function.
 -First, `_modify(_startVotingPeriod, tokenId, 0, true)` is called at `i=_startVotingPeriod`.
  At this time, after `_calculateRewards(_startVotingPeriod)` is called in the `_modify()` function, `_lastUpdateTimestamp` is set to `block.timestamp` in #285L.
 -Next, `_modify(_startVotingPeriod+1, tokenId, 0, true)` is called at `i=_startVotingPeriod+1`.
  At this time, `_calculateRewards(_startVotingPeriod+1)` is called in the `_modify()` function.
  At this time, `timestamp` in #311L is set to `endTime`(block.timestamp > endTime), and therefore `timestamp < lastUpdateTimestamp` in #312L, so 0 is returned.
  That is, rewards are not distributed from `i=_startVotingPeriod+1`.

As a result, the bribe reward distribution does not proceed accurately and the remaining rewards are permanently locked in the contract.
## Impact
Users cannot receive reawrds accurately, and the remaining rewards are permanently locked in the contract.
## Code Snippet
https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/rewarders/BribeRewarder.sol#L300-L313
https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/rewarders/BribeRewarder.sol#L153-L164
https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/rewarders/BribeRewarder.sol#L260-L298
## Tool used

Manual Review

## Recommendation
Do not manage `_lastUpdateTimestamp` overall, but manage it separately for the rewarder corresponding to every voting period.
```solidity
    function _calculateRewards(uint256 periodId) internal view returns (uint256) {
        (uint256 startTime, uint256 endTime) = IVoter(_caller).getPeriodStartEndtime(periodId);

        if (endTime == 0 || startTime > block.timestamp) {
            return 0;
        }

        uint256 duration = endTime - startTime;
        uint256 emissionsPerSecond = _amountPerPeriod / duration;

---     uint256 lastUpdateTimestamp = _lastUpdateTimestamp;
+++     RewardPerPeriod storage reward = _rewards[_indexByPeriodId(periodId)];
+++     Rewarder2.Parameter storage rewarder = reward.rewarder;
+++     uint256 lastUpdateTimestamp = rewarder.lastUpdateTimestamp;
+++     if (block.timestamp <= startTime) {
+++         lastUpdateTimestamp = startTime;
+++     }

        uint256 timestamp = block.timestamp > endTime ? endTime : block.timestamp;
        return timestamp > lastUpdateTimestamp ? (timestamp - lastUpdateTimestamp) * emissionsPerSecond : 0;
    }
```