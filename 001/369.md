Sleepy Navy Ant

High

# Period after the BribeRewarder's start period will not accumulate as much rewards, causing users to claim less and leaving the last depositor without rewards

## Summary

The `BribeRewarder` contract is intended to bribe user to vote for a certain pool, but due to updating `_lastUpdateTimestamp` early, the early users will receive less rewards, while the last voter will not earn any rewards.



## Vulnerability Detail


Users deposit into a bribe by calling the `Voter::vote` function, which then calls `_notifyBribes`, which calls `BribeRewarder::deposit` function:

```solidity
  function vote(
        uint256 tokenId,
        address[] calldata pools,
        uint256[] calldata deltaAmounts
    ) external {
            ...handle Voting
            
            _notifyBribes(_currentVotingPeriodId, pool, tokenId, deltaAmount); // msg.sender, deltaAmount);
        }
```

```solidity
function _notifyBribes(
        uint256 periodId,
        address pool,
        uint256 tokenId,
        uint256 deltaAmount
    ) private {
        IBribeRewarder[] storage rewarders = _bribesPerPriod[periodId][pool];
        for (uint256 i = 0; i < rewarders.length; ++i) {
            if (address(rewarders[i]) != address(0)) {
                rewarders[i].deposit(periodId, tokenId, deltaAmount); <@ The user is depositing into the bribe
                _userBribesPerPeriod[periodId][tokenId].push(rewarders[i]);
            }
        }
    }
```

The [BribeRewarder::deposit](https://github.com/sherlock-audit/2024-06-magicsea-kostadin-m/blob/42e799446595c542eff9519353d3becc50cdba63/magicsea-staking/src/rewarders/BribeRewarder.sol#L143-L147) function calls `BribeRewarder::_modify` which takes care of user's amounts for the specific period.


```solidity
 function _modify(uint256 periodId, uint256 tokenId, int256 deltaAmount, bool isPayOutReward)
        private
        returns (uint256 rewardAmount)
    {
        if (!IVoter(_caller).ownerOf(tokenId, msg.sender)) {
            revert BribeRewarder__NotOwner();
        }

        // extra check so we dont calc rewards before starttime
        (uint256 startTime,) = IVoter(_caller).getPeriodStartEndtime(periodId);
        if (block.timestamp <= startTime) {
            _lastUpdateTimestamp = startTime;
        }

        RewardPerPeriod storage reward = _rewards[_indexByPeriodId(periodId)];
        Amounts.Parameter storage amounts = reward.userVotes;
        Rewarder2.Parameter storage rewarder = reward.rewarder;

        (uint256 oldBalance, uint256 newBalance, uint256 oldTotalSupply,) = amounts.update(tokenId, deltaAmount);

        uint256 totalRewards = _calculateRewards(periodId);

        rewardAmount = rewarder.update(bytes32(tokenId), oldBalance, newBalance, oldTotalSupply, totalRewards);

        if (block.timestamp > _lastUpdateTimestamp) {
            _lastUpdateTimestamp = block.timestamp;
        }

    ...
    }
```

The `_modify` function updates `_lastUpdateTimestamp` to the current timestamp at the end, affecting future reward calculations.

Reward Calculation:

```solidity
 function _calculateRewards(
        uint256 periodId
    ) internal view returns (uint256) {
        (uint256 startTime, uint256 endTime) = IVoter(_caller)
            .getPeriodStartEndtime(periodId);

        if (endTime == 0 || startTime > block.timestamp) {
            return 0;
        }

        uint256 duration = endTime - startTime;

        uint256 emissionsPerSecond = _amountPerPeriod / duration;

        uint256 lastUpdateTimestamp = _lastUpdateTimestamp;
        uint256 timestamp = block.timestamp > endTime
            ? endTime
            : block.timestamp;
        
        
        return
=>          timestamp > lastUpdateTimestamp ? (timestamp lastUpdateTimestamp) * emissionsPerSecond : 0;
    }
```

If `_lastUpdateTimestamp` is equal or greater than the period's endTime the `_calculatedRewards` function will return 0, meaning that no rewards should be distributed.


After calculating the rewards, we are calling `Rewarder2::update`, which handles the reward distributions and user's debt.

```solidity
    function update(
        Parameter storage rewarder,
        bytes32 id,
        uint256 oldBalance,
        uint256 newBalance,
        uint256 totalSupply,
        uint256 totalRewards 
    ) internal returns (uint256 rewards) {
        uint256 accDebtPerShare = (
            rewarder,
            totalSupply,
            totalRewards
        );

        rewards = oldBalance == 0
            ? 0
            : getDebt(accDebtPerShare, oldBalance) - rewarder.debt[id];

        rewarder.debt[id] = getDebt(accDebtPerShare, newBalance);
    }
```


However, when users claim for any period that is not the start period for which the bribe register for, they will not accumulate as much rewards due to updating `_lastUpdateTimestamp` in the first iteration.

```solidity
 function claim(uint256 tokenId) external override {
        uint256 endPeriod = IVoter(_caller).getLatestFinishedPeriod();

        uint256 totalAmount;

        // calc emission per period cause every period can every other durations
        for (uint256 i = _startVotingPeriod; i <= endPeriod; ++i) {
=>            totalAmount += _modify(i, tokenId, 0, true);
        }

        emit Claimed(tokenId, _pool(), totalAmount);
    }
```

Let's say that `_startVotingPeriod=1` and the `endPeriod=2`.

Since the function only allows claiming from the latest finished period, this means that this period's endTime is ether equal or smaller than the current timestamp.

When we iterate through the first period we update `_lastUpdateTimestamp` to the current timestamp, which means after starting to iterate for the endPeriod, `_calculateRewards` will return 0, since `_lastUpdateTimestamp` is either equal or greater than the endTime of the last period. Leading to the user's reward not being updated correctly.

The longer the difference from the last deposit of that period and it's end Timestamp, the more rewards will not be distributed.



## Proof of Concept

[Foundry PoC of users claiming less for the second period](https://gist.github.com/kostadin-m/f895f282049908a3297086060b234c2a)

```bash
Logs:
  All 4 users deposited into a period the same way:
  Alice's rewards who deposited at the beggining 1st period 7500000000000000000
  Bob's rewards who deposited halfway 1st period            2500000000000000000
  Jack's rewards who deposited at the beggining 2nd period  5000000000000000000
  John's rewards who deposited at the halfway 2nd period    0
```

## Impact


Early depositors in any period beyond the `_startVotingPeriod` will experience a reduction in rewards, while the last depositor will receive no rewards, irrespective of when they staked. 
The longer the duration between the last deposit and the period's end, the greater the loss of rewards. This creates an inequitable reward distribution, particularly disadvantaging less popular pools.

## Code Snippet

https://github.com/sherlock-audit/2024-06-magicsea/tree/main/magicsea-staking/src/Voter.sol#L221-L229
https://github.com/sherlock-audit/2024-06-magicsea/tree/main/magicsea-staking/src/rewarders/BribeRewarder.sol#L153-L164
https://github.com/sherlock-audit/2024-06-magicsea/tree/main/magicsea-staking/src/rewarders/BribeRewarder.sol#L260-L298
https://github.com/sherlock-audit/2024-06-magicsea/tree/main/magicsea-staking/src/rewarders/BribeRewarder.sol#L300-L313
https://github.com/sherlock-audit/2024-06-magicsea/tree/main/magicsea-staking/src/libraries/Rewarder2.sol#L130-L143

## Tool used

Manual Review

## Recommendation

Consider tracking `_lastUpdateTimestamp` separately for every period

```solidity
mapping(uint256 periodId => uint256 _lastUpdateTimeStamp);
```
And inside of `_modify` only update the timestamp for the respective period.