Furry Viridian Copperhead

High

# Rewards are calculated as distributed even if there are no voters, locking the rewards forever

## Summary
In the ``Voter.sol`` contract, protocols can create ``BribeRewarder.sol`` contract instances in order to bribe users to vote for the pool specified by the protocol. The more users vote for a specific pool, the bigger the weight of that pool will be compared to other pools and thus users staking the pool **LP** token in the ``MasterchefV2.sol`` contract will receive more rewards. The LP token of the pool the protocol is bribing for, receives bigger allocation of the **LUM** token in the ``MasterchefV2.sol`` contract. Thus incentivizing people to deposit tokens in the AMM associated with the LP token in order to acquire the LP token for a pool with higher weight, thus providing more liquidity in a trading pair.  In the ``Voter.sol`` contract a voting period is defined by an id, start time, and a end time which is the start time + the globaly specified **_periodDuration**. A new voting period can be started by the owner of the ``Voter.sol`` contract by calling the [startNewVotingPeriod()](https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/Voter.sol#L107-L115) function. If a protocol wants to connect a ``BriberRewarder.sol`` contract instance for a specific voting period they have to do so, before the voting period is started. This is a requirement statement in the [onRegister()](https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/Voter.sol#L130-L144) function, which is called by the [_bribe()](https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/rewarders/BribeRewarder.sol#L226-L258) function from the ``BriberRewarder.sol`` contract instance. A protocol team that wishes to deploy an instance of the ``BriberRewarder.sol`` contract must do so via the ``RewarderFactory.sol`` contract. Later on in order to incentivize users to vote for a pool of their choosing they have to first call either [fundAndBribe()](https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/rewarders/BribeRewarder.sol#L111-L124) function and approve the rewarder contract for **amountPerPeriod * number of voting periods** they wish to incetiveze users for.  Or call the [bribe()](https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/rewarders/BribeRewarder.sol#L132-L134) function but first deposit the required reward token amount to the rewarder contract. Both of the above functions call the [_bribe()](https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/rewarders/BribeRewarder.sol#L226-L258) function internally. However if there are no users that vote for that specific pool in exactly the first block that the voting period starts, the rewards that should be distributed until the first vote occurs, will be lost forever, due to the way the [_modify()](https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/rewarders/BribeRewarder.sol#L260-L298) function is implemented.

```solidity
    function _modify(uint256 periodId, uint256 tokenId, int256 deltaAmount, bool isPayOutReward)
        private
        returns (uint256 rewardAmount)
    {
        ...
        RewardPerPeriod storage reward = _rewards[_indexByPeriodId(periodId)];
        Amounts.Parameter storage amounts = reward.userVotes;
        Rewarder2.Parameter storage rewarder = reward.rewarder;

        (uint256 oldBalance, uint256 newBalance, uint256 oldTotalSupply,) = amounts.update(tokenId, deltaAmount);

        uint256 totalRewards = _calculateRewards(periodId);

        rewardAmount = rewarder.update(bytes32(tokenId), oldBalance, newBalance, oldTotalSupply, totalRewards);

        if (block.timestamp > _lastUpdateTimestamp) {
            _lastUpdateTimestamp = block.timestamp;
        }
        ...
    }
```

When the first user votes the [Amounts.update()](https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/libraries/Amounts.sol#L68-L85) function will be called first in order to update the totalSupply and the specific user supply associated with the token id. However the **oldTotalSupply** will be **0**. Later on the [_calculateRewards()](https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/rewarders/BribeRewarder.sol#L300-L313) function will calculate the rewards from the voting period start time until the current **block.timestamp**. However when the [Rewarder2.update()](https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/libraries/Rewarder2.sol#L130-L1430) function is called the **rewarder.accDebtPerShare** won't be increased since **oldTotalSupply** is **0**.

```solidity
    function update(
        Parameter storage rewarder,
        bytes32 id,
        uint256 oldBalance,
        uint256 newBalance,
        uint256 totalSupply,
        uint256 totalRewards
    ) internal returns (uint256 rewards) {
        uint256 accDebtPerShare = updateAccDebtPerShare(rewarder, totalSupply, totalRewards);

        rewards = oldBalance == 0 ? 0 : getDebt(accDebtPerShare, oldBalance) - rewarder.debt[id];

        rewarder.debt[id] = getDebt(accDebtPerShare, newBalance);
    }
```
As can be seen from the above code snippet [Rewarder2.update()](https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/libraries/Rewarder2.sol#L130-L1430) function internally calls [Rewarder2.updateAccDebtPerShare()](https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/libraries/Rewarder2.sol#L153-L162) function:
```solidity
    function updateAccDebtPerShare(Parameter storage rewarder, uint256 totalSupply, uint256 totalRewards)
        internal
        returns (uint256)
    {
        uint256 debtPerShare = getDebtPerShare(totalSupply, totalRewards);

        if (block.timestamp > rewarder.lastUpdateTimestamp) rewarder.lastUpdateTimestamp = block.timestamp;

        return debtPerShare == 0 ? rewarder.accDebtPerShare : rewarder.accDebtPerShare += debtPerShare;
    }
```
As can be seen from the above code snippet the [Rewarder2.updateAccDebtPerShare()](https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/libraries/Rewarder2.sol#L153-L162) function calls the [Rewarder2.getDebtPerShare()](https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/libraries/Rewarder2.sol#L37-L39) function internally: 
```solidity
    function getDebtPerShare(uint256 totalDeposit, uint256 totalRewards) internal pure returns (uint256) {
        return totalDeposit == 0 ? 0 : (totalRewards << Constants.ACC_PRECISION_BITS) / totalDeposit;
    }
```
As can be seen from the above code snippets when **totalDeposit** is **0** which is the **oldTotalSupply** parameter supplied to the [Rewarder2.update()](https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/libraries/Rewarder2.sol#L130-L1430) function **rewarder.accDebtPerShare** will be **0** as well. 

Consider the following scenario(the following values are selected for simplicity and in order to better illustrate the issue). We have a voting period of **20_000 seconds** and **10_000** token rewards per  second. The reward token is with 6 decimals thus **200e6** tokens have to be deposited in the rewarder contract by the protocol launching it. Voting period starts at **block timestamp = 1**.  From **1 - 10_001** there are no votes, first vote occurs at **10_001** and the amount is **1e18**, second vote will be for the same amount. 
- First user(UserA) votes at block timestamp **10_001** the [_calculateRewards()](https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/rewarders/BribeRewarder.sol#L300-L313) function will return **10_000 * 10_000 = 100_000_000**. However the above mentioned **oldTotalSupply** will be **0** and thus **rewarder.accDebtPerShare** will be **0** as well, **rewarder.debt** will be **0** as well. 
- Let's say a second user(UserB) votes **5_000 seconds** later at block timestamp **15_001**. Now the [_calculateRewards()](https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/rewarders/BribeRewarder.sol#L300-L313) function will return **10_000 * 5_000 = 50_000_000**, **oldTotalSupply** will be **1e18** and the **rewarder.accDebtPerShare** will be **922337203**,  **rewarder.debt** will be **49999999 ≈ 50e6**.
- Another **5_0001 seconds** pass and the voting period is over, now UserA decides to claim his rewards.  The [_calculateRewards()](https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/rewarders/BribeRewarder.sol#L300-L313) function will return **10_000 * 5_000 = 50_000_000**, **oldTotalSupply** will be **2e18** and the **rewarder.accDebtPerShare** will be **1383505804**. When [Rewarder2.update()](https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/libraries/Rewarder2.sol#L130-L1430) function internally calls the [Rewarder2.getDebt()](https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/libraries/Rewarder2.sol#L27-L29) function it will return **74999999 ≈ 75e6** since the **rewarder.debt** associated with UserA NFT is 0, this will be the amount of tokens sent to UserA. 
- UserB decides to claim his withdraw as well. It doesn't matter if anymore time passes or not as [_calculateRewards()](https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/rewarders/BribeRewarder.sol#L300-L313) function will return 0 once the voting period has ended. **rewarder.accDebtPerShare** is still **1383505804** and the [Rewarder2.getDebt()](https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/libraries/Rewarder2.sol#L27-L29) function it will return **74999999 ≈ 75e6**, however since the **rewarder.debt** associated with UserB NFT is **49999999 ≈ 50e6**, the reward received by the UserB will be **25000000 = 25e6**.

From the **200e6** reward tokens deposited by the protocol only **≈100e6** were distributed to users. In the ``BribeRewarder.sol`` contract there in no option for the protocol that created it to create new incentives for new voting periods once the periods are set the first time, there is no function that allows for funds left in the contract to be withdrawn as well. This means that in the above example **≈100e6** reward tokens will be locked in the contract forever. 

## Vulnerability Detail
[Gist](https://gist.github.com/AtanasDimulski/bc8b548900acaabaf8ccfd373a783679)

After following the steps in the above mentioned [gist](https://gist.github.com/AtanasDimulski/bc8b548900acaabaf8ccfd373a783679) add the following test to the ``AuditorTests.t.sol`` file:

```solidity
    function test_LostRewards() public {
        uint256 lockDuration = 60 * 60 * 24 * 30 * 3; // 3 months with 30 days each
        mintMlumPosition(alice, 1e18, lockDuration);
        mintMlumPosition(bob, 1e18, lockDuration);

        vm.startPrank(tom);
        BribeRewarder rewarder;
        rewarder = BribeRewarder(payable(address(rewarderFactory.createBribeRewarder(bribeRewardToken, pool))));

        ///@notice the reward will be for 1 period of 20_000 seconds, reward per second is 10_000 
        /// 20_000 * 10_000 = 200e6, bribeRewardToken is with 6 decimals
        bribeRewardToken.mint(tom, 200e6);
        bribeRewardToken.approve(address(rewarder), type(uint256).max);
        rewarder.fundAndBribe(1, 1, 200e6);
        vm.stopPrank();

        vm.startPrank(owner);
        ///@notice set period durtation to 20_000 seconds
        voter.updatePeriodDuration(20_000);
        voter.startNewVotingPeriod();
        console2.log("Current block timestamp: ", block.timestamp);
        (uint256 periodStartTime, uint256 periodEndTime) = voter.getPeriodStartEndtime(1);
        console2.log("Period 1 start time: ", periodStartTime);
        console2.log("Period 1 end time: ", periodEndTime);
        vm.stopPrank();

        ///@notice nobody deposits in the first 10_000 seconds
        skip(10_000);
        
        vm.startPrank(alice);
        console2.log("Block timestamp before alice deposit: ", block.timestamp);
        address[] memory pools = new address[](1);
        uint256[] memory deltaAmounts = new uint256[](1);
        pools[0] = pool;
        ///@notice deposits part of their voting power for easier calculations
        deltaAmounts[0] = 1e18;
        voter.vote(1, pools, deltaAmounts);
        vm.stopPrank();

        ///@notice another deposit happens after 5_000 seconds
        skip(5_000);
        vm.startPrank(bob);
        console2.log("Block timestamp before bob deposit: ", block.timestamp);
        voter.vote(2, pools, deltaAmounts);
        vm.stopPrank();

        ///@notice we skip over the voting period in which all rewards should have been distributed
        skip(5001);
        console2.log("Block timestamp after voting period has ended: ", block.timestamp);
        console2.log("Rewarder balance of briber reward token of the rewarder contract before users claim: ", bribeRewardToken.balanceOf(address(rewarder)));

        vm.startPrank(alice);
        console2.log("Alice balance of briber reward token before she claims her rewards: ", bribeRewardToken.balanceOf(alice));
        rewarder.claim(1);
        console2.log("Alice balance of briber reward token after she claims her rewards: ", bribeRewardToken.balanceOf(alice));
        vm.stopPrank();

        vm.startPrank(bob);
        console2.log("Bob balance of briber reward token before he claims her rewards: ", bribeRewardToken.balanceOf(bob));
        rewarder.claim(2);
        console2.log("Bob balance of briber reward token after he claims her rewards: ", bribeRewardToken.balanceOf(bob));
        vm.stopPrank();

        console2.log("Rewarder balance of briber reward token of the rewarder contract after users have claimed: ", bribeRewardToken.balanceOf(address(rewarder)));
    }
```

```solidity
Logs:
  Current block timestamp:  1
  Period 1 start time:  1
  Period 1 end time:  20001
  Block timestamp before alice deposit:  10001
  Total rewards from BribeRewarder.sol:  100000000
  Block timestamp before bob deposit:  15001
  Total rewards from BribeRewarder.sol:  50000000
  Block timestamp after voting period has ended:  20002
  Rewarder balance of briber reward token of the rewarder contract before users claim:  200000000
  Alice balance of briber reward token before she claims her rewards:  0
  Total rewards from BribeRewarder.sol:  50000000
  Alice balance of briber reward token after she claims her rewards:  74999999
  Bob balance of briber reward token before he claims her rewards:  0
  Total rewards from BribeRewarder.sol:  0
  Bob balance of briber reward token after he claims her rewards:  25000000
  Rewarder balance of briber reward token of the rewarder contract after users have claimed:  100000001
```

To run the test use: ``forge test -vvv --mt test_LostRewards``
## Impact
If nobody votes in the beginning of a period rewards up until the time the first person votes will be lost, the funds that should have gone to the first voters for that bribe rewarder will instead accrue to nobody, and be locked in the contract forever. Each pool  that users can vote for, can have up to 5 instances of the ``BribeRewarder.sol`` contract for each voting period, which is set to 2 weeks in the beginning. The chances of users not voting for a specific pool in the exact beginning of each voting period are pretty high. Average block on IOTA EVM at the time of writing this report is 17 seconds. Let's say there are 10 pools with 5 ``BribeRewarder.sol`` contract instances each, and the reward per second is **1e6 USDC**. If only for a single voting period a vote doesn't occur for any of the ``BribeRewarder.sol`` contract instances, the tokens locked forever will be **17 \* 1e6 \* 5 \* 10 = 850e6**. So for a single voting period **850e6** USDC will be locked forever, and this is in case votes don't occur only in the first block. So when the Magicsea protocol gains recognition and there are a lot of pools that try and bribe users for many voting periods the amount of reward tokens locked forever will increase almost exponentially, thus the high severity. 

## Code Snippet
https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/rewarders/BribeRewarder.sol#L260-L298

## Tool used
Manual Review & Foundry

## Recommendation
Consider implementing a function that will allow the owner of the ``BribeRewarder.sol`` contract to collect any tokens, that haven't been distributed during the voting periods it was incentivizing users for.