Elegant Vanilla Crane

High

# User is unable to vote for a pool, blocking rewards claiming

## Summary
A user is unable to use the vote function in the Voter contract, even when they own an NFT of the corresponding pool or are approved by the owner.

## Vulnerability Detail

The internal function `BribeRewards::_modify()` checks whether the caller is the owner of the poolâ€™s NFT, which will never be the case as the entity calling the deposit function must be the Voter contract, as constrained by the `onlyVoter` modifier.

See the following trace:

1. `Voter::vote` (caller is owner)
2. `Voter:notifyBribes`
3. `BribeRewards:deposit` (caller is Voter contract)
4. `BribeRewards:_modify` (checks if the caller is the owner)

To validate this issue, you need to use the actual implementation for testing, rather than a mock version of the Voter contract. The mock version cannot catch this issue because it always returns `true` when calling `ownerOf()`.

### PoC

To run this test, create a new file in the test directory. add it into MlumStaking.t.sol.

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.20;

import "forge-std/Test.sol";

import "openzeppelin-contracts-upgradeable/access/OwnableUpgradeable.sol";
import "openzeppelin/token/ERC721/IERC721.sol";

import "../src/interfaces/IBribeRewarder.sol";
import "../src/interfaces/IMasterChef.sol";
import "../src/rewarders/BribeRewarder.sol";
import "../src/rewarders/BaseRewarder.sol";
import "../src/rewarders/RewarderFactory.sol";
import "../src/Voter.sol";
import "../src/MlumStaking.sol";
import "../src/transparent/TransparentUpgradeableProxy2Step.sol";
import "./mocks/ERC20.sol";

contract VoteProperTest is Test {
    BribeRewarder rewarder;
    RewarderFactory factory;

    ERC20Mock stakingToken;
    ERC20Mock rewardToken;

    address DEV = makeAddr("dev");
    address ALICE = makeAddr("alice");
    address BOB = makeAddr("bob");

    MlumStaking pool;
    Voter voter;

    function setUp() public {
        // Create tokens
        stakingToken = new ERC20Mock("MagicLum", "MLUM", 18);
        rewardToken = new ERC20Mock("USDT", "USDT", 6);

        // Create MlumStaking pool 
        address _poolImpl = address(new MlumStaking(stakingToken, rewardToken));
        pool = MlumStaking(
            address(
                new TransparentUpgradeableProxy2Step(
                    _poolImpl, ProxyAdmin2Step(address(1)), abi.encodeWithSelector(MlumStaking.initialize.selector, DEV)
                )
            )
        );

        address factoryImpl = address(new RewarderFactory());
        factory = RewarderFactory(
            address(
                new TransparentUpgradeableProxy2Step(
                    factoryImpl,
                    ProxyAdmin2Step(address(1)),
                    abi.encodeWithSelector(
                        RewarderFactory.initialize.selector, address(this), new uint8[](0), new address[](0)
                    )
                )
            )
        );

        voter = new Voter(
            IMasterChef(address(0)),
            IMlumStaking(address(pool)),
            IRewarderFactory(address(factory))
        );

        factory.setRewarderImplementation(
            IRewarderFactory.RewarderType.BribeRewarder, IRewarder(address(new BribeRewarder(address(voter))))
        );

        rewarder = BribeRewarder(payable(address(factory.createBribeRewarder(rewardToken, address(pool)))));

        rewardToken.mint(address(this), 20e18);
        rewardToken.approve(address(rewarder), 20e18);
        rewarder.fundAndBribe(1, 2, 10e18);
    }

    function testVuln_VotingNotPossible() public {
        uint256 _amount = 200e18;
        uint256 lockTime  = 60 * 60 * 24 * 30 * 3 + 1; // 3 months + 1 second

        // set rewards
        rewardToken.mint(address(pool), 100_000e6);

        // alice mints new position
        stakingToken.mint(ALICE, _amount);

        vm.startPrank(ALICE);
        stakingToken.approve(address(pool), _amount);
        pool.createPosition(_amount, lockTime);
        vm.stopPrank();

        assertTrue(voter.ownerOf(1, ALICE));

        // start voting period
        vm.prank(address(0));
        voter.startNewVotingPeriod();

        vm.warp(voter.getPeriodStartTime());

        // alice votes for a pool
        address[] memory pools = new address[](1);
        pools[0] = address(pool);

        uint256[] memory amounts = new uint256[](1);
        amounts[0] = pool.getStakingPosition(1).amountWithMultiplier;
       
        // reverts even if voter is approved!
        vm.prank(ALICE);
        pool.approve(address(voter), 1);

        bytes4 reason = IBribeRewarder.BribeRewarder__NotOwner.selector;
 
        vm.prank(ALICE);
        vm.expectRevert(reason);
        voter.vote(1, pools, amounts);
    }
}
```

## Impact

Users will be unable to cast their votes, consequently, they will never be able to claim their rewards. As a result, the rewards will remain stuck in the `BribeRewards` contract indefinitely.

## Code Snippet

https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/rewarders/BribeRewarder.sol#L264-L266
https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/test/mocks/VoterMock.sol#L105

## Tool used

Manual Review, Foundry

## Recommendation

Modify the `BribeRewards::_modify()` function to check the actual owner of the NFT instead of the caller.

Use the actual Voter contract for testing instead of `VoterMock`
