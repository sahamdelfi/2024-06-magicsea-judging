Hollow Paisley Panda

High

# `Voter.vote` Never succeeds if a bribe is set for the period

## Summary

Due to incorrect permission checks, the `Voter` contract cannot deposit votes into `BribeRewarder`, causing the transaction to revert. Consequently, users cannot cast their votes and will lose rewards.

## Vulnerability Detail

When voting using the `Voter.vote` function, if a bribe is set for the period via `BribeRewarder`, the function `Voter._notifyBribes` will be called. This function attempts to deposit votes using `BribeRewarder.deposit`.

The problem arises because the `deposit` function calls `BribeRewarder._modify`, which checks if `msg.sender` is the owner of the `tokenId` used for voting.

Since `deposit` is called by `Voter`, the `msg.sender` will be `Voter`, and the function will revert with `BribeRewarder__NotOwner`, causing the transaction to fail.

```solidity
File: magicsea-staking/src/rewarders/BribeRewarder.sol
  260:     function _modify(uint256 periodId, uint256 tokenId, int256 deltaAmount, bool isPayOutReward)
  261:         private
  262:         returns (uint256 rewardAmount)
  263:     {
  264:         if (!IVoter(_caller).ownerOf(tokenId, msg.sender)) { //audit: sender can be voter if call by deposit function
  265:             revert BribeRewarder__NotOwner();
  266:         }
```

## Impact

If a bribe is set for a given period, users will not be able to vote, so the main Voter function will stop working.
The effect of such behavior will be a complete paralysis of votes and bribes.
high

## Code Snippet

- [`Voter.sol:211`](https://github.com/sherlock-audit/2024-06-magicsea/blob/7fd1a65b76d50f1bf2555c699ef06cde2b646674/magicsea-staking/src/Voter.sol#L211-L211)
- [`Voter.sol:225`](https://github.com/sherlock-audit/2024-06-magicsea/blob/7fd1a65b76d50f1bf2555c699ef06cde2b646674/magicsea-staking/src/Voter.sol#L225-L225)
- [`BribeRewarder.sol:143`](https://github.com/sherlock-audit/2024-06-magicsea/blob/7fd1a65b76d50f1bf2555c699ef06cde2b646674/magicsea-staking/src/rewarders/BribeRewarder.sol#L143-L143)
- [`BribeRewarder.sol:264`](https://github.com/sherlock-audit/2024-06-magicsea/blob/7fd1a65b76d50f1bf2555c699ef06cde2b646674/magicsea-staking/src/rewarders/BribeRewarder.sol#L264-L264)

## Tool used

Manual Review

## Recommendation

Remove the permission check from the private `BribeRewarder._modify` function and move it higher up in the hierarchy to ensure proper ownership verification.

The `_modify` function is used in two places:

- `Voter.vote` -> `_notifyBribes`: Ownership is already checked here.
- `BribeRewarder.claim`: Add an ownership check here.

### Coded POC

```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.0;

import "forge-std/Test.sol";
import "../src/transparent/TransparentUpgradeableProxy2Step.sol";
import {MlumStaking} from "../src/MlumStaking.sol";
import {ERC20Mock} from "./mocks/ERC20.sol";
import "../src/rewarders/RewarderFactory.sol";
import "../src/rewarders/BribeRewarder.sol";
import "../src/Voter.sol";
import {MasterChefMock} from "./mocks/MasterChefMock.sol";

contract AuditTests is Test {

    address public admin = makeAddr("proxyAdmin");
    address public DEV = makeAddr("DEV");
    address public ALICE = makeAddr("ALICE");
    address public BOB = makeAddr("BOB");

    MlumStaking public stakingPool;
    RewarderFactory public rewarderFactory;
    BribeRewarder public bribeRewarder;
    Voter public voter;

    ERC20Mock public stakingToken;
    ERC20Mock public rewardToken;

    function setUp() public {
        vm.startPrank(DEV);
        stakingToken = new ERC20Mock("MagicLum", "MLUM", 18);
        rewardToken = new ERC20Mock("USDT", "USDT", 6);

        ProxyAdmin2Step proxyAdmin = new ProxyAdmin2Step(admin);

        stakingPool = MlumStaking(
            address(
                new TransparentUpgradeableProxy2Step(
                    address(new MlumStaking(stakingToken, rewardToken)),
                    proxyAdmin,
                    abi.encodeWithSelector(MlumStaking.initialize.selector, DEV)
                )
            )
        );

        rewarderFactory = RewarderFactory(
            address(
                new TransparentUpgradeableProxy2Step(
                    address(new RewarderFactory()),
                    proxyAdmin,
                    abi.encodeWithSelector(RewarderFactory.initialize.selector, DEV, new uint8[](0), new address[](0))
                )
            )
        );

        voter = Voter(
            address(
                new TransparentUpgradeableProxy2Step(
                    address(new Voter(new MasterChefMock(), stakingPool, rewarderFactory)),
                    proxyAdmin,
                    abi.encodeWithSelector(MlumStaking.initialize.selector, DEV)
                )
            )
        );

        rewarderFactory.setRewarderImplementation(
            IRewarderFactory.RewarderType.BribeRewarder, IRewarder(address(new BribeRewarder(address(voter))))
        );
        bribeRewarder = BribeRewarder(payable(address(rewarderFactory.createBribeRewarder(rewardToken, address(stakingPool)))));

    }

    function testVoteAlwaysRevert() public {
        vm.startPrank(DEV);
        rewardToken.mint(DEV, 10e18);
        rewardToken.approve(address(bribeRewarder), 10e18);

        bribeRewarder.fundAndBribe(1, 1, 1e18);

        voter.startNewVotingPeriod();
        assertEq(1, voter.getCurrentVotingPeriod());

        stakingToken.mint(ALICE, 10e18);
        vm.startPrank(ALICE);
        stakingToken.approve(address(stakingPool), 1e18);
        stakingPool.createPosition(1e18, 365 days);

        // check lockduration
        MlumStaking.StakingPosition memory position = stakingPool.getStakingPosition(1);
        assertEq(position.lockDuration, 365 days);

        address[] memory pools = new address[](1);
        pools[0] = address(stakingPool);
        uint256[] memory deltaAmounts = new uint256[](1);
        deltaAmounts[0] = 1e18;

        vm.expectRevert(IBribeRewarder.BribeRewarder__NotOwner.selector);
        voter.vote(1, pools, deltaAmounts);
    }
}
```