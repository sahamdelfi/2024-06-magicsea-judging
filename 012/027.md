Damp Basil Wolverine

High

# All claims/usual withdrawals from MasterChef would be blocked if contract won't have any LUM tokens

## Summary
Users won't be able to claim their rewards and withdraw their funds partially if MasterChef's property mintLum would be false and MasterChef won't have any LUM tokens in it.

## Vulnerability Detail
In each claim/withdraw call MasterChef calls function `_mintLum(uint256 amount)` which does the following:
if mintLum is true - it will mint new LUM tokens to the MasterChef and Treasury contracts, but if it's false it will just transfer funds to Treasury contract. But the problem is that there's no check that MasterChef has enough funds to perform this transfer so when MasterChef won't have needed LUM tokens to transfer - claim/withdraw will fail.
 
## Impact

Users won't be able to claim their LUM rewards and withdraw their funds paritally, because only way to withdraw would be to call `emergencyWithdraw`.

## Code Snippet

https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/MasterchefV2.sol#L589-L592

## PoC

Update 2 functions on VoterMock.sol:
`getTotalWeight`:
```solidity
    function getTotalWeight() external pure returns (uint256) {
        return 100;
    }
```
`getWeight`:
```solidity
 function getWeight(uint256 pid) external pure returns (uint256) {
        {
            pid;
        }
        return 100;
    }
```
And then create new t.sol file in test/ folder:

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.20;

import "forge-std/Test.sol";

import "openzeppelin-contracts-upgradeable/access/OwnableUpgradeable.sol";

import "../src/interfaces/IMasterChefRewarder.sol";
import "../src/interfaces/IVoter.sol";
import "../src/interfaces/IMasterChef.sol";
import "../src/interfaces/ILum.sol";
import "../src/rewarders/MasterChefRewarder.sol";
import "../src/MasterChefV2.sol";
import "../src/rewarders/BaseRewarder.sol";
import "../src/rewarders/RewarderFactory.sol";
import "../src/transparent/TransparentUpgradeableProxy2Step.sol";
import "./mocks/ERC20.sol";
import "./mocks/VoterMock.sol";
import {MlumStaking} from "../src/MlumStaking.sol";

contract MasterChefTest is Test {
    address payable immutable ADMIN = payable(makeAddr("admin"));
    address payable immutable DEPOSITOR = payable(makeAddr("depositor"));
    address payable immutable DEPOSITOR2 = payable(makeAddr("depositor2"));
    address payable immutable ALICE = payable(makeAddr("alice"));
    address payable immutable BOB = payable(makeAddr("bob"));
    address extraRewarder = makeAddr("extraRewarder");
    MlumStaking private _pool;
    ERC20Mock lum;
    IERC20 rewardToken;
    IERC20 stakingToken;
    MasterChef masterChef;
    IVoter voter;
    MasterChefRewarder rewarder;
    RewarderFactory factory;
    function setUp() public {
        vm.startPrank(ADMIN);
        lum = new ERC20Mock("LUM", "LUM", 18);
        stakingToken = new ERC20Mock("Staking Token", "ST", 18);
        rewardToken = new ERC20Mock("Reward Token", "RT", 6);
        voter = new VoterMock();
        address poolImpl = address(new MlumStaking(stakingToken, rewardToken));

        _pool = MlumStaking(
            address(
                new TransparentUpgradeableProxy2Step(
                    poolImpl,
                    ProxyAdmin2Step(address(1)),
                    abi.encodeWithSelector(
                        MlumStaking.initialize.selector,
                        ADMIN
                    )
                )
            )
        );
        address factoryImpl = address(new RewarderFactory());

        factory = RewarderFactory(
            address(
                new TransparentUpgradeableProxy2Step(
                    factoryImpl,
                    ProxyAdmin2Step(address(1)),
                    abi.encodeWithSelector(
                        RewarderFactory.initialize.selector,
                        ADMIN,
                        new uint8[](0),
                        new address[](0)
                    )
                )
            )
        );

        rewarder = MasterChefRewarder(payable(ADMIN));
        factory.setRewarderImplementation(
            IRewarderFactory.RewarderType.MasterChefRewarder,
            IRewarder(address(rewarder))
        );

        address masterChefImpl = address(
            new MasterChef(
                ILum(address(lum)),
                voter,
                factory,
                address(0),
                0.02e18
            )
        );
        masterChef = MasterChef(
            address(
                new TransparentUpgradeableProxy2Step(
                    masterChefImpl,
                    ProxyAdmin2Step(address(1)),
                    abi.encodeWithSelector(
                        MasterChef.initialize.selector,
                        ADMIN,
                        address(1)
                    )
                )
            )
        );
        masterChef.setVoter(voter);
        masterChef.setLumPerSecond(10);
        masterChef.updateOperator(ADMIN);
        masterChef.add(rewardToken, IMasterChefRewarder(address(0)));
        vm.stopPrank();
    }

    function test_withdrawBug() public {
        IERC20 _lum = IERC20(address(masterChef.getLum()));
        deal(address(rewardToken), address(DEPOSITOR), 100 ether);
        vm.startPrank(DEPOSITOR);
        rewardToken.approve(address(masterChef), 1 ether);
        masterChef.deposit(0, 1e17);
        vm.warp(block.timestamp + 10 days);
        vm.expectRevert();
        masterChef.withdraw(0, 1e17); // BUG HERE we can't withdraw our funds because MasterChef doesn't have any LUM to transfer
        vm.expectRevert();
        uint256[] memory pids = new uint[](1);
        pids[0] = 0;
        masterChef.claim(pids); // BUG HERE we can't claim our rewards because MasterChef doesn't have any LUM to transfer
        vm.stopPrank();
    }
}
```

## Tool used

Foundry.

## Recommendation

Add LUM balance validation to the contract to ensure that claim won't fail when LUM's amount is not enought.