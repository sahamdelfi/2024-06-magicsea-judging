Generous Cinnamon Lizard

High

# `MasterChefRewarder.unlink` function does not execute `_setRewardParameters(0, block.timestamp, 0)` though it should

## Summary
Because the `MasterChefRewarder.unlink` function does not execute `_setRewardParameters(0, block.timestamp, 0)`, `_totalUnclaimedRewards` and `rewarder.accDebtPerShare` become less than what they should be at the moment when the `MasterChefRewarder` contract is stopped, and `_rewardsPerSecond` is not updated to 0, which allows accounts to still earn and claim rewards after the `MasterChefRewarder` contract is stopped even though no new rewards should be given after such stop.

## Vulnerability Detail
Since calling the `MasterChefRewarder.stop` function reverts, the `MasterChefRewarder` contract can only be stopped by calling the `MasterChefRewarder.unlink` function according to the `MasterChefRewarder.stop` function's comment. Unlike the `BaseRewarder.stop` function, the `MasterChefRewarder.unlink` function does not execute `_setRewardParameters(0, block.timestamp, 0)`; thus, when the `MasterChefRewarder.unlink` function is called:
- `_totalUnclaimedRewards` is not incremented by the pending rewards, which is returned by the `_rewarder.getTotalRewards` function, at the moment when the `MasterChefRewarder` contract is stopped though it should be;
- since the `_rewarder.updateAccDebtPerShare` function is not called, `rewarder.accDebtPerShare` is not incremented by the `debtPerShare` corresponding to the total supply and pending rewards at the moment when the `MasterChefRewarder` contract is stopped though it should be;
- and `_rewardsPerSecond` is not updated to 0 though it should be when the `MasterChefRewarder` contract is stopped.

## Impact
The accounting for `_totalUnclaimedRewards` and `rewarder.accDebtPerShare` become inaccurate in which these state variables become less than what they should be at the moment when the `MasterChefRewarder` contract is stopped. Moreover, because `_rewardsPerSecond` is not updated to 0, accounts can still earn and claim rewards after the `MasterChefRewarder` contract is stopped even though no new rewards should be given after such stop, which causes these accounts to receive such rewards that they are not entitled to.

## Code Snippet

https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/rewarders/MasterChefRewarder.sol#L51-L56
```solidity
    /**
     * @dev Reverts as the MasterChefRewarder contract should be stopped by the unlink function.
     */
    function stop() public pure override(IBaseRewarder, BaseRewarder) {
        revert MasterChefRewarder__UseUnlink();
    }
```

https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/rewarders/MasterChefRewarder.sol#L42-L49
```solidity
    function unlink(uint256 pid) public override {
        if (msg.sender != _caller) revert BaseRewarder__InvalidCaller();
        if (pid != _pid()) revert BaseRewarder__InvalidPid(pid);
        if (_status != Status.Linked) revert MasterChefRewarder__NotLinked();

        _status = Status.Stopped;
        _isStopped = true;
    }
```

https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/rewarders/BaseRewarder.sol#L184-L198
```solidity
    function stop() public virtual override onlyOwner {
        if (_isStopped) revert BaseRewarder__AlreadyStopped();

        uint256 totalSupply = _getTotalSupply();
        uint256 totalPendingRewards = _rewarder.getTotalRewards(_rewardsPerSecond, _endTimestamp, totalSupply);

        _totalUnclaimedRewards += totalPendingRewards;
        _rewarder.updateAccDebtPerShare(totalSupply, totalPendingRewards);

        _setRewardParameters(0, block.timestamp, 0);

        _isStopped = true;
        ...
    }
```

https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/rewarders/BaseRewarder.sol#L340-L376
```solidity
    function _setRewardParameters(uint256 maxRewardPerSecond, uint256 startTimestamp, uint256 expectedDuration)
        internal
        virtual
        returns (uint256 rewardPerSecond)
    {
        if (startTimestamp < block.timestamp) revert BaseRewarder__InvalidStartTimestamp(startTimestamp);
        if (_isStopped) revert BaseRewarder__Stopped();
        if (expectedDuration == 0 && maxRewardPerSecond != 0) revert BaseRewarder__InvalidDuration();

        uint256 totalUnclaimedRewards = _totalUnclaimedRewards;
        uint256 totalSupply = _getTotalSupply();

        uint256 totalRewards = _rewarder.getTotalRewards(_rewardsPerSecond, _endTimestamp, totalSupply);

        totalUnclaimedRewards += totalRewards;

        uint256 remainingReward = _balanceOfThis(_token()) - totalUnclaimedRewards;
        uint256 maxExpectedReward = maxRewardPerSecond * expectedDuration;

        rewardPerSecond = maxExpectedReward > remainingReward ? remainingReward / expectedDuration : maxRewardPerSecond;
        uint256 expectedReward = rewardPerSecond * expectedDuration;

        if (expectedDuration != 0 && expectedReward == 0) revert BaseRewarder__ZeroReward();

        uint256 endTimestamp = startTimestamp + expectedDuration;

        _rewardsPerSecond = rewardPerSecond;
        _reserve = totalUnclaimedRewards + expectedReward;

        _endTimestamp = endTimestamp;
        _totalUnclaimedRewards = totalUnclaimedRewards;

        _rewarder.updateAccDebtPerShare(totalSupply, totalRewards);
        _rewarder.lastUpdateTimestamp = startTimestamp;
        ...
    }
```

https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/libraries/Rewarder.sol#L51-L63
```solidity
    function getTotalRewards(
        Parameter storage rewarder,
        uint256 rewardPerSecond,
        uint256 endTimestamp,
        uint256 totalSupply
    ) internal view returns (uint256) {
        if (totalSupply == 0) return 0;

        uint256 lastUpdateTimestamp = rewarder.lastUpdateTimestamp;
        uint256 timestamp = block.timestamp > endTimestamp ? endTimestamp : block.timestamp;

        return timestamp > lastUpdateTimestamp ? (timestamp - lastUpdateTimestamp) * rewardPerSecond : 0;
    }
```

https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/libraries/Rewarder.sol#L153-L162
```solidity
    function updateAccDebtPerShare(Parameter storage rewarder, uint256 totalSupply, uint256 totalRewards)
        internal
        returns (uint256)
    {
        uint256 debtPerShare = getDebtPerShare(totalSupply, totalRewards);

        if (block.timestamp > rewarder.lastUpdateTimestamp) rewarder.lastUpdateTimestamp = block.timestamp;

        return debtPerShare == 0 ? rewarder.accDebtPerShare : rewarder.accDebtPerShare += debtPerShare;
    }
```

## Tool used
Manual Review

## Recommendation
The `MasterChefRewarder.unlink` function can be updated to execute `_setRewardParameters(0, block.timestamp, 0)` like what the `BaseRewarder.stop` function does.