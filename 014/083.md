Proper Sand Loris

Medium

# Duplicate LP Token Could Lead to Incorrect Reward Distribution

## Summary

The [add function](https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/MasterchefV2.sol#L367) in the MasterChefV2 contract does not check for duplicate LP tokens. This allows the same LP token to be added to multiple pools, potentially leading to incorrect reward calculations due to incorrect totalSupply values.

## Vulnerability Detail

The add function does not verify whether the token being added already exists in another pool. If the same LP token is added to multiple pools, the totalSupply calculation used for reward distribution can be incorrect, leading to unfair reward distribution.

## Impact

Adding the same LP token to multiple pools can lead to incorrect reward calculations, resulting in unfair distribution of rewards to users. Users may receive more or fewer rewards than they are entitled to, causing dissatisfaction and potential financial loss.

Case Study - 
1. The add function does not check whether the token being added already exists in another pool.
2. This lack of check means that the same LP token can be added to multiple pools.
3. In the _modify function, the totalSupply is obtained from `farm.amounts.getTotalAmount()`.
4. If the same LP token is used in multiple pools, the `totalSupply` calculation could be incorrect, as the same token balance would be accounted for multiple times.
5. This incorrect `totalSupply` can lead to incorrect reward calculations in `_getRewardForPid` and other functions relying on totalSupply.
6. The token.balanceOf(address(this)) check does not prevent the same token from being added to multiple pools. It only verifies the balance of the token held by the contract.

## Code Snippet

https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/MasterchefV2.sol#L367

```solidity
/**
     * @dev Adds a farm.
     * @param token The token of the farm.
     * @param extraRewarder The extra rewarder of the farm.
     */
function add(IERC20 token, IMasterChefRewarder extraRewarder) external override {
    if (msg.sender != address(_lbHooksManager)) _checkOwnerOrOperator();

    uint256 pid = _farms.length;

    Farm storage farm = _farms.push();

    farm.token = token;
    farm.rewarder.lastUpdateTimestamp = block.timestamp;

    if (address(extraRewarder) != address(0)) _setExtraRewarder(pid, extraRewarder);

    token.balanceOf(address(this)); // sanity check

    emit FarmAdded(pid, token);
}
```
https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/MasterchefV2.sol#L539
```solidity
function _modify(uint256 pid, address account, int256 deltaAmount, bool isPayOutReward) private {
    Farm storage farm = _farms[pid];
    Rewarder.Parameter storage rewarder = farm.rewarder;
    IMasterChefRewarder extraRewarder = farm.extraRewarder;

    (uint256 oldBalance, uint256 newBalance, uint256 oldTotalSupply,) = farm.amounts.update(account, deltaAmount);

    uint256 totalLumRewardForPid = _getRewardForPid(rewarder, pid, oldTotalSupply);
    uint256 lumRewardForPid = _mintLum(totalLumRewardForPid);

    uint256 lumReward = rewarder.update(account, oldBalance, newBalance, oldTotalSupply, lumRewardForPid);

    if (isPayOutReward) {
        lumReward = lumReward + unclaimedRewards[pid][account];
        unclaimedRewards[pid][account] = 0;
        if (lumReward > 0) _lum.safeTransfer(account, lumReward);
    } else {
        unclaimedRewards[pid][account] += lumReward;
    }

    if (address(extraRewarder) != address(0)) {
        extraRewarder.onModify(account, pid, oldBalance, newBalance, oldTotalSupply);
    }

    emit PositionModified(pid, account, deltaAmount, lumReward);
}
```

```solidity
function _getRewardForPid(Rewarder.Parameter storage rewarder, uint256 pid, uint256 totalSupply) private view returns (uint256) {
    return _getRewardForPid(pid, rewarder.getTotalRewards(_lumPerSecond, totalSupply), _voter.getTotalWeight());
}

function _getRewardForPid(uint256 pid, uint256 totalRewards, uint256 totalWeight) private view returns (uint256) {
    return totalWeight == 0 ? 0 : totalRewards * _voter.getWeight(pid) / totalWeight;
}
```

## Tool used

Manual Review

## Recommendation

Implement a check in the add function to prevent the same LP token from being added to multiple pools.
1. Add Global Variable to Track Tokens
```solidity
mapping(address => bool) private _addedTokens;
```

2. Update Add Function
```solidity
/**
     * @dev Adds a farm.
     * @param token The token of the farm.
     * @param extraRewarder The extra rewarder of the farm.
     */
function add(IERC20 token, IMasterChefRewarder extraRewarder) external override {
    if (msg.sender != address(_lbHooksManager)) _checkOwnerOrOperator();

    require(!_addedTokens[address(token)], "Token already added");

    uint256 pid = _farms.length;

    Farm storage farm = _farms.push();

    farm.token = token;
    farm.rewarder.lastUpdateTimestamp = block.timestamp;

    if (address(extraRewarder) != address(0)) _setExtraRewarder(pid, extraRewarder);

    token.balanceOf(address(this)); // sanity check

    _addedTokens[address(token)] = true;

    emit FarmAdded(pid, token);
}
```

