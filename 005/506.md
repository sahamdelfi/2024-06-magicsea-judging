Soft Mint Lizard

Medium

# MasterchefV2::setLumPerSecond should update all the pools

## Summary

`_lumPerSecond` is used to calculate the reward emissions distributed in the `MasterchefV2` and in case it is being updated, all the farms that are currently distributing rewards should accrue the rewards for the last time period, which is not being done in `setLumPerSecond`.

## Vulnerability Detail

If we take a look at the `MasterchefV2::_updateAll` we can see that the time since the last update is taken and based on `_lumPerSecond`, new `lum` tokens are being minted to satisfy the users who have been deposited for their favorite pools. The problem is when  `_lumPerSecond` is being updated, there is no `updateAll` called for all the farms and their rewards until that period will be calculated based on the new `_lumPerSecond`, which is wrong. In case it is being increased, that will create a stepwise jump in all top farm rewards and users will receive an unfair amount of lum tokens for the previous period. Or the other scenario when it is decreased, less lum will be distributed for the previous period.

```solidity
  function setLumPerSecond(uint96 lumPerSecond) external override onlyOwner {
    if (lumPerSecond > Constants.MAX_LUM_PER_SECOND) revert MasterChef__InvalidLumPerSecond();

    // _updateAll(_voter.getTopPoolIds()); // todo remove this

    _lumPerSecond = lumPerSecond;

    emit LumPerSecondSet(lumPerSecond);
}
```

There is another impact caused by not updating all top pools `rewarder::lastUpdateTimestamp` before updating the lum per second, and that is the unfair lum distribution amongst the farms. Imagine the scenario of farm A and farm B, B is more active and hasn’t been updated for 1 hour, while A hasn’t been updated for a whole day, when `setLumPerSecond` with bigger value, farm A will receive more rewards just because it is the less active farm and his `block.timestamp - lastUpdateTimestamp` is 1 day:

```solidity
 function getTotalRewards(
        Parameter storage rewarder,
        uint256 rewardPerSecond,
        uint256 endTimestamp,
        uint256 totalSupply
    ) internal view returns (uint256) {
        if (totalSupply == 0) return 0;

        uint256 lastUpdateTimestamp = rewarder.lastUpdateTimestamp;
        uint256 timestamp = block.timestamp > endTimestamp ? endTimestamp : block.timestamp;

        return timestamp > lastUpdateTimestamp ? (timestamp - lastUpdateTimestamp) * rewardPerSecond : 0;
    }
```

```solidity
  function _updateAll(uint256[] memory pids) private {
      uint256 length = pids.length;

      uint256 totalWeight = _voter.getTotalWeight();
      uint256 lumPerSecond = _lumPerSecond;

      for (uint256 i; i < length; ++i) {
          uint256 pid = pids[i];

          Farm storage farm = _farms[pid];
          Rewarder.Parameter storage rewarder = farm.rewarder;

          uint256 totalSupply = farm.amounts.getTotalAmount();
          uint256 totalRewards = rewarder.getTotalRewards(lumPerSecond, totalSupply);

          uint256 totalLumRewardForPid = _getRewardForPid(pid, totalRewards, totalWeight);
          uint256 lumRewardForPid = _mintLum(totalLumRewardForPid);

          rewarder.updateAccDebtPerShare(totalSupply, lumRewardForPid);
      }
  }
```

## Impact

updated `_lumPerSecond` will be applied starting from the time period **before**, instead of after, which will unfairly change the user rewards for the old period. 

## Code Snippet

https://github.com/sherlock-audit/2024-06-magicsea/blob/main/magicsea-staking/src/MasterchefV2.sol#L352-L360

## Tool used

Manual Review

## Recommendation

Uncomment the `_updateAll` call.