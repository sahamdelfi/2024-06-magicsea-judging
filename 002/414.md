Zealous Olive Yeti

High

# Protocol is incompatible with FOT tokens in multiple instances


## Summary

See _Title_.

## Vulnerability Detail

As hinted from the title protocol is not compatible with tokens that charge fee on transfers, this is quite a popular bug case so no need to waste too much time on going into details.

In short, all instances where there is a need for transfers of some sort, the protocol currently _erroneously_ assumes that the `amount` specified in the transfer is actually what's been sent to/fro, however this is inaccurate, which would then mean that all instances where transfer/safetramsfer(from) is used is affected, as they would always be an inflation of the amount received/sent.

For example take a look at https://github.com/sherlock-audit/2024-06-magicsea/blob/42e799446595c542eff9519353d3becc50cdba63/magicsea-staking/src/rewarders/BribeRewarder.sol#L259-L298

```solidity

    function _modify(uint256 periodId, uint256 tokenId, int256 deltaAmount, bool isPayOutReward)
        private
        returns (uint256 rewardAmount)
    {
        if (!IVoter(_caller).ownerOf(tokenId, msg.sender)) {
            revert BribeRewarder__NotOwner();
        }

        // extra check so we dont calc rewards before starttime
        (uint256 startTime,) = IVoter(_caller).getPeriodStartEndtime(periodId);
        if (block.timestamp <= startTime) {
            _lastUpdateTimestamp = startTime;
        }

        RewardPerPeriod storage reward = _rewards[_indexByPeriodId(periodId)];
        Amounts.Parameter storage amounts = reward.userVotes;
        Rewarder2.Parameter storage rewarder = reward.rewarder;

        (uint256 oldBalance, uint256 newBalance, uint256 oldTotalSupply,) = amounts.update(tokenId, deltaAmount);

        uint256 totalRewards = _calculateRewards(periodId);

        rewardAmount = rewarder.update(bytes32(tokenId), oldBalance, newBalance, oldTotalSupply, totalRewards);

        if (block.timestamp > _lastUpdateTimestamp) {
            _lastUpdateTimestamp = block.timestamp;
        }

        if (isPayOutReward) {
            rewardAmount = rewardAmount + unclaimedRewards[periodId][tokenId];
            unclaimedRewards[periodId][tokenId] = 0;
            if (rewardAmount > 0) {
                IERC20 token = _token();
                _safeTransferTo(token, msg.sender, rewardAmount);
            }
        } else {
            unclaimedRewards[periodId][tokenId] += rewardAmount;
        }
    }
```

We can see that transferring the tokens, is dependent on the `isPayOutReward` bool, so if there is a transfer to be done, the ` unclaimedRewards[periodId][tokenId] += rewardAmount` never gets triggered, however if there is no transfer to be made ` unclaimedRewards[periodId][tokenId] += rewardAmount` is updated which would then mean that in the future, when `modify()` is called and there is a need to do the end transfer, this attempt would revert for the last set of users cause, it's been inflated, and previous transfers have diluted the protocol's reserve.

Would be key to note that there is no slippage logic, so a user can't even specify that they would like a bit less, since if `isPayOutReward` bool is _true_ all previous unclaimed rewards would be attached to the now queried `rewardAmount` and hardcoded to be transferred, CC @audit tag, causing an inability for users to access their tokens, and earlier users effectively stealing tokens from later ones.

## Impact

Protocol is incompatible with FOT tokens in multiple instances which would also go on to break the accounting in multiple instances, one of the potential impact/window was also hinted in the Vulnerability Details_, where depositing would have tokens be heavily inflated.

Notw that a similar logic is also applicable to [these instances in the MasterChefV2 contract](https://github.com/sherlock-audit/2024-06-magicsea/blob/42e799446595c542eff9519353d3be0cc50cdba63/magicsea-staking/src/MasterchefV2.sol#L284-L320)

```solidity
    function depositOnBehalf(uint256 pid, uint256 amount, address to) external override onlyTrusted {
        _modify(pid, to, amount.toInt256(), false);

        if (amount > 0) _farms[pid].token.safeTransferFrom(msg.sender, address(this), amount);
    }

    function deposit(uint256 pid, uint256 amount) external override {
        _modify(pid, msg.sender, amount.toInt256(), false);

        if (amount > 0) _farms[pid].token.safeTransferFrom(msg.sender, address(this), amount);
    }


    function withdraw(uint256 pid, uint256 amount) external override {
        _modify(pid, msg.sender, -amount.toInt256(), true);

        if (amount > 0) _farms[pid].token.safeTransfer(msg.sender, amount);
    }

    function claim(uint256[] calldata pids) external override {
        for (uint256 i; i < pids.length; ++i) {
            _modify(pids[i], msg.sender, 0, true);
        }
    }
```

This is because all the function above make a call to the MasterChefV2's native `_modify()` that also expects the `amount` attached in the transfer to be the amount sent.

See https://github.com/sherlock-audit/2024-06-magicsea/blob/42e799446595c542eff9519353d3becc50cdba63/magicsea-staking/src/MasterchefV2.sol#L538-L564

```solidity

    function _modify(uint256 pid, address account, int256 deltaAmount, bool isPayOutReward) private {
        Farm storage farm = _farms[pid];
        Rewarder.Parameter storage rewarder = farm.rewarder;
        IMasterChefRewarder extraRewarder = farm.extraRewarder;

        (uint256 oldBalance, uint256 newBalance, uint256 oldTotalSupply,) = farm.amounts.update(account, deltaAmount);

        uint256 totalLumRewardForPid = _getRewardForPid(rewarder, pid, oldTotalSupply);
        uint256 lumRewardForPid = _mintLum(totalLumRewardForPid);

        uint256 lumReward = rewarder.update(account, oldBalance, newBalance, oldTotalSupply, lumRewardForPid);

        if (isPayOutReward) {
            lumReward = lumReward + unclaimedRewards[pid][account];
            unclaimedRewards[pid][account] = 0;
            if (lumReward > 0) _lum.safeTransfer(account, lumReward);
        } else {
            unclaimedRewards[pid][account] += lumReward;
        }

        if (address(extraRewarder) != address(0)) {
            extraRewarder.onModify(account, pid, oldBalance, newBalance, oldTotalSupply);
        }

        emit PositionModified(pid, account, deltaAmount, lumReward);
    }
```

## Code Snippet

https://github.com/sherlock-audit/2024-06-magicsea/blob/42e799446595c542eff9519353d3becc50cdba63/magicsea-staking/src/rewarders/BribeRewarder.sol#L259-L298

## Tool used

Manual Review

## Recommendation

Do not support these tokens, or introduce a functionality to check the balance before/after the transfwe to ensure the rtight amount of tokens are transferred.
