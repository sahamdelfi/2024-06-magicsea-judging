Young Iron Beaver

Medium

# Potential Economic Losses When Using Rebase Tokens as StakedToken in the MlumStaking contract

## Summary
When a rebase token is used as stakedToken, users can only withdraw the initially staked amount, resulting in potential losses as any additional generated tokens remain locked in the contract.

## Vulnerability Detail

**Based on the README: Any type of ERC20 token is within consideration.**

```md
Any type of ERC20 token. Pools are permissionless. So users can open pools even with weird tokens. Issues regarding any weird token will be valid if they have Med/High impact.
```

However, when `stakedToken` is a rebase token, users may suffer potential economic losses.

```solidity
function createPosition(uint256 amount, uint256 lockDuration) external override nonReentrant {
        // no new lock can be set if the pool has been unlocked
        if (isUnlocked()) {
            require(lockDuration == 0, "locks disabled");
        }

        _updatePool();

        // handle tokens with transfer tax
        amount = _transferSupportingFeeOnTransfer(stakedToken, msg.sender, amount);
        require(amount != 0, "zero amount"); // createPosition: amount cannot be null

        // mint NFT position token
        uint256 currentTokenId = _mintNextTokenId(msg.sender);

        // calculate bonuses
        uint256 lockMultiplier = getMultiplierByLockDuration(lockDuration);
        uint256 amountWithMultiplier = amount * (lockMultiplier + 1e4) / 1e4;
        // uint256 amountWithMultiplier = amount + (amount*lockMultiplier / 1e4)/1e18;

        // create position
        _stakingPositions[currentTokenId] = StakingPosition({
            initialLockDuration: lockDuration,
            amount: amount,
            rewardDebt: amountWithMultiplier * (_accRewardsPerShare) / (PRECISION_FACTOR),
            lockDuration: lockDuration,
            startLockTime: _currentBlockTimestamp(),
            lockMultiplier: lockMultiplier,
            amountWithMultiplier: amountWithMultiplier,
            totalMultiplier: lockMultiplier
        });

        // update total lp supply
        _stakedSupply = _stakedSupply + amount;
        _stakedSupplyWithMultiplier = _stakedSupplyWithMultiplier + amountWithMultiplier;

        emit CreatePosition(currentTokenId, amount, lockDuration);
    }
```

Taking the classic stETH as an example of `stakedToken`, normally, the user's stETH token balance will increase as consensus rewards are distributed. Suppose a user, Alice, has 10e stETH at time T and calls the above `createPosition` function to stake all her stETH for 365 days. The `position.amount` will store 10e stETH.

```solidity
    function withdrawFromPosition(uint256 tokenId, uint256 amountToWithdraw) external nonReentrant {
        _requireOnlyApprovedOrOwnerOf(tokenId);

        _updatePool();
        address nftOwner = ERC721Upgradeable.ownerOf(tokenId);
        _withdrawFromPosition(nftOwner, tokenId, amountToWithdraw);
    }

    function _withdrawFromPosition(address nftOwner, uint256 tokenId, uint256 amountToWithdraw) internal {
        require(amountToWithdraw > 0, "null");
        // withdrawFromPosition: amount cannot be null

        StakingPosition storage position = _stakingPositions[tokenId];
        require(
            _unlockOperators.contains(nftOwner)
                || (position.startLockTime + position.lockDuration) <= _currentBlockTimestamp() || isUnlocked(),
            "locked"
        );
        // withdrawFromPosition: invalid amount
        require(position.amount >= amountToWithdraw, "invalid");

        _harvestPosition(tokenId, nftOwner);

        // update position
        position.amount = position.amount - amountToWithdraw;

        // update total lp supply
        _stakedSupply = _stakedSupply - amountToWithdraw;

        if (position.amount == 0) {
            // destroy if now empty
            _stakedSupplyWithMultiplier = _stakedSupplyWithMultiplier - position.amountWithMultiplier;
            _destroyPosition(tokenId);
        } else {
            _updateBoostMultiplierInfoAndRewardDebt(position);
        }

        emit WithdrawFromPosition(tokenId, amountToWithdraw);
        stakedToken.safeTransfer(nftOwner, amountToWithdraw);
    }
```

A year later, Alice wants to withdraw the stETH from her position by calling the above `withdrawFromPosition` function. Due to the presence of `require(position.amount >= amountToWithdraw, "invalid");`, Alice can only pass in 10e stETH as the amount, resulting in the withdrawal of only 10e stETH. However, since stETH is a rebase token that accrues consensus rewards, assuming an annual yield of 3%, the original 10e stETH would have become 10.3e stETH by then. The remaining 0.3e stETH will be permanently locked in the MlumStaking contract and cannot be withdrawn, causing Alice to suffer a financial loss.

## Impact
The impact is that users staking rebase tokens, such as stETH, will only be able to withdraw their initially staked amount, resulting in the loss of any additional tokens gained through rebasing. This can lead to significant economic losses, as the extra tokens generated by the rebase process remain locked in the contract and are inaccessible to the user.

## Code Snippet
https://github.com/sherlock-audit/2024-06-magicsea/blob/42e799446595c542eff9519353d3becc50cdba63/magicsea-staking/src/MlumStaking.sol#L354-L390

https://github.com/sherlock-audit/2024-06-magicsea/blob/42e799446595c542eff9519353d3becc50cdba63/magicsea-staking/src/MlumStaking.sol#L619-L646

## Tool used

Manual Review

## Recommendation

Given the README statement that "Pools are permissionless. So users can open pools even with weird tokens," if users are aware of this issue, they can avoid using rebase tokens. A more complex solution would involve redesigning the mechanism and code implementation for creating and destroying positions specifically for rebase tokens. For example, in the case of stETH, storing a constant `share` rather than a dynamically changing `amount (balance)` could be a feasible solution.